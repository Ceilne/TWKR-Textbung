@자유행동처리(ARG)
;자유행동조건を満たしていない場合は발생せず、중단される
IF !자유행동조건(ARG)
	SIF Activity_Duration:ARG
		CALL 자유행동내용(ARG, Activity_Type:ARG, "중단")
	Activity_Duration:ARG = 0
	Activity_Type:ARG = 0
	RETURN
ENDIF
;自由행동중の処理
IF Activity_Duration:ARG
	;経過時間分減っていく
	Activity_Duration:ARG = MAX(Activity_Duration:ARG - TIME_PROGRESS(TIME:1), 0)
	;0になったら종료
	IF !Activity_Duration:ARG
		CALL 자유행동내용(ARG, Activity_Type:ARG, "종료")
	;まだ행동중
	ELSE
		CALL 자유행동내용(ARG, Activity_Type:ARG, "행동중")
		;連鎖処理,なんかもうちょっとスマートにやりたかった
		;連鎖しない행동は除く（考え事,読書）
		IF !GROUPMATCH(Activity_Type:ARG, 0, 6)
			FOR LOCAL, 1, CHARANUM
				;LOCALが連鎖対象でARGが連鎖主
				IF !Activity_Duration:LOCAL && CFLAG:LOCAL:현재위치 == CFLAG:ARG:현재위치 && RELATION:LOCAL:ARG > 100 && LOCAL != ARG
					;引数を付けて발생
					CALL 자유행동내용(LOCAL, Activity_Type:ARG, "발생", ARG)
					IF RESULT
						;連鎖主の행동をトレースする
						Activity_Duration:LOCAL = Activity_Duration:ARG
						Activity_Type:LOCAL = Activity_Type:ARG
						TCVAR:LOCAL:가능한한움직이지않는다 = 1
					ENDIF
				ENDIF
			NEXT
		ENDIF
	ENDIF
	;가능한한움직이지않는다ようにする
	TCVAR:ARG:가능한한움직이지않는다 = 1
	RETURN
ENDIF

;自由행동발생処理

;基本발생率,移動率の15を参考
SIF 5 * TIME_PROGRESS(TIME:1) < RAND:1000
	RETURN
;とりあえず20個分回す
CALL FISHER_YATES_SHAFFLE(20)
FOR LOCAL, 0, 20
	CALL 자유행동내용(ARG, SHAFFLE_ARRAY:LOCAL, "발생")
	IF RESULT
		;RESULT=행동時間
		Activity_Duration:ARG = RESULT
		Activity_Type:ARG = SHAFFLE_ARRAY:LOCAL
		TCVAR:ARG:가능한한움직이지않는다 = 1
		BREAK
	ENDIF
NEXT


@자유행동내용(CHARA, ARG, ARGS, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 현위치
#DIM 메시지표시
현위치 = CFLAG:CHARA:현재위치
메시지표시 = 현위치 == CFLAG:MASTER:현재위치 ? 1 # 0
SELECTCASE ARG
;考え事（その他）
CASE 0
	SELECTCASE ARGS
	CASE "발생"
		;在住キャラが自室にいる時
		SIF !CFLAG:CHARA:신사거주
			RETURN 0
		SIF 현위치 != CFLAG:CHARA:신사거주
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 무엇인가 생각을 하기 시작했다.
		;返し値は행동時間
		RETURN 20 + RAND:20
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 생각에 잠겨 있다.
	CASE "종료"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 생각을 마친 것 같다.
	CASE "중단"
		;중단メッセージは우후후突入時等でも表示されてしまうので表示しない
	ENDSELECT
;遊ぶ
CASE 1
	SELECTCASE ARGS
	CASE "발생"
		;［유치］持ちが屋外にいる時
		SIF !TALENT:CHARA:유치
			RETURN 0
		SIF !OUTROOF(현위치) && !전투훈련가능(현위치)
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"과")% 같이 놀기 시작했다. # %NAME_FROM_PLACE(현위치)%에서 놀기 시작했다.\@
		RETURN 60 + RAND:60
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 잘 놀고 있다.
	CASE "종료"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 놀다 지쳤던 모양이다.
	CASE "중단"
	ENDSELECT
;食事
CASE 2
	SELECTCASE ARGS
	CASE "발생"
		;（在住キャラが）空腹状態で푹쉼장소または自室にいる時
		;試しに抜いてみる
;		SIF !CFLAG:CHARA:신사거주
;			RETURN 0
		SIF !푹쉼장소(현위치) && 현위치 != CFLAG:CHARA:신사거주
			RETURN 0
		SIF TIME_PROGRESS(TCVAR:CHARA:공복시각) <= 0
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"과")% 같이 식사를 시작했다.# 식사를 준비했다.\@
		;행동が始まった段階で空腹は満たされる,以降も행동が終わるまで一定の満腹を得る,この時点では満腹だけ
		TCVAR:CHARA:공복시각 = 1440 * DAY + TIME + 120
		RETURN 60 + RAND:30
	CASE "행동중"
		CALL 자유행동식사(CHARA)
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 식사를 하고 있다.
	CASE "종료"
		CALL 자유행동식사(CHARA)
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 식사를 끝냈다.
	CASE "중단"
		CALL 자유행동식사(CHARA)
	ENDSELECT
;つまみ食い
CASE 3
	SELECTCASE ARGS
	CASE "발생"
		;요정および特定のキャラが空腹状態で집어먹기스팟にいる時
		SIF !TALENT:CHARA:요정 && !GROUPMATCH(CHARA, [[루미아]], [[코이시]], [[유유코]], [[시온]])
			RETURN 0
		SIF !집어먹기스팟(현위치)
			RETURN 0
		SIF TIME_PROGRESS(TCVAR:CHARA:공복시각) <= 0
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"과")% 같이 집어먹기 시작했다.# 몰래 집어먹기 시작했다.\@
		TCVAR:CHARA:공복시각 = 1440 * DAY + TIME + 120
		RETURN 30 + RAND:30
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 몰래 집어먹고 있다.
		CALL 자유행동식사(CHARA)
	CASE "종료"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 집어먹기를 마치고 만족한 것 같다.
		CALL 자유행동식사(CHARA)
	CASE "중단"
		CALL 자유행동식사(CHARA)
	ENDSELECT
;掃除
CASE 4
	SELECTCASE ARGS
	CASE "발생"
		;個室にいる時
		SIF !BEDROOM(현위치)
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		;악취내성（청소부）によって積極性が変わる
		IF TALENT:CHARA:악취내성 < 0 || TALENT:CHARA:청소부
			SIF YOGORE:(현위치) < 2000
				RETURN 0
		ELSEIF TALENT:CHARA:악취내성 == 0
			SIF YOGORE:(현위치) < 3500
				RETURN 0
		ELSEIF TALENT:CHARA:악취내성 > 0
			SIF YOGORE:(현위치) < 5000
				RETURN 0
		ENDIF
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"을")% 돕기 시작했다.# 더러움이 신경쓰여 청소를 시작했다.\@
		RETURN 60 + RAND:30
	CASE "행동중"
		;実際に掃除する
		YOGORE:(현위치) = MAX(YOGORE:(현위치) -100 * TIME_PROGRESS(TIME:1) / 5, 0)
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 싹싹 방청소를 하고 있다.
	CASE "종료"
		;청소경험が入る
		EXP:CHARA:청소경험 += 1
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 청소를 끝낸 것 같다.
	CASE "중단"
		;중단されても청소경험は入る
		EXP:CHARA:청소경험 += 1
	ENDSELECT
;運動
CASE 5
	SELECTCASE ARGS
	CASE "발생"
		;전투능력1以上のキャラが訓練できる場所にいる時,ただし演奏優先
		SIF ABL:CHARA:전투능력 < 1
			RETURN 0
		SIF BASE:CHARA:기력 <= MAXBASE:CHARA:기력 / 2
			RETURN 0
		SIF TALENT:CHARA:음악지식
			RETURN 0
		SIF !전투훈련가능(현위치)
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"과")% 행동을 같이하기 시작했다.# 한숨 돌리고 운동을 시작했다.\@
		RETURN 60 + RAND:60
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 열심히 운동하고 있다.
	CASE "종료"
		EXP:CHARA:전투경험 += 1
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 운동을 마치고 한숨 돌렸다.
	CASE "중단"
		EXP:CHARA:전투경험 += 1
	ENDSELECT
;読書
CASE 6
	SELECTCASE ARGS
	CASE "발생"
		;교양1以上のキャラが読書できる場所または自室にいる時
		SIF ABL:CHARA:교양 < 1
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		SIF !IS_LIBRARY(현위치, 1) || 현위치 != CFLAG:CHARA:신사거주
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 책을 읽기 시작했다.
		RETURN 60 + RAND:60
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 묵묵히 책을 읽고 있다.
	CASE "종료"
		EXP:CHARA:학습경험 += 1
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 책을 다 읽은 것 같다.
	CASE "중단"
		EXP:CHARA:학습경험 += 1
	ENDSELECT
;料理
CASE 7
	SELECTCASE ARGS
	CASE "발생"	
		;요리기능1以上のキャラが空腹状態で台所にいる時
		SIF ABL:CHARA:요리기능 < 1
			RETURN 0
		SIF !KITCHEN(현위치)
			RETURN 0
		SIF TIME_PROGRESS(TCVAR:CHARA:공복시각) <= 0
			RETURN 0
		IF 메시지표시
			IF CHAIN
				IF Activity_Duration:CHARA > 30
					PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %조사처리(CALLNAME:CHAIN,"을")% 돕기 시작했다.
				ELSE
					PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:CHAIN%의 요리를 나눠받았다.
				ENDIF
			ELSE
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 공복을 느껴 요리를 시작했다.
			ENDIF
		ENDIF
		RETURN 60 + RAND:30
	CASE "행동중"
		;食事とセット
		IF Activity_Duration:CHARA > 30
			SIF 메시지표시
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 경쾌하게 요리를 하고 있다.
		ELSE
			SIF 메시지표시
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 만든 음식을 먹고 있다.
			CALL 자유행동식사(CHARA)
		ENDIF
	CASE "종료"
		;종료までしたら요리경험+2
		EXP:CHARA:요리경험 += 2
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 식사를 마친 것 같다.
		CALL 자유행동식사(CHARA)
	CASE "중단"
		EXP:CHARA:요리경험 += 1
		SIF Activity_Duration:CHARA <= 30
			CALL 자유행동식사(CHARA)
	ENDSELECT
;演奏
CASE 8
	SELECTCASE ARGS
	CASE "발생"
		;［음악지식］持ちのキャラが訓練できる場所にいる時
		SIF !TALENT:CHARA:음악지식
			RETURN 0
		SIF !전투훈련가능(현위치)
			RETURN 0
		SIF BASE:CHARA:기력 <= MAXBASE:CHARA:기력 / 2
			RETURN 0
		IF 메시지표시
			IF CHAIN
				SELECTCASE TALENT:CHAIN:음악지식
				CASE 1
					SELECTCASE TALENT:CHARA:음악지식
					CASE 1
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %조사처리(CALLNAME:CHAIN,"과")%의 세션을 시작했다.
					CASE 2
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:CHAIN%의 연주에 맞춰 노래하기 시작했다.
					CASE 3
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:CHAIN%의 연주에 맞춰 춤추기 시작했다.
					ENDSELECT
				CASE 2
					SELECTCASE TALENT:CHARA:음악지식
					CASE 1
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:CHAIN%의 노래에 맞춰 연주하기 시작했다.
					CASE 2
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %조사처리(CALLNAME:CHAIN,"과")%의 코러스를 시작했다.
					CASE 3
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:CHAIN%의 노래에 맞춰 춤추기 시작했다.
					ENDSELECT
				CASE 3
					SELECTCASE TALENT:CHARA:음악지식
					CASE 1
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:CHAIN%의 안무에 맞춰 연주를 시작했다.
					CASE 2
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:CHAIN%의 안무에 맞춰 노래하기 시작했다.
					CASE 3
						PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %조사처리(CALLNAME:CHAIN,"과")% 같이 춤추기 시작했다.
					ENDSELECT
				ENDSELECT
			ELSE
				SELECTCASE TALENT:CHARA:음악지식
				CASE 1
					PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 천천히 연주를 시작했다.
				CASE 2
					PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 천천히 노래부르기 시작했다.
				CASE 3
					PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 천천히 춤추기 시작했다.
				ENDSELECT
			ENDIF
		ENDIF
		RETURN 60 + RAND:30
	CASE "행동중"
		IF 메시지표시
			SELECTCASE TALENT:CHARA:음악지식
			CASE 1
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 시원하게 연주하고 있다.
			CASE 2
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 마음대로 노래하고 있다.
			CASE 3
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 화려하게 춤추고 있다.
			ENDSELECT
		ENDIF
	CASE "종료"
		SELECTCASE TALENT:CHARA:음악지식
		CASE 1
			EXP:CHARA:연주경험 += 1
			SIF 메시지표시
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 연주를 마치고 한숨 돌렸다.
		CASE 2
			EXP:CHARA:가창경험 += 1
			SIF 메시지표시
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 노래를 마치고 한숨 돌렸다.
		CASE 3
			EXP:CHARA:무도경험 += 1
			SIF 메시지표시
				PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 안무를 마치고 한숨 돌렸다.
		ENDSELECT
	CASE "중단"
		SELECTCASE TALENT:CHARA:음악지식
		CASE 1
			EXP:CHARA:연주경험 += 1
		CASE 2
			EXP:CHARA:가창경험 += 1
		CASE 3
			EXP:CHARA:무도경험 += 1
		ENDSELECT
	ENDSELECT
;採集
CASE 9
	SELECTCASE ARGS
	CASE "발생"
		;채집기능1以上のキャラが採集できる場所にいる時
		SIF ABL:CHARA:채집기능 < 1
			RETURN 0
		SIF !GATHERING_SPOT(현위치)
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"을")% 돕기 시작했다.# 주위를 둘러본 후, 채집작업을 시작했다.\@
		RETURN 60 + RAND:120
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 드문 소재를 찾고 있다.
	CASE "종료"
		EXP:CHARA:채집경험 += 1
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 채집을 마친 것 같다.
	CASE "중단"
		EXP:CHARA:채집경험 += 1
	ENDSELECT
;釣り
CASE 10
	SELECTCASE ARGS
	CASE "발생"
		;낚시기능1以上のキャラが釣りできる場所にいる時
		SIF ABL:CHARA:낚시기능 < 1
			RETURN 0
		SIF !FISHING_SPOT(현위치)
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"과")% 행동을 같이하기 시작했다.# 낚시대를 꺼냈다.\@
		RETURN 60 + RAND:120
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 한가로이 낚시를 하고 있다.
	CASE "종료"
		;종료までいったら経験+2
		EXP:CHARA:낚시경험 += 2
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 낚시를 마친 것 같다.
	CASE "중단"
		EXP:CHARA:낚시경험 += 1
	ENDSELECT
;実験
CASE 11
	SELECTCASE ARGS
	CASE "발생"
		;조제기능1以上のキャラが調合できる場所（あなた私室以外）または自室にいる時
		SIF ABL:CHARA:조제기능 < 1
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		;連鎖時はスキップ
		IF !CHAIN
			SIF PHARMACY_SPOT(현위치) != 2 && 현위치 != CFLAG:CHARA:신사거주
				RETURN 0
		ENDIF
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"을")% 돕기 시작했다.# 실험 준비를 시작했다.\@
		RETURN 60 + RAND:30
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 수상한 실험을 하고 있다.
	CASE "종료"
		EXP:CHARA:조제경험 += 1
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 실험을 마친 듯 하다.
	CASE "중단"
		EXP:CHARA:조제경험 += 1
	ENDSELECT
;のんびり
CASE 12
	SELECTCASE ARGS
	CASE "발생"
		;昼間に休憩できる場所または푹쉼장소にいる時
		SIF !INRANGE(TIME, 600, 960)
			RETURN 0
		SIF !휴식가능(현위치) && !푹쉼장소(현위치)
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %CALLNAME:CHAIN%에게 꾀여 한가로이 지내기 시작했다.# 한숨 돌려 쉬기 시작했다.\@
		RETURN 60 + RAND:30
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 한가롭다.
	CASE "종료"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 휴식을 마친 것 같다.
	CASE "중단"
	ENDSELECT
;晩酌
CASE 13
	SELECTCASE ARGS
	CASE "발생"
		;酒飲めるキャラが夜間に푹쉼장소にいる時
		SIF TALENT:CHARA:술내성 < 0
			RETURN 0
		SIF !INRANGE(TIME, 1200, 1440) && !INRANGE(TIME, 0, 120)
			RETURN 0
		SIF !푹쉼장소(현위치)
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"과")% 행동을 같이하기 시작했다.# 술울 꺼내 저녁 반주를 시작했다.\@
		RETURN 60 + RAND:30
	CASE "행동중"
		;ちょっとずつ酔う,酔いすぎるとSHIRAHUではなくなり중단されるので注意
		BASE:CHARA:술기운 += TIME_PROGRESS(TIME:1) * (RAND:3 + 1) * (4 - TALENT:CHARA:술내성)
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 한가로이 술을 마시고 있다.
	CASE "종료"
		BASE:CHARA:술기운 += TIME_PROGRESS(TIME:1) * (RAND:3 + 1) * (4 - TALENT:CHARA:술내성)
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 저녁 반주를 마친 것 같다.
	CASE "중단"
		BASE:CHARA:술기운 += TIME_PROGRESS(TIME:1) * (RAND:3 + 1) * (4 - TALENT:CHARA:술내성)
	ENDSELECT
;買い物
CASE 14
	SELECTCASE ARGS
	CASE "발생"
		;お店にいる時
		SIF !KAIDASHI_SHOP(현위치) && !FLOWER_SHOP(현위치) && !SAKE_SHOP(현위치) && !YASAI_SHOP(현위치)
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"과")% 행동을 같이하기 시작했다.# 장바구니를 꺼냈다.\@
		RETURN 60 + RAND:30
	CASE "행동중"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 마음대로 쇼핑하고 있다.
	CASE "종료"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 쇼핑을 마친 것 같다.
	CASE "중단"
	ENDSELECT
ENDSELECT

@자유행동조건(ARG)
#FUNCTION
;拠点にいない
SIF !AT_HOME(ARG)
	RETURNF 0
;正常な状態ではない
SIF !SHIRAHU(ARG)
	RETURNF 0
;우후후中
SIF CFLAG:ARG:우후후
	RETURNF 0
;仕事中（행동중）
SIF CFLAG:ARG:행동
	RETURNF 0
;데일리中
SIF CFLAG:ARG:데일리
	RETURNF 0
;동행中
SIF CFLAG:ARG:동행
	RETURNF 0
;移動している
SIF CFLAG:ARG:현재위치 != CFLAG:ARG:전턴위치
	RETURNF 0
;天候ダメージがある
SIF 날씨에의한영향(ARG, CFLAG:ARG:현재위치) < 0
	RETURNF 0
;妊娠中
SIF CFLAG:ARG:임신자각상태 == 1
	RETURNF 0
RETURNF 1

@자유행동식사(CHARA)
#DIM CHARA
;2時間分の食事,あまり頻発するとあなたが食べさせられなくなってしまうので少なめ
TCVAR:CHARA:공복시각 = 1440 * DAY + TIME + 120
CALL RECOVER(CHARA, 3 * TIME_PROGRESS(TIME:1), "체력", "식사에 의한 회복")
CALL RECOVER(CHARA, 3 * TIME_PROGRESS(TIME:1), "기력", "식사에 의한 회복")
