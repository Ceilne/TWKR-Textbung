@SKIP_MOVE_2(ARG:0,ARG:1)
#DIM CONST 마을문 = 201
#DIM CONST 중앙광장 = 202
#DIM CONST 남쪽길 = 203
#DIM CONST 동쪽길 = 204
#DIM CONST 북쪽길 = 205
#DIM CONST 서쪽길 = 206
#DIM CONST 용신상 = 207

#DIM CONST 술집 = 211
#DIM CONST 카페 = 212
#DIM CONST 스즈나안 = 213
#DIM CONST 연립앞 = 214
#DIM CONST 꽃집 = 215
#DIM CONST 식료품점 = 216
#DIM CONST 과자가게 = 217
#DIM CONST 일품요리점 = 218
#DIM CONST 집회소 = 219
#DIM CONST 망루 = 220
#DIM CONST 아큐저택 = 221
#DIM CONST SPT서당 = 222
#DIM CONST 대중욕탕 = 223
#DIM CONST 케이네집 = 224
#DIM CONST SPT혼숙여관 = 225
#DIM CONST 코스즈집 = 226
#DIM CONST 야채가게 = 227
#DIM CONST 대절욕탕 = 228

#DIM CONST 아나타방 = 230
#DIM CONST 반키방 = 231
#DIM CONST 렌코방 = 232
#DIM CONST 메리방 = 233
#DIM CONST 유키방 = 234
#DIM CONST 마이방 = 235
#DIM CONST 공용화장실 = 236
#DIM CONST 공용우물 = 237

;LOCAL 일람
;0	현재위치

;현재위치 취득
LOCAL = CFLAG:MASTER:현재위치
SIF ARG:1
	LOCAL = ARG:1

;잠정 목적지에 직접 이동할 수 있는 경우
IF CAN_MOVE(LOCAL, ARG) & 1
	;거기에 이동
	RETURN ARG
ENDIF

;막다른 골목으로부터 갈 수 있는 장소는 고정이므로, 거기를 목표로 한다
SELECTCASE LOCAL
CASE 마을문
	RETURN 남쪽길
CASE 술집, 카페, 일품요리점
	RETURN 남쪽길
CASE 과자가게
	RETURN 동쪽길
CASE SPT혼숙여관
	RETURN 서쪽길
CASE 코스즈집
	RETURN 스즈나안
CASE 대절욕탕
	RETURN 대중욕탕
CASE 아나타방 TO 공용우물
	RETURN 연립앞
ENDSELECT

;PRINTFORML LOCAL는{LOCAL}

;직접 이동할 수 없는 경우,  1개가까운 위치를 잠정 목적지로 다시 설정해 루프
;지점 추가시 요점 보충(전케이스 수정하는 것)
SELECTCASE ARG
	CASE 마을문, 술집, 카페, 일품요리점
		RETURN 남쪽길
	CASE 중앙광장
		SELECTCASE LOCAL
		CASE 스즈나안, 과자가게, 대중욕탕
			RETURN 동쪽길
		CASE 집회소, SPT서당, 케이네집
			RETURN 북쪽길
		CASE 연립앞, 아큐저택, SPT혼숙여관
			RETURN 서쪽길
		CASEELSE
			RETURN 남쪽길
		ENDSELECT
	CASE 남쪽길
		SELECTCASE LOCAL
		CASE 집회소, SPT서당, 케이네집
			RETURN 동쪽길
		CASEELSE
			RETURN 중앙광장
		ENDSELECT
	CASE 동쪽길
		SELECTCASE LOCAL
		CASE 연립앞
			RETURN 남쪽길
		CASE 아큐저택
			RETURN 북쪽길
		CASEELSE
			RETURN 중앙광장
		ENDSELECT
	CASE 북쪽길
		RETURN 중앙광장
	CASE 서쪽길
		SELECTCASE LOCAL
		CASE 집회소, SPT서당, 케이네집
			RETURN 북쪽길
		CASEELSE
			RETURN 중앙광장
		ENDSELECT
	CASE 용신상
		SELECTCASE LOCAL
		CASE 집회소, 아큐저택, SPT서당, 케이네집
			RETURN 북쪽길
		CASEELSE
			RETURN 중앙광장
		ENDSELECT
	CASE 스즈나안, 과자가게, 대중욕탕
		RETURN 동쪽길
	CASE 연립앞
		SELECTCASE LOCAL
		CASE 동쪽길
			RETURN 남쪽길
		CASEELSE
			RETURN 서쪽길
		ENDSELECT
	CASE 꽃집, 식료품점, 망루
		RETURN 중앙광장
	CASE 집회소, SPT서당, 케이네집
		SELECTCASE LOCAL
		CASE 서쪽길, 용신상, 아큐저택
			RETURN 북쪽길
		CASEELSE
			RETURN 동쪽길
		ENDSELECT
	CASE 아큐저택
		SELECTCASE LOCAL
		CASE 용신상, 집회소, SPT서당, 케이네집
			RETURN 북쪽길
		CASEELSE
			RETURN 서쪽길
		ENDSELECT
	CASE SPT서당, 케이네집
		SELECTCASE LOCAL
		CASE 아큐저택, 용신상
			RETURN 북쪽길
		CASEELSE
			RETURN 동쪽길
		ENDSELECT
	CASE SPT혼숙여관
		RETURN 서쪽길
	CASE 코스즈집
		RETURN 스즈나안
	CASE 야채가게
		SELECTCASE LOCAL
		CASE 집회소, SPT서당, 케이네집
			RETURN 동쪽길
		CASEELSE
			RETURN 중앙광장
		ENDSELECT
	CASE 대절욕탕
		RETURN 대중욕탕
	CASE 아나타방 TO 공용우물
		RETURN 연립앞
	CASEELSE
		RETURN 마을문
ENDSELECT
