
@T_시스템처리
;100 = 공격 명중
;101 = 공격 빗나감
;110 = 플레이어 스킬표 생성
;120 = 플레이어 회피시도
;121 = 적 회피 시도
FOR LOCAL,1,1000
    TRYCALLFORM ONGOING_T_SKILL{LOCAL}(T_턴주인,"시스템체크")
NEXT

@T_COM공격한다
#DIM DYNAMIC 보너스
PRINTFORML %T_턴주인의% 공격이 %T_턴반대에게% 날아갔다!
IF 상태이상체크(T_턴주인, "실명")&&RAND:2 == 0
    T_시스템 = 101
    CALL T_시스템처리
    PRINTFORML 그러나 앞이 제대로 보이지 않아 빗맞추고 말았다...
    RETURN 0
ENDIF
T_시스템 = 100
CALL T_시스템처리
보너스 = RAND:100
IF T_턴주인 == T_나
    CALL T_데미지(T_턴반대,T_내공격력+보너스,"물리")
ELSEIF T_턴주인 == T_적
    CALL T_데미지(T_턴반대,T_적공격력+보너스,"물리")
ENDIF
RETURN 0

@T_COM회피한다(ARG)
IF ARG == T_나
    PRINTFORML 공격을 피하기 위해 신경을 집중했다!  
    T_시스템 = 120
    CALL T_시스템처리
    ;========
    ;회피 판정
    ;========
    ;플레이어는 미니게임(화살표 방향 누르기)를 해야 버프를 받게해? 나중에 고려해본다
    ;
    CALL 버프부여(ARG, "회피", T_내방어력, 1)
    RETURN 0
ELSEIF ARG == T_적
    
    PRINTFORML %T_적은% 공격을 회피하려 하였다!
    T_시스템 = 121
    CALL T_시스템처리
    CALL 버프부여(ARG, "회피", T_적방어력, 1)
    T_적회피 = 1
ENDIF

@T_COM스킬사용
#DIM DYNAMIC 표시카운터
#DIM 스킬번호

T_시스템 = 110
CALL T_시스템처리

PRINTL 무엇을 쓸까?
DRAWLINE
FOR 스킬번호,1,1000
    IF HAVE_T_SKILL(T_턴주인, 스킬번호)
        ;스킬 소유, 스킬창에 이름 표시
        CALL CAN_T_SKILL(T_턴주인, 스킬번호)
        IF RESULT == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
        PRINTFORMLC [{스킬번호}] %RESULTS%
        RESETCOLOR
        PRINTL
    ELSE
        ;스킬 소유하고 있지 않음
    ENDIF
NEXT
PRINTL
PRINTL [9999]돌아간다
WHILE 1 ;$스킬입력루프
    INPUT
    LOCAL = RESULT
    IF LOCAL == 9999
        RETURN 0
    ELSEIF HAVE_T_SKILL(T_턴주인, LOCAL)
        DRAWLINE
        CALL DO_T_SKILL(T_턴주인, LOCAL)
        RETURN RESULT
    ELSE
        ;시전 불가시는 DO_T_SKILL에서 처리
        PRINTL
        PRINTW 없는 번호입니다.
        CLEARLINE 1
    ENDIF
WEND

;시전 대응
@DO_T_SKILL(사용자, 스킬번호)
#DIM 사용자
#DIM 스킬번호
;스킬의 시전시 처리, 대사만 넣으면 된다
CALL CAN_T_SKILL(사용자, 스킬번호)
IF RESULT == 0
    ;시전불가
    PRINTL 시전할수 없습니다
    RETURN 0
ELSE
    ;시전성공
    CALLFORM AFTER_T_SKILL{스킬번호}(사용자)
    RETURN 1
ENDIF

;그 스킬을 소유하고 있는지 여부
;보유 대응
@HAVE_T_SKILL(사용자, 스킬번호)
#FUNCTION
#DIM 사용자
#DIM 스킬번호
LOCAL = 스킬번호 + 500 ;TALENT.csv 내 스킬번호

SIF TALENTNAME:LOCAL == ""
    RETURNF 0

SIF TALENT:사용자:LOCAL == 1
    RETURNF 1
SIF 탄막디버그 == 1
    RETURNF 1

RETURNF 0

;지금 스킬을 시전할수 있는지
;실행가부 대응
@CAN_T_SKILL(사용자, 스킬번호)
#DIM 사용자
#DIM 스킬번호
;식중함수는 CALLFORM 사용 불가 이기 때문에 수정

IF HAVE_T_SKILL(사용자, 스킬번호)
    ;이 스킬 소유중
    CALLFORM ONGOING_T_SKILL{스킬번호}(사용자, "남은쿨타임")
    IF RESULT == 0
        ;쿨타임 0 시전가능
        IF 상태이상체크(사용자, "침묵")
            RETURN 0
        ENDIF
        RETURN 1
    ELSE
        ;아직 쿨타임이라 시전불가능
        RETURN 0
    ENDIF
ELSE
    ;이스킬 없음, 시전불가
    RETURN 0
ENDIF


@ARG능력치(ARG,능력치)
#FUNCTION
#DIMS 능력치
#DIM DYNAMIC 결과값
IF ARG == T_나
    IF 능력치 == "힘"
        결과값 = T_내힘
    ELSEIF 능력치 == "지성"
        결과값 = T_내지성
    ELSEIF 능력치 == "속도"
        결과값 = T_내속도
    ELSEIF 능력치 == "인내"
        결과값 = T_내인내
    ELSEIF 능력치 == "운"
        결과값 = T_내운
    ELSEIF 능력치 == "체력"
        결과값 = T_내체력
    ELSEIF 능력치 == "최대체력"
        결과값 = T_내최대체력
    ELSEIF 능력치 == "공격력"
        결과값 = T_내공격력
    ELSEIF 능력치 == "방어력"
        결과값 = T_내방어력
    ELSEIF 능력치 == "물리내성"
        결과값 = T_내물리내성
    ELSEIF 능력치 == "화염내성"
        결과값 = T_내화염내성
    ELSEIF 능력치 == "빙결내성"
        결과값 = T_내빙결내성
    ELSEIF 능력치 == "전격내성"
        결과값 = T_내전격내성
    ELSEIF 능력치 == "질풍내성"
        결과값 = T_내질풍내성
    ELSEIF 능력치 == "마력내성"
        결과값 = T_내마력내성
    ELSEIF 능력치 == "수격내성"
        결과값 = T_내수격내성
    ELSEIF 능력치 == "정신내성"
        결과값 = T_내정신내성
    ENDIF
ELSEIF ARG == T_적
    IF 능력치 == "힘"
        결과값 = T_적힘
    ELSEIF 능력치 == "지성"
        결과값 = T_적지성
    ELSEIF 능력치 == "속도"
        결과값 = T_적속도
    ELSEIF 능력치 == "인내"
        결과값 = T_적인내
    ELSEIF 능력치 == "운"
        결과값 = T_적운
    ELSEIF 능력치 == "체력"
        결과값 = T_적체력
    ELSEIF 능력치 == "최대체력"
        결과값 = T_적최대체력
    ELSEIF 능력치 == "공격력"
        결과값 = T_적공격력
    ELSEIF 능력치 == "방어력"
        결과값 = T_적방어력
    ELSEIF 능력치 == "물리내성"
        결과값 = T_적물리내성
    ELSEIF 능력치 == "화염내성"
        결과값 = T_적화염내성
    ELSEIF 능력치 == "빙결내성"
        결과값 = T_적빙결내성
    ELSEIF 능력치 == "전격내성"
        결과값 = T_적전격내성
    ELSEIF 능력치 == "질풍내성"
        결과값 = T_적질풍내성
    ELSEIF 능력치 == "마력내성"
        결과값 = T_적마력내성
    ELSEIF 능력치 == "수격내성"
        결과값 = T_적수격내성
    ELSEIF 능력치 == "정신내성"
        결과값 = T_적정신내성
    ENDIF
ENDIF
RETURNF 결과값

@ARG반대능력치(ARG,능력치)
#FUNCTION
#DIMS 능력치

IF ARG == T_나
    RETURNF ARG능력치(T_적, 능력치)
ELSEIF ARG == T_적
    RETURNF ARG능력치(T_나, 능력치)
ENDIF
