
@T_시스템처리
FOR LOCAL,1,1000
    TRYCALLFORM T_SKILL{LOCAL}(T_턴주인,"시스템체크")
NEXT




@T_COM공격한다
PRINTFORML %T_턴주인의% 공격이 %T_턴반대에게% 날아갔다!
T_시스템 = 100
CALL T_시스템처리
CALL T_데미지(T_턴반대,T_내공격력,"물리")
RETURN 0

@T_COM스킬사용
#DIM DYNAMIC 표시카운터
PRINTL 무엇을 쓸까?
DRAWLINE
FOR LOCAL,1,1000
    TRYCALLFORM T_SKILL{LOCAL}(T_턴주인, "표시")
    IF RESULT == 1

    ENDIF
NEXT
PRINTL
PRINTL [9999]돌아간다
INPUT
IF RESULT == 9999
    RETURN 0
ELSE
    DRAWLINE
    TRYCALLFORM T_SKILL{RESULT}(T_턴주인, "시전")
ENDIF

@T_SKILL1(ARG,인자)
#DIMS 스킬명
#DIM 스킬번호
#DIM 쿨타임
#DIM 지속시간
#DIM 명중률
#DIMS 인자
#DIM 남은쿨타임
#DIM 남은지속시간
#DIM 피해량
#DIMS 속성
;ARG = 사용자
;-------------------
;성능 입력
;쿨타임 0 = 한 턴에 몇 번이라도, 쿨타임 1 = 한 턴에 한번, 쿨타임 2= 두 턴에 한번
;-------------------
스킬명 = "문 라이트 레이"
스킬번호 = 1
쿨타임 = 1
지속시간 = 0
명중률 = 0
피해량 = 250
속성 = "마력"
;-------------------
IF 인자 == "사용가부"
    ;지금 스킬을 시전할수 있는지
    CALLFORM T_SKILL{스킬번호}(ARG, "소유")
    IF RESULT == 1
        ;이 스킬 소유중
        CALLFORM T_SKILL{스킬번호}(ARG, "쿨타임")
        IF RESULT == 0
            ;쿨타임 0 시전가능
            RETURN 1
        ELSE
            ;아직 쿨타임이라 시전불가능
            RETURN 0
        ENDIF
    ELSE
        ;이스킬 없음, 시전불가
        RETURN 0
    ENDIF
ELSEIF 인자 == "소유"
    ;그 스킬을 소유하고 있는지 여부
    ;임시
    RETURN 1
ELSEIF 인자 == "쿨타임"
    ;쿨타임을 반환, 건들일 없다
    RETURN 남은쿨타임
ELSEIF 인자 == "턴엔드"
    ;매 턴이 지날때마다 지속시간과 쿨타임처리를 하는 부분
    IF 남은쿨타임 > 0
        남은쿨타임--
    ENDIF
    IF 남은지속시간 > 0
        남은지속시간--
    ENDIF
ELSEIF 인자 == "지속시간"
    ;남은 지속시간 표시, 건들일 없다
    RETURN 남은지속시간
ELSEIF 인자 == "표시"
    ;스킬이 스킬창에서 어떻게 표시되는지, 어지간해선 건들일 없다, 스킬명에 특수기호를 넣으려는게 아니라면?
    CALLFORM T_SKILL{스킬번호}(ARG, "소유")
    IF RESULT == 1
        ;스킬 소유, 스킬창에 이름 표시
        CALLFORM T_SKILL{스킬번호}(ARG, "사용가부")
        IF RESULT == 1
            ;시전가능, 흰색으로 빛남
            SETCOLOR 0xFFFFFF
        ELSE
            ;시전불가, 회색임
            SETCOLOR 0x585858
        ENDIF
        PRINTFORM [{스킬번호}] %스킬명%
        RESETCOLOR
        RETURN 1
    ELSE
        ;스킬 소유하고 있지 않음
        RETURN 0
    ENDIF
ELSEIF 인자 == "명중"
    ;명중판정, 건들일은 없으나 빗나갔을때 처리를 넣으려면 이용
    IF 명중률 >= RAND:100 + 1
        ;명중
        RETURN 1
    ELSE
        ;빗나감
        RETURN 0
    ENDIF
ELSEIF 인자 == "명중률"
    ;명중률을 반환함, 특정상황에서 명중률을 변동하고 싶으면 사용
    RETURN 명중률
ELSEIF 인자 == "시스템체크"
    ;시스템 메시지에 따른 검사, 패시브 스킬등에 이용

ELSEIF 인자 == "시전"
    ;스킬의 시전시 처리, 대사만 넣으면 된다
    CALLFORM T_SKILL{스킬번호}(ARG, "사용가부")
    IF RESULT == 0
        ;시전불가
        PRINTL 사용할수 없다
    ELSE
        ;시전성공
        CALLFORM T_SKILL{스킬번호}(ARG, "명중")
        IF RESULT == 0
            ;!감나빗!
            SETCOLOR 0xFF4444
            PRINTFORML 빗나감!
            RESETCOLOR
            ;----------------
            ;대사는 이쪽
            ;----------------
            PRINTFORML 빛이 %T_턴반대의% 등 뒤에 쏟아진다...
            ;----------------
            ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
            ;----------------
            남은쿨타임 = 쿨타임
        ELSE
            ;명중!
            ;----------------
            ;대사는 이쪽
            ;----------------
            PRINTFORML 빛이 %T_턴반대에게% 내려꽃힌다!
            ;----------------
            ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
            ;----------------
            CALL T_데미지(T_턴반대, 피해량, 속성)
            남은쿨타임 = 쿨타임
        ENDIF
    ENDIF
ENDIF
