
@T_시스템처리(ARG,ARG:1 = 0,ARG:2 = 0,ARG:3 = 0,ARG:4 = 0,ARG:5 = 0,ARG:6 = 0,ARG:7 = 0,ARG:8 = 0,ARG:9 = 0)
#DIM DYNAMIC 대피소,2
;기본적으로 홀수 = 적 짝수 = 플레이어

;1 = 적 턴 시작
;2 = 플레이어 턴 시작
;10 = 게임 시작
;100 = 플레이어공격 명중
;101 = 적 공격 명중
;102 = 플레이어 공격 빗나감
;103 = 적 공격 빗나감
;104 = 플레이어가 적에게 데미지를 줌,최종데미지,원래데미지,피격자
;105 = 적이 플레이어에게 데미지를 줌,최종데미지,원래데미지,피격자
;106 = 플레이어가 적에게 데미지를 받기직전,최종데미지,원래데미지,피격자
;107 = 적이 플레이어에게 데미지를 받기직전,최종데미지,원래데미지,피격자
;110 = 플레이어 스킬표 생성
;120 = 플레이어 회피시도
;121 = 적 회피 시도
;200 = 플레이어 체력 0
;201 = 적 체력 0
;220 = 플레이어 운 데미지 관통
;221 = 적 운 데미지 관통
;1000 = 플레이어 항복 시도
;1001 = 적 항복 시도
T_시스템:0 = ARG
T_시스템:1 = ARG:1
T_시스템:2 = ARG:2
T_시스템:3 = ARG:3
T_시스템:4 = ARG:4
T_시스템:5 = ARG:5
T_시스템:6 = ARG:6
T_시스템:7 = ARG:7
T_시스템:8 = ARG:8
T_시스템:9 = ARG:9
대피소:0 = T_시전자
대피소:1 = T_시전자반대
FOR LOCAL,0,1000
    T_시전자 = T_턴주인
    T_시전자반대 = T_턴반대
    SIF HAVE_T_SKILL(T_턴주인,LOCAL)
        TRYCALLFORM ONGOING_T_SKILL{LOCAL}(T_턴주인,"시스템체크")
    T_시전자 = T_턴반대
    T_시전자반대 = T_턴주인
    SIF HAVE_T_SKILL(T_턴반대,LOCAL)
        TRYCALLFORM ONGOING_T_SKILL{LOCAL}(T_턴반대,"시스템체크")
NEXT
T_시전자 = 대피소:0
T_시전자반대 = 대피소:1
VARSET T_시스템

@T_COM공격한다
#DIM DYNAMIC 보너스
PRINTFORML %T_턴주인의% 공격이 %T_턴반대에게% 날아갔다!
IF TB_SUM_REMAIN(T_턴주인,TCV_실명)&&RAND:2 == 0
    IF T_턴주인 == MASTER
        CALL T_시스템처리(102)
    ELSE
        CALL T_시스템처리(103)
    ENDIF
    PRINTFORML 그러나 앞이 제대로 보이지 않아 빗맞추고 말았다...
    RETURN 0
ENDIF
IF T_턴주인 == MASTER
    CALL T_시스템처리(100)
ELSE
    CALL T_시스템처리(101)
ENDIF
보너스 = RAND:100
CALL T_데미지(T_턴반대,TCVAR:T_턴주인:T_공격력+보너스,"물리")
RETURN 1

@T_COM회피한다(ARG)
;ARG = 회피하는 대상
IF ARG == MASTER
    PRINTFORML 공격을 피하기 위해 신경을 집중했다!  
    CALL T_시스템처리(120)
    ;========
    ;회피 판정
    ;========
    ;플레이어는 미니게임(화살표 방향 누르기)를 해야 버프를 받게해? 나중에 고려해본다
    ;
    
ELSEIF ARG == TARGET
    
    PRINTFORML %타겟은% 공격을 회피하려 하였다!
    CALL T_시스템처리(121)
ENDIF
IF TALENT:ARG:그레이즈능숙함
    CALL 통합버프(ARG, TCV_회피, -1,200, 1)
ELSE
    CALL 통합버프(ARG, TCV_회피,-1,100, 1)
ENDIF
RETURN 0

@T_COM스킬사용
#DIM 스킬번호

CALL T_시스템처리(110)

T_시전자 = T_턴주인
T_시전자반대 = T_턴반대
$스킬표시
PRINTL 무엇을 쓸까?
DRAWLINE
FOR 스킬번호,1,T_스킬한계값
    IF HAVE_T_SKILL(T_턴주인, 스킬번호)
        ;스킬 소유, 스킬창에 이름 표시
        CALLFORMF T_SKILL{스킬번호}("타입")
        IF RESULT == 자동형
            CONTINUE
        ENDIF
        CALL CAN_T_SKILL(T_턴주인, 스킬번호)
        IF RESULT == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
        PRINTFORMLC [{스킬번호}] %RESULTS%
        RESETCOLOR
        CALLFORMF T_SKILL{스킬번호}("HP코스트")
        IF RESULT > 0
            SETCOLOR C_RED
            PRINTFORMLC  {RESULT}
            RESETCOLOR
        ENDIF
        CALLFORMF T_SKILL{스킬번호}("TSP코스트")
        IF RESULT > 0
            SETCOLOR 0x1111FF
            PRINTFORMLC  {RESULT}
            RESETCOLOR
        ENDIF
        PRINTL
    ELSE
        ;스킬 소유하고 있지 않음
    ENDIF
NEXT
PRINTL
PRINTL [9998]설명
PRINTL [9999]돌아간다
WHILE 1 ;$스킬입력루프
    INPUT
    LOCAL = RESULT
    IF LOCAL == 9999
        RETURN 0
    ELSEIF LOCAL == 9998
        CALL T_HELP
        GOTO 스킬표시
    ELSEIF HAVE_T_SKILL(T_턴주인, LOCAL)
        DRAWLINE
        CALL DO_T_SKILL(T_턴주인, LOCAL)
        ;시전할수 없을때엔 턴 넘김 없어야 합니다
        ;RESULT == 1일때만 턴 넘어감
        RETURN RESULT
    ELSE
        ;시전 불가시는 DO_T_SKILL에서 처리
        PRINTL
        PRINTW 없는 번호입니다.
        CLEARLINE 1
    ENDIF
WEND

@T_HELP
#DIM 스킬번호
PRINTL 어떤 스킬의 설명을 볼까?
DRAWLINE
FOR 스킬번호,1,1000
    IF HAVE_T_SKILL(T_턴주인, 스킬번호)
        ;스킬 소유, 스킬창에 이름 표시
        CALL CAN_T_SKILL(T_턴주인, 스킬번호)
        IF RESULT == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
        PRINTFORMLC [{스킬번호}] %RESULTS%
        RESETCOLOR
        PRINTL
    ELSE
        ;스킬 소유하고 있지 않음
    ENDIF
NEXT
PRINTL
PRINTL [9999]돌아간다
WHILE 1
    INPUT
    LOCAL = RESULT
    IF LOCAL == 9999
        RETURN 0
    ELSEIF HAVE_T_SKILL(T_턴주인, LOCAL)
        DRAWLINE
        CALLFORM HELP_T_SKILL{LOCAL}
        DRAWLINE
        PRINTW
        CLEARLINE 9
    ELSE
        ;시전 불가시는 DO_T_SKILL에서 처리
        PRINTL
        PRINTW 없는 번호입니다.
        CLEARLINE 1
    ENDIF
    RESULT = 0
WEND



;시전 대응
@DO_T_SKILL(사용자, 스킬번호)
#DIM 사용자
#DIM 스킬번호
;스킬의 시전시 처리, 대사만 넣으면 된다
CALL CAN_T_SKILL(사용자, 스킬번호)
IF RESULT == 0
    ;시전불가
    PRINTL 시전할수 없습니다
    RETURN 0
ELSE
    ;시전성공
    CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
    IF 사용자 == MASTER
        SETCOLOR 0xFFFFFF
    ELSE
        SETCOLOR 0xFF4444
    ENDIF
    PRINTFORML %RESULTS%
    RESETCOLOR
    ;이 아래의 대사 작성
    CALLFORM AFTER_T_SKILL{스킬번호}(사용자)
    RETURN 1
ENDIF

;그 스킬을 소유하고 있는지 여부
;보유 대응
@HAVE_T_SKILL(사용자, 스킬번호)
#FUNCTION
#DIM 사용자
#DIM 스킬번호
LOCAL = 스킬번호 + 500 ;TALENT.csv 내 스킬번호

SIF TALENTNAME:LOCAL == ""
    RETURNF 0

SIF TALENT:사용자:LOCAL == 1
    RETURNF 1
SIF 탄막디버그 == 1
    RETURNF 1

RETURNF 0

;지금 스킬을 시전할수 있는지
;실행가부 대응
@CAN_T_SKILL(사용자, 스킬번호)
#DIM 사용자
#DIM 스킬번호
;식중함수는 CALLFORM 사용 불가 이기 때문에 수정

IF HAVE_T_SKILL(사용자, 스킬번호)
    ;이 스킬 소유중
    TRYCALLFORM ONGOING_T_SKILL{스킬번호}(사용자, "남은쿨타임")
    IF RESULT == 0
        ;쿨타임 0 시전가능
        IF TB_SUM_REMAIN(사용자,TCV_침묵)
            RETURN 0
        ENDIF
        CALLFORMF T_SKILL{스킬번호}("HP코스트")
        IF RESULT >= TCVAR:사용자:T_체력
            RETURN 0
        ENDIF
        RESULT = 0
        CALLFORMF T_SKILL{스킬번호}("TSP코스트")
        IF RESULT > BASE:사용자:TSP&&사용자 == MASTER
            RETURN 0
        ENDIF
        RESULT = 0
        RETURN 1
    ELSE
        ;아직 쿨타임이라 시전불가능
        RETURN 0
    ENDIF
ELSE
    ;이스킬 없음, 시전불가
    RETURN 0
ENDIF

;해당 문자열을 변수명으로 하는 TCVAR 내 값을 반환함.
@ARG능력치(ARG,능력치)
#FUNCTION
#DIMS 능력치
#DIM 결과값
SELECTCASE 능력치
    CASE "힘"
        결과값 = TCVAR:ARG:T_힘
    CASE "지성"
        결과값 = TCVAR:ARG:T_지성
    CASE "속도"
        결과값 = TCVAR:ARG:T_속도
    CASE "인내"
        결과값 = TCVAR:ARG:T_인내
    CASE "운"
        결과값 = TCVAR:ARG:T_운
    CASE "체력"
        결과값 = TCVAR:ARG:T_체력
    CASE "최대체력"
        결과값 = TCVAR:ARG:T_최대체력
    CASE "공격력"
        결과값 = TCVAR:ARG:T_공격력
    CASE "방어력"
        결과값 = TCVAR:ARG:T_방어력
    CASE "물리내성"
        결과값 = TCVAR:ARG:T_물리내성
    CASE "화염내성"
        결과값 = TCVAR:ARG:T_화염내성
    CASE "빙결내성"
        결과값 = TCVAR:ARG:T_빙결내성
    CASE "전격내성"
        결과값 = TCVAR:ARG:T_전격내성
    CASE "질풍내성"
        결과값 = TCVAR:ARG:T_질풍내성
    CASE "마력내성"
        결과값 = TCVAR:ARG:T_마력내성
    CASE "수격내성"
        결과값 = TCVAR:ARG:T_수격내성
    CASE "정신내성"
        결과값 = TCVAR:ARG:T_정신내성
    CASEELSE
        THROW 상정하지 않은 능력치 %능력치%
ENDSELECT

RETURNF 결과값

@ARG반대능력치(ARG,능력치)
#FUNCTION
#DIMS 능력치

IF ARG == T_턴주인
    RETURNF ARG능력치(T_턴반대, 능력치)
ELSEIF ARG == T_턴반대
    RETURNF ARG능력치(T_턴주인, 능력치)
ENDIF
