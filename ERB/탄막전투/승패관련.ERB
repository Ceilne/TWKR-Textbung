;머리가 아파서 구현하기 힘듭니다 하 배고파

@T_승패처리(ARG)
;승패를 처리하는 함수
SELECTCASE ARG
    CASE 0
        ;타겟 패배
        SETCOLOR 0xFF0000
        PRINTFORML 인생의 승리자 : %CALLNAME:MASTER%
        SETCOLOR 0x29088A
        PRINTFORML 패배자 : %CALLNAME:TARGET%
        RESETCOLOR
        CALL 종속도상승(ARG, 5)
        CALL T_패자처리(TARGET,MASTER)
        CALL 스킬강탈
    CASE 1
        ;무승부
        PRINTFORML 무승부!
        CALL 노말라이제이션(ARG, 5)
    CASE 2
        ;아나타 패배
        SETCOLOR 0xFF0000
        PRINTFORML 인생의 승리자 : %CALLNAME:TARGET%
        SETCOLOR 0x29088A
        PRINTFORML 패배자 : %CALLNAME:MASTER%
        RESETCOLOR
        CALL 주도도상승(ARG, 5)
        CALL T_패자처리(MASTER,TARGET)
ENDSELECT
RESETCOLOR

@T_패자처리(ARG,ARG:1)
#DIM DYNAMIC 검사용변수
;ARG = 패배자
;ARG:1 = 승리자
IF 항복함 == 1
    ;아무일도 없다
ELSE
    ;항복한게 아니라 피통 0 되서 끝남
    IF 고로시 == 1
        IF ARG == TARGET
            ;타겟이 쓰러짐
            PRINTFORML %T_턴반대는% 바닥에 쓰러졌다...
            $타겟쓰러짐조건루프
            PRINTFORML 어쩔까?
            PRINTFORML [0] 일으켜준다
            PRINTFORML [1] 뭔가 한다
            PRINTFORML [2] Coup De Grace
            $타겟쓰러짐입력루프
            INPUT
            IF RESULT == 0
                ;살려줌, 호감 신뢰 증가
                PRINTFORML 쓰러진 %타겟을% 일으켜 다독여주었다...
                CFLAG:ARG:호감도+= 500
                CFLAG:ARG:신뢰도+= 250
                RETURN 1
            ELSEIF RESULT == 1
                PRINTFORML 뭘 할까??
                PRINTFORML [0] 돈을 빼앗는다 (%ARG의% 소지금:{CFLAG:ARG:소지금})
                PRINTFORML [1] 범한다
                SIF !TALENT:ARG:당신의노예
                    PRINTFORML [2] 노예로 삼는다
                PRINTFORML [999] 역시 관둘래
                WHILE 1
                    INPUT
                    IF RESULT == 0
                        ;약탈
                        IF CFLAG:ARG:소지금 == 0
                            SETCOLOR 0xFF0000
                            PRINTFORMW %ARG는% 지금 빈털털이다...
                            RESETCOLOR
                        ELSE
                            PRINTFORML %ARG는% 가진돈을 몽땅 빼앗았다...
                            MONEY += CFLAG:ARG:소지금
                            CFLAG:ARG:소지금 = 0
                            BREAK
                        ENDIF
                    ELSEIF RESULT == 1
                        ;우후후모드 온, 억지로 범하고 있는 플래그를 찾을수 있다면 넣겠는데 못찾겠음
                        CFLAG:ARG:우후후 = 1
                        BREAK
                    ELSEIF RESULT == 2&&!TALENT:ARG:당신의노예
                        ;노예화
                        PRINTFORML %ARG는% 잠시 머뭇거리더니 고개를 끄덕였다...
                        CALL 종속도상승(ARG, 200)
                        PRINTFORML %ARG는% %마스터의% [노예]가 되었습니다
                        TALENT:ARG:당신의노예 = 1
                        TALENT:ARG:주인 = 0
                        FOR LOCAL, 1, CHARANUM
                            ;주인이 몇 명 있는가 검사
                            SIF TALENT:LOCAL:주인 == 1
                                검사용변수++
                        NEXT
                        ;한명도 없으면 노예상태는 탈출
                        SIF 검사용변수 == 0
                            TALENT:MASTER:노예 = 0
                        BREAK
                    ELSEIF RESULT == 999
                        GOTO 타겟쓰러짐조건루프
                    ENDIF
                WEND
                RETURN 1
            ELSEIF RESULT == 2
                ;대사처리 좀더 해야한다
                PRINTFORML 쓰러진 %타겟에게% 마지막 일격을 날렸다!
                PRINTFORML %타겟은% 죽었다...
                CALL T_타겟고로시
                RETURN 1
            ELSE
                GOTO 타겟쓰러짐입력루프
            ENDIF
        ELSEIF ARG == MASTER
            ;마스터가 쓰러짐
            ;다른 이벤트도 넣을법 하지만 일단은 사망만 넣어둔다
            CALL T_마스터사망
        ENDIF
    ELSE
        ;뭐, 일반모드는 그냥 노는거니 가볍게 대화나 하고 끝나는거다
        ;일단 임시 대사, 나중에 더 쓸 필요 있다
        IF ARG == TARGET
            ;타겟이 쓰러짐
            PRINTFORML %타겟은% 헤롱헤롱한채로 바닥에 쓰러져 있다...
        ELSEIF ARG == MASTER
            PRINTFORML %타겟은% 쓰러진 %마스터를% 보며 의기양양한 얼굴이다...
        ENDIF
    ENDIF
ENDIF

@T_타겟고로시
IF TALENT:TARGET:봉래인 == 0
    TALENT:TARGET:사망 = 1
    CFLAG:TARGET:현재위치 = SUKIMA()
    PRINTFORMW ★☆★%타겟은% 사라졌습니다★☆★
ELSE
    PRINTFORML ..만 봉래인인 %타겟은% 죽지 않고 다시 재생했다...
    PRINTFORMW 하지만 지쳐서 더는 움직일수 없는 모양이다
    BASE:TARGET:체력 = -1000
ENDIF

@T_마스터사망
;좀더 대사처리 필요함, 저장 불러오기 기능도
PRINTFORML %타겟은% 쓰러진 %마스터에게% 마지막 일격을 날렸다...
PRINTFORMW .........
PRINTFORMW ......
PRINTFORMW ...
PRINTFORMW ★☆★%마스터는% 사라졌습니다★☆★
PRINTW
QUIT


@스킬강탈(ARG)
#DIM DYNAMIC 스킬번호
#DIM DYNAMIC 다시뽑기카운터
#DIMS DYNAMIC 스킬명
;승리시의 대상의 스킬을 하나 혹은 전부 가져오는 함수
IF 고로시 == 1
    FOR LOCAL, 500, T_스킬한계값
        IF TALENT:ARG:LOCAL == 1
            IF TALENT:MASTER:LOCAL == 1
                CONTINUE
            ELSE
                TALENT:MASTER:LOCAL = 1
                CALLFORMF T_SKILL{LOCAL}_STR("스킬명")
                PRINTFORML %조사처리(스킬명,"을")% 획득했다
            ENDIF
        ENDIF
    NEXT
ELSE
    $다시뽑기
    CALL RANDOM_PICK_T_SKILL(ARG)
    IF TALENT:MASTER:(SKILL_ARRAY:스킬번호) == 1&&다시뽑기카운터 < 10
        다시뽑기카운터++
        GOTO 다시뽑기
    ELSE
        TALENT:MASTER:(SKILL_ARRAY:스킬번호) = 1
        CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
        PRINTFORML %조사처리(스킬명,"을")% 획득했다
    ENDIF
ENDIF

@T_항복(ARG)
#DIM DYNAMIC 검사용변수
;항복 시도 처리
;타겟의 항복을 수락 = RETURN 3
;타겟의 항복을 거절 = RETURN 2
;플레이어의 항복을 수락 = RETURN 1
;플레이어의 항복을 거절 = RETURN 0
IF 고로시 == 1
    IF ARG == MASTER
        ;나중에 추가할것 : 정말정말 못이길거 같을게 확실할경우 같은 특정 상황에서 항복 수락
        ;그런 경우엔 마스터가 [노예]소질을 얻는등의 일을 부여하거나 돈을 뺏기거나 하는 패널티를 부여한다
        ;지금은 일단 무조건 안돼게
        CALL 시스템처리(1000)
        IF RESULT == 1
            ;강제 성공루트
            PRINTFORML %마스터의% 말에 %T_턴반대는% 얼결에 받아들이고 말았다...
            항복함 = 1
            RETURN 1
        ENDIF
        PRINTFORML %T_턴반대는% 들은 채도 안하고 있다...
        RETURN 0
    ELSEIF ARG == TARGET  
        PRINTFORML %T_턴반대는% 숨을 헐떡이며 항복 의사를 표했다...
        CALL 시스템처리(1001)
        IF RESULT == 1
            ;강제 성공루트
            PRINTFORMW %T_턴반대의% 말에 %마스터는% 얼결에 받아들이고 말았다...
            항복함 = 2
            RETURN 3
        ENDIF
        $조건루프
        PRINTFORML 어쩔까?
        PRINTFORML [0] 받아들인다
        PRINTFORML [1] 조건이 있다
        PRINTFORML [2] 그런거 없다
        $항복루프
        INPUT
        IF RESULT == 0
            ;조건없이 받아들여줌, 호감 신뢰 증가
            CFLAG:ARG:호감도+= 300
            CFLAG:ARG:신뢰도+= 150
            항복함 = 2
            RETURN 3
        ELSEIF RESULT == 1
            PRINTFORML 무슨 조건?
            PRINTFORML [0] 돈을 줘 (%ARG의% 소지금:{CFLAG:ARG:소지금})
            PRINTFORML [1] 함 대줘
            SIF !TALENT:ARG:당신의노예
                PRINTFORML [2] 너 내 노예가 되라
            PRINTFORML [999] 역시 관둘래
            WHILE 1
                INPUT
                IF RESULT == 0
                    ;약탈
                    IF CFLAG:ARG:소지금 == 0
                        SETCOLOR 0xFF0000
                        PRINTFORMW %ARG는% 지금 빈털털이다...
                        RESETCOLOR
                    ELSE
                        PRINTFORML %ARG는% 가진돈을 몽땅 내놓았다...
                        MONEY += CFLAG:ARG:소지금
                        CFLAG:ARG:소지금 = 0
                        BREAK
                    ENDIF
                ELSEIF RESULT == 1
                    ;우후후모드 온, 억지로 범하고 있는 플래그를 찾을수 있다면 넣겠는데 못찾겠음
                    CFLAG:ARG:우후후 = 1
                    BREAK
                ELSEIF RESULT == 2&&!TALENT:ARG:당신의노예
                    ;노예화
                    PRINTFORML %ARG는% 잠시 머뭇거리더니 고개를 끄덕였다...
                    CALL 종속도상승(ARG, 200)
                    PRINTFORML %ARG는% %마스터의% [노예]가 되었습니다
                    TALENT:ARG:당신의노예 = 1
                    TALENT:ARG:주인 = 0
                    FOR LOCAL, 1, CHARANUM
                        ;주인이 몇 명 있는가 검사
                        SIF TALENT:LOCAL:주인 == 1
                            검사용변수++
                    NEXT
                    ;한명도 없으면 노예상태는 탈출
                    SIF 검사용변수 == 0
                        TALENT:MASTER:노예 = 0
                    BREAK
                ELSEIF RESULT == 999
                    GOTO 조건루프
                ENDIF
            WEND
            항복함 = 2
            RETURN 3
        ELSEIF RESULT == 2
            PRINTFORML %마스터의% 말에 이를 악물은 %T_턴반대는% 마지막 힘을 쥐어짰다...!
            CALL T_회복(ARG, 500)
            항복불가 = 1
            RETURN 2
        ELSE
            GOTO 항복루프
        ENDIF
    ENDIF
ELSE
    ;일반모드는 항복이 무조건 성공한다
    IF ARG == MASTER
        PRINTFORML %마스터는% 두손을 들고 항복 의사를 전했다...
        항복함 = 1
        RETURN 1
    ELSEIF ARG == TARGET
        PRINTFORML %ARG는% 지쳤는지 숨을 헐떡이며 항복해왔다...
        항복함 = 2
        RETURN 3
    ENDIF
    
ENDIF
