@통합버프(대상,상태이상,S_ID,강도,지속시간)
#DIM DYNAMIC 대상
#DIM DYNAMIC 상태이상
#DIM DYNAMIC S_ID
#DIM DYNAMIC 강도
#DIM DYNAMIC 지속시간
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB남은시간:LOCAL == 0
        CALLF TB_RESET(LOCAL)
        T_버프:TB대상:LOCAL = 대상
        T_버프:TB내용:LOCAL = 상태이상
        T_버프:TBID:LOCAL = S_ID
        T_버프:TB값:LOCAL = 강도
        T_버프:TB남은시간:LOCAL = 지속시간
        BREAK
    ENDIF
NEXT
CALL TB_SIMPLER(MASTER)
CALL TB_SIMPLER(TARGET)
CALL 공방세팅(MASTER,TARGET)

@TB_RESET(ARG = -1)
;ARG번 버프를 해제하거나 모든 버프를 해제
#FUNCTION
IF ARG < -1||ARG > TB버프한계
    RETURNF -1
ENDIF
IF ARG == -1
    FOR LOCAL, 0, TB버프한계
        T_버프:TB대상:LOCAL = 0
        T_버프:TB내용:LOCAL = 0
        T_버프:TBID:LOCAL = 0
        T_버프:TB값:LOCAL = 0
        T_버프:TB남은시간:LOCAL = 0
    NEXT
ELSE
    T_버프:TB대상:ARG = 0
    T_버프:TB내용:ARG = 0
    T_버프:TBID:ARG = 0
    T_버프:TB값:ARG = 0
    T_버프:TB남은시간:ARG = 0
ENDIF
RETURNF 1

@TB_MINUS
;턴 종료시 모든 버프의 지속시간을 1 내리는 함수
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB남은시간:LOCAL != 0
        T_버프:TB남은시간:LOCAL --
        IF T_버프:TB내용:LOCAL == TCV_독
            CALL T_데미지((T_버프:TB대상:LOCAL),50,"독")
        ELSEIF T_버프:TB내용:LOCAL == TCV_출혈
            CALL T_데미지((T_버프:TB대상:LOCAL),(TCVAR:(T_버프:TB대상:LOCAL):T_최대체력/20),"출혈")
        ELSEIF T_버프:TB내용:LOCAL == TCV_저주
            SIF TB_SUM_REMAIN((T_버프:TB대상:LOCAL),TCV_저주) == 0
                CALL T_데미지((T_버프:TB대상:LOCAL),TCVAR:(T_버프:TB대상:LOCAL):T_최대체력,"저주")
        ENDIF
    ENDIF
NEXT

@TB_SIMPLER(ARG)
;ARG의 버프중, 겹치는걸 하나로 뭉치는 함수
#DIM DYNAMIC 기절시간
#DIM DYNAMIC 침묵시간
#DIM DYNAMIC 실명시간
#DIM DYNAMIC 혼란시간
#DIM DYNAMIC 면역시간
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB대상:LOCAL == ARG&&T_버프:TB남은시간:LOCAL > 0
        IF T_버프:TB내용:LOCAL == TCV_기절
            기절시간 += T_버프:TB남은시간:LOCAL
        ELSEIF T_버프:TB내용:LOCAL == TCV_침묵
            침묵시간 += T_버프:TB남은시간:LOCAL
        ELSEIF T_버프:TB내용:LOCAL == TCV_실명
            실명시간 += T_버프:TB남은시간:LOCAL
        ELSEIF T_버프:TB내용:LOCAL == TCV_혼란
            혼란시간 += T_버프:TB남은시간:LOCAL
        ELSEIF T_버프:TB내용:LOCAL == TCV_면역
            면역시간 += T_버프:TB남은시간:LOCAL
        ENDIF
    ENDIF
NEXT
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB대상:LOCAL == ARG&&T_버프:TB남은시간:LOCAL > 0
        SIF T_버프:TB내용:LOCAL == TCV_기절||T_버프:TB내용:LOCAL == TCV_침묵||T_버프:TB내용:LOCAL == TCV_실명||T_버프:TB내용:LOCAL == TCV_혼란||T_버프:TB내용:LOCAL == TCV_면역
            CALLF TB_RESET(LOCAL)
    ENDIF
NEXT
SIF 기절시간 > 0
    CALL 통합버프(ARG,TCV_기절,1,기절시간)
SIF 침묵시간 > 0
    CALL 통합버프(ARG,TCV_침묵,1,침묵시간)
SIF 실명시간 > 0
    CALL 통합버프(ARG,TCV_실명,1,실명시간)
SIF 혼란시간 > 0
    CALL 통합버프(ARG,TCV_혼란,1,혼란시간)
SIF 면역시간 > 0
    CALL 통합버프(ARG,TCV_면역,1,면역시간)

@TB_SUM_VALUE(대상, 상태이상,S_ID = -1)
;특정 상태이상의 값의 총 합을 구하는 함수
#FUNCTION
#DIM DYNAMIC 대상
#DIM DYNAMIC 상태이상
#DIM DYNAMIC S_ID
#DIM DYNAMIC 출력
FOR LOCAL, 0,TB버프한계
    IF T_버프:TB대상:LOCAL == 대상&&T_버프:TB내용:LOCAL == 상태이상
        IF S_ID == -1||T_버프:TBID:LOCAL == S_ID
            출력 += T_버프:TB값:LOCAL
        ENDIF
    ENDIF
NEXT
RETURNF 출력

@TB_SUM_REMAIN(대상, 상태이상,S_ID = -1)
;특정 상태이상의 남은시간의 총 합을 구하는 함수
#FUNCTION
#DIM DYNAMIC 대상
#DIM DYNAMIC 상태이상
#DIM DYNAMIC S_ID
#DIM DYNAMIC 출력
FOR LOCAL, 0,TB버프한계
    IF T_버프:TB대상:LOCAL == 대상&&T_버프:TB내용:LOCAL == 상태이상
        IF S_ID == -1||T_버프:TBID:LOCAL == S_ID
            출력 += T_버프:TB남은시간:LOCAL
        ENDIF
    ENDIF
NEXT
RETURNF 출력


@TB_효과처리(ARG)
;ARG의 버프중, 능력치 변동을 일으키는 모든 값을 더해서 반영해줌
#DIM DYNAMIC 변동량, 5
#DIM DYNAMIC 보정
;힘지인속운
변동량:0 = TB_SUM_VALUE(ARG,TCV_힘)
변동량:1 = TB_SUM_VALUE(ARG,TCV_지성)
변동량:2 = TB_SUM_VALUE(ARG,TCV_인내)
변동량:3 = TB_SUM_VALUE(ARG,TCV_속도)
변동량:4 = TB_SUM_VALUE(ARG,TCV_운)
TCVAR:ARG:T_힘 = 변동량:0 + CFLAG:ARG:힘
TCVAR:ARG:T_지성 = 변동량:1 + CFLAG:ARG:지성
TCVAR:ARG:T_인내 = 변동량:2 + CFLAG:ARG:인내
TCVAR:ARG:T_속도 = 변동량:3 + CFLAG:ARG:속도
TCVAR:ARG:T_운 = 변동량:4 + CFLAG:ARG:운
보정 = TB_SUM_VALUE(ARG,TCV_증폭) - TB_SUM_VALUE(ARG,TCV_약화)
TCVAR:ARG:T_공격력보정 = 보정
보정 = TB_SUM_VALUE(ARG,TCV_회피) - TB_SUM_VALUE(ARG,TCV_취약)
TCVAR:ARG:T_방어력보정 = 보정

@TB_TYPERESET(ARG,TYPE,NUM)
;ARG의 특정 타입 버프를 해제
#DIM DYNAMIC TYPE
#DIM DYNAMIC NUM
#DIM DYNAMIC NUM2
IF TYPE == -2
    IF NUM == -1
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                CALLF TB_RESET(LOCAL)
            ENDIF
        NEXT
    ELSE
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                CALLF TB_RESET(LOCAL)
                NUM2 ++
            ENDIF
            IF NUM2 >= NUM
                BREAK
            ENDIF
        NEXT
    ENDIF
ELSEIF TYPE == -1
    ;디버프만 해제
    IF NUM == -1
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                IF T_버프:TB내용:LOCAL == TCV_기절||T_버프:TB내용:LOCAL == TCV_침묵||T_버프:TB내용:LOCAL == TCV_실명||T_버프:TB내용:LOCAL == TCV_혼란||T_버프:TB내용:LOCAL == TCV_독||T_버프:TB내용:LOCAL == TCV_취약||T_버프:TB내용:LOCAL == TCV_출혈||T_버프:TB내용:LOCAL == TCV_저주
                    CALLF TB_RESET(LOCAL)
                ENDIF
            ENDIF
        NEXT
    ELSE
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                IF T_버프:TB내용:LOCAL == TCV_기절||T_버프:TB내용:LOCAL == TCV_침묵||T_버프:TB내용:LOCAL == TCV_실명||T_버프:TB내용:LOCAL == TCV_혼란||T_버프:TB내용:LOCAL == TCV_독||T_버프:TB내용:LOCAL == TCV_취약||T_버프:TB내용:LOCAL == TCV_출혈||T_버프:TB내용:LOCAL == TCV_저주
                    CALLF TB_RESET(LOCAL)
                    NUM2 ++
                ENDIF
            ENDIF
            IF NUM2 >= NUM
                BREAK
            ENDIF
        NEXT
    ENDIF
ELSE
    FOR LOCAL, 0, TB버프한계
        IF T_버프:TB대상:LOCAL == ARG&&T_버프:TB내용:LOCAL == TYPE
            CALLF TB_RESET(LOCAL)
            NUM2 ++
        ENDIF
        IF NUM2 >= NUM
            BREAK
        ENDIF
    NEXT
ENDIF
RETURN 1