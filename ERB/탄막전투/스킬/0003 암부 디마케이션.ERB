
;========================================================================
;각 함수별 역할 정리
;T_SKILL3 : 해당 스킬의 기본 정수 스텟 처리
;T_SKILL3_STR : 해당 스킬의 기본 문자열 스텟 처리
;T_SKILL3_COLOR : 상황에 따른 출력문 색깔 처리
;ONGOING_T_SKILL3 : 스킬 사용 이후 처리 (쿨타임 감소, 지속효과 등)
;AFTER_T_SKILL3 : 스킬 사용 직후 처리
;
;시전자 = T_턴주인, 상대방 = T_턴상대
;========================================================================
@T_SKILL3(ARGS, ARG)
#FUNCTION
;ARGS = 인자
;ARG = 사용자, 상대의 턴중 인터럽트 실행을 대비한 요소입니다, 인터럽트 실행시 피해량등의 수치에 능력치를 반영하기 위해서 입니다
;인터럽트 실행을 하지 않는 스킬의 경우 생략해도 좋습니다

#DIM 쿨타임
#DIM 지속시간
#DIM 명중률
#DIM 피해량
;-------------------
;성능 입력
;쿨타임 0 = 한 턴에 몇 번이라도, 쿨타임 1 = 한 턴에 한번, 쿨타임 2= 두 턴에 한번
;-------------------
쿨타임 = 10;스킬의 쿨타임을 넣으세요
지속시간 = 3;스킬의 효과가 지속되는 시간을 넣으세요
명중률 = 100; 몇% 확률로 명중하는지 쓰세요
피해량 =  5*ARG능력치(ARG, "지성")+5;얼마만큼의 데미지를 주는지 쓰세요, 스텟을 참고해도 좋습니다
;-------------------
SELECTCASE ARGS
CASE "쿨타임"
    ;쿨타임을 반환, 건들일 없다
    RETURNF 쿨타임
CASE "명중률"
    ;명중률을 반환함
    RETURNF 명중률
CASE "공격력"
    ;피해량을 반환, 공격력을 참조하려면 사용
    RETURNF 피해량
CASE "지속시간"
    RETURNF 지속시간
ENDSELECT

;문자열부
@T_SKILL3_STR(ARGS)
#FUNCTIONS
#DIMS 스킬명
#DIMS 타입
#DIMS 속성
;-------------------
;성능 입력
;-------------------
스킬명 '= "암부 「디마케이션」";스킬의 이름을 넣으세요
타입 '= "상태이상형";공격형/방어형/회복형/상태이상형/버프형
속성 '= "마력";물리/화염/빙결/전격/질풍/정신/마력/수격/고정
;-------------------
SELECTCASE ARGS
CASE "스킬명"
    RESULTS '= 스킬명
CASE "타입"
    RESULTS '= 타입
CASE "속성"
    ;피해속성을 반환
    RESULTS '= 속성
CASEELSE
    THROW 지정되지 않은 인자 %ARGS%
ENDSELECT
RETURNF RESULTS

;표시할 때 다른 색깔을 쓰고 싶은 경우 변경
@T_SKILL3_COLOR(ARGS)
;ARGS = 인자
SELECTCASE ARGS
CASE "색깔_시전가능"
    ;시전가능, 흰색으로 빛남
    RETURN 0xFFFFFF
CASE "색깔_시전불가"
    ;시전불가, 회색임
    RETURN 0x585858
ENDSELECT

@ONGOING_T_SKILL3(사용자, ARGS)
#DIM 사용자
#DIM 남은지속시간, 135 ;캐릭터별 지속시간 저장 배열
#DIM 남은쿨타임, 135 ;캐릭터별 지속시간 저장 배열
#DIM 피해량
#DIMS 속성

SELECTCASE ARGS
;스킬 발동시 남은 지속시간/쿨타임 처리 부분
CASE "쿨타임설정"
    남은쿨타임:사용자 = T_SKILL3("쿨타임")
CASE "지속시간설정"
    남은지속시간:사용자 = T_SKILL3("지속시간")
CASE "턴엔드"
    ;매 턴이 지날때마다 지속시간과 쿨타임처리를 하는 부분
    IF 남은지속시간:사용자 > 0
        남은지속시간:사용자--
        CALLFORM ONGOING_T_SKILL3(사용자, "지속효과발동")
    ENDIF
    SIF 남은쿨타임:사용자 > 0
        남은쿨타임:사용자--
CASE "남은지속시간"
    ;남은 지속시간 표시, 건들일 없다
    RETURN 남은지속시간:사용자
CASE "남은쿨타임"
    ;남은쿨타임을 반환, 건들일 없다
    RETURN 남은쿨타임:사용자
CASE "시스템체크"
    ;시스템 메시지에 따른 검사, 패시브 스킬등에 이용, 액티브는 딱히 쓸 일이 있나?

CASE "지속효과발동"
    ;지속적으로 효과가 발동하는 스킬의 경우, 여기에 효과를 입력
    IF 남은지속시간 > 0
        피해량 = T_SKILL3("공격력")
        속성 '= T_SKILL3_STR("속성")
        ;턴 엔드시엔 무조건 턴 주인이 TARGET이기 때문에...
        IF 사용자 == MASTER
            CALL T_데미지(TARGET, 피해량, 속성)
            CALL 상태이상부여(TARGET, TCV_실명, 75, 1)
        ELSE
            CALL T_데미지(MASTER, 피해량, 속성)
            CALL 상태이상부여(MASTER, TCV_실명, 75, 1)
        ENDIF
    ENDIF
ENDSELECT

@AFTER_T_SKILL3(사용자)
#DIM 사용자
#DIM 명중률
#DIM 피해량
#DIMS 속성

;쿨타임은 미리 설정함
CALL ONGOING_T_SKILL3(사용자, "쿨타임설정")

;명중 대응, 특정상황에서 명중률을 변동하고 싶으면 사용
IF TCVAR:사용자:T_실명&&RAND:2 == 0
    명중률 = T_SKILL1("명중률") / 2
ELSE
    명중률 = T_SKILL1("명중률")
ENDIF

IF 명중률 >= RAND:100 + 1
    ;명중!
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 암부 「디마케이션」
    PRINTFORML 어둠이 주위에 깔린다...
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------
    CALL ONGOING_T_SKILL3(사용자, "지속시간설정")
    RETURN 1
ELSE
    ;!감나빗!
    SETCOLOR 0xFF4444
    PRINTFORML 빗나감!
    RESETCOLOR
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 어둠이 주위에 깔리는가 싶었지만 너무 옅었다...
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------
    RETURN 0
ENDIF