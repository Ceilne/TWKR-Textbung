
;========================================================================
;각 함수별 역할 정리
;T_SKILL213 : 해당 스킬의 기본 정수 스텟 처리
;T_SKILL213_STR : 해당 스킬의 기본 문자열 스텟 처리
;T_SKILL213_COLOR : 상황에 따른 출력문 색깔 처리
;ONGOING_T_SKILL213 : 스킬 사용 이후 처리 (쿨타임 감소, 지속효과 등)
;AFTER_T_SKILL213 : 스킬 사용 직후 처리
;
;시전자 = T_턴주인, 상대방 = T_턴상대
;========================================================================
@T_SKILL213(ARGS, ARG)
#FUNCTION
;ARGS = 인자
;ARG = 사용자, 상대의 턴중 인터럽트 실행을 대비한 요소입니다, 인터럽트 실행시 피해량등의 수치에 능력치를 반영하기 위해서 입니다
;인터럽트 실행을 하지 않는 스킬의 경우 생략해도 좋습니다

#DIM 쿨타임
#DIM 지속시간
#DIM 명중률
#DIM 피해량
#DIM 타입
#DIM HP코스트
#DIM TSP코스트
;-------------------
;성능 입력
;쿨타임 0 = 한 턴에 몇 번이라도, 쿨타임 1 = 한 턴에 한번, 쿨타임 2= 두 턴에 한번
;-------------------
쿨타임 = 20
지속시간 = 5
명중률 = 60
피해량 =  30 * ARG능력치(T_시전자, "지성") + 200
타입 = 공격형 
TSP코스트 = 500
;여러 타입을 한꺼번에 입력하고자 하는 경우:
;타입 = 공격형 + 버프형
;과 같이 + 를 포함해 입력하시면 됩니다.
;-------------------
SELECTCASE ARGS
CASE "쿨타임"
    ;쿨타임을 반환, 건들일 없다
    RESULT = 쿨타임
CASE "명중률"
    ;명중률을 반환함
    RESULT = 명중률
CASE "공격력"
    ;피해량을 반환, 공격력을 참조하려면 사용
    RESULT = 피해량
CASE "지속시간"
    RESULT = 지속시간
CASE "타입"
    RESULT = 타입
CASE "HP코스트"
    RESULT = HP코스트
CASE "TSP코스트"
    RESULT = TSP코스트
ENDSELECT
RETURNF RESULT

;문자열부
@T_SKILL213_STR(ARGS)
#FUNCTIONS
#DIMS 스킬명
#DIMS 속성
;-------------------
;성능 입력
;-------------------
스킬명 = 산부「진실의 달」
속성 = 물리
;-------------------
SELECTCASE ARGS
CASE "스킬명"
    RESULTS '= 스킬명
CASE "속성"
    ;피해속성을 반환
    RESULTS '= 속성
CASEELSE
    THROW 지정되지 않은 인자 %ARGS%
ENDSELECT
RETURNF RESULTS

;표시할 때 다른 색깔을 쓰고 싶은 경우 변경
@T_SKILL213_COLOR(ARGS)
;ARGS = 인자
SELECTCASE ARGS
CASE "색깔_시전가능"
    ;시전가능, 빨간색으로 빛남
    RETURN 0xB9062F
CASE "색깔_시전불가"
    ;시전불가, 회색임
    RETURN 0x585858
ENDSELECT

@ONGOING_T_SKILL213(사용자, ARGS)
#DIM 사용자
#DIM 남은지속시간, 135 ;캐릭터별 지속시간 저장 배열
#DIM 남은쿨타임, 135 ;캐릭터별 지속시간 저장 배열
#DIM 피해량
#DIMS 속성
피해량 = T_SKILL213("공격력")
속성 '= T_SKILL213_STR("속성")
SELECTCASE ARGS
;스킬 발동시 남은 지속시간/쿨타임 처리 부분
CASE "쿨타임설정"
    남은쿨타임:사용자 = T_SKILL213("쿨타임")
CASE "지속시간설정"
    남은지속시간:사용자 = T_SKILL213("지속시간")
CASE "턴엔드"
    ;매 턴이 지날때마다 지속시간과 쿨타임처리를 하는 부분
    IF 남은지속시간:사용자 > 0
        남은지속시간:사용자--
        CALL ONGOING_T_SKILL213(사용자, "지속효과발동")
    ENDIF
    SIF 남은쿨타임:사용자 > 0
        남은쿨타임:사용자 --
CASE "남은지속시간"
    ;남은 지속시간 표시, 건들일 없다
    RETURN 남은지속시간:사용자
CASE "남은쿨타임"
    ;남은쿨타임을 반환, 건들일 없다
    RETURN 남은쿨타임:사용자
CASE "초기화"
    FOR LOCAL, 0, CHARANUM
        남은쿨타임:LOCAL = 0
        남은지속시간:LOCAL = 0
    NEXT
CASE "시스템체크"
    ;시스템 메시지에 따른 검사, 패시브 스킬등에 이용, 액티브는 딱히 쓸 일이 있나?

CASE "지속효과발동"
    ;지속적으로 효과가 발동하는 스킬의 경우, 여기에 효과를 입력
        IF 남은지속시간:사용자 > 0
            
            PRINTFORML 수많은 탄환이 %조사처리(CALLNAME:사용자반대(사용자),"를")% 덮쳤다
            CALL T_데미지(T_시전자반대, 피해량, 속성)
            IF RAND:5 == 0
                PRINTFORML 한 발 더 맞았다
                CALL T_데미지(T_시전자반대, 피해량, 속성)
            ENDIF
        ENDIF
ENDSELECT

@AFTER_T_SKILL213(사용자)
#DIM 사용자
#DIM 명중률
#DIM 피해량
#DIMS 속성

;쿨타임은 미리 설정함
CALL ONGOING_T_SKILL213(사용자, "쿨타임설정")

;명중 대응, 특정상황에서 명중률을 변동하고 싶으면 사용
IF TB_SUM_REMAIN(사용자,TCV_실명)&&RAND:2 == 0
    명중률 = T_SKILL213("명중률") / 2
ELSE
    명중률 = T_SKILL213("명중률")
ENDIF

IF 명중률 >= RAND:100 + 1
    ;명중!
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 수많은 탄환들의 안무는 이제 예술의 경지에 도달했다
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------

    피해량 = T_SKILL213("공격력")
    속성 '= T_SKILL213_STR("속성")
    CALL T_데미지(T_시전자반대, 피해량, 속성)

    CALL ONGOING_T_SKILL213(사용자, "지속시간설정")
    RETURN 1
ELSE
    ;!감나빗!
    SETCOLOR 0xFF4444
    PRINTFORML 빗나감!
    RESETCOLOR
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 가까스로 %T_턴반대의% 머리 위로 탄환이 지나갔다
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------
    RETURN 0
ENDIF

@HELP_T_SKILL213
;스킬의 설명을 출력하는 함수
;적당히 8줄 이하의 설명을 써두자
PRINTFORML 레이센의 스펠카드
PRINTFORML
PRINTFORML 수도 없이 쏟아지는 탄막을 보면 마치 불꽃놀이같다
PRINTFORML 예쁘다고 가만히 보고만 있지 말고 피하자
PRINTFORML
PRINTFORML
PRINTFORML
PRINTFORML

@IS_UTIMATE213
;TRYCALLFORM으로 호출되어 이 스킬의 습득을 억제하는 함수
;FUCTION으로 고치면 안됨, 1인경우 습득 억제, 0인 경우 습득 허가
RETURN 1
