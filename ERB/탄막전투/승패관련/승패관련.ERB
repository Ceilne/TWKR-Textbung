;머리가 아파서 구현하기 힘듭니다 하 배고파
@뉴탄막경험치바(ARG, ARG:1)
#DIM 백분율
백분율 = ((ARG:1)*20)/(1000+(ABL:ARG:레벨*50))
;색깔 선정

SELECTCASE 백분율
CASE IS >= 15
    SETCOLOR 0x19FF19
CASE IS >= 10
    SETCOLOR 0xFF8000
CASE IS >= 5
    SETCOLOR 0xFFFF00
CASEELSE
    SETCOLOR 0xFFFFFF
ENDSELECT
IF 백분율 >= 20
    PRINTFORM [
    PRINTFORM  L . E . V . E . L . U . P 
    PRINTFORM ]
ELSE
    PRINTFORM [
    REPEAT 백분율
        PRINTFORM *
    REND
    REPEAT 20 - 백분율
        PRINTFORM .
    REND
    PRINTFORM ]
ENDIF

RESETCOLOR

@경험치처리(ARG, ARG:1)
#DIM DYNAMIC 포인트
IF ARG == MASTER&&FLAG:조교전
    ARG:1 = ARG:1 * 2 + 250
ENDIF
CFLAG:ARG:경험치 += ARG:1

PRINTFORM %ARG의% EXP:
CALL 뉴탄막경험치바(ARG, CFLAG:ARG:경험치)
PRINTFORM ({CFLAG:ARG:경험치}/{1000+(ABL:ARG:레벨*50)})
PRINTFORML    +{ARG:1}

IF CFLAG:ARG:경험치 >= (1000+(ABL:ARG:레벨*50))
    포인트 = CFLAG:ARG:경험치/(1000+(ABL:ARG:레벨*50))
    CFLAG:ARG:경험치 -= 포인트 * (1000+(ABL:ARG:레벨*50))
    IF ARG == MASTER
        CALL 레벨업(포인트)
    ELSE
        PRINTFORML %ARG는% {포인트}레벨만큼 성장했다
        WHILE 1
            SELECTCASE RAND:13
                CASE 0
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:힘++
                CASE 1
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:지성++
                CASE 2
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:속도++
                CASE 3
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:인내++
                CASE 4
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:운++
                CASE 5
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:물리내성+= 5
                CASE 6
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:화염내성+= 5
                CASE 7
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:빙결내성+= 5
                CASE 8
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:전격내성+= 5
                CASE 9
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:질풍내성+= 5
                CASE 10
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:마력내성+= 5
                CASE 11
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:수격내성+= 5
                CASE 12
                    포인트--
                    ABL:ARG:레벨++
                    CFLAG:ARG:정신내성+= 5
            ENDSELECT
            IF 포인트 == 0
                RETURN 1
            ENDIF
        WEND
    ENDIF

ENDIF

@레벨업(ARG)
#DIM DYNAMIC 포인트
#DIM 연차
포인트 = ARG
연차 = 1
PRINTFORMW %마스터는% 그간의 전투로 인해 더욱 강해짐을 느꼈다!
WHILE 1
    IF 연차 > 포인트
        연차 = 1
    ENDIF
    PRINTFORML 어느 스탯을 올릴까? (남은 포인트:{포인트})
    PRINTFORML [1]  힘 {CFLAG:MASTER:힘} -> {CFLAG:MASTER:힘 + 1}
    PRINTFORML [2]  지성 {CFLAG:MASTER:지성} -> {CFLAG:MASTER:지성 + 1}
    PRINTFORML [3]  속도 {CFLAG:MASTER:속도} -> {CFLAG:MASTER:속도 + 1}
    PRINTFORML [4]  인내 {CFLAG:MASTER:인내} -> {CFLAG:MASTER:인내 + 1}
    PRINTFORML [5]  운 {CFLAG:MASTER:운} -> {CFLAG:MASTER:운 + 1}
    PRINTFORML [6]  물리 내성 {CFLAG:MASTER:물리내성} -> {CFLAG:MASTER:물리내성 + 5}
    PRINTFORML [7]  화염 내성 {CFLAG:MASTER:화염내성} -> {CFLAG:MASTER:화염내성 + 5}
    PRINTFORML [8]  빙결 내성 {CFLAG:MASTER:빙결내성} -> {CFLAG:MASTER:빙결내성 + 5}
    PRINTFORML [9]  전격 내성 {CFLAG:MASTER:전격내성} -> {CFLAG:MASTER:전격내성 + 5}
    PRINTFORML [10] 질풍 내성 {CFLAG:MASTER:질풍내성} -> {CFLAG:MASTER:질풍내성 + 5}
    PRINTFORML [11] 마력 내성 {CFLAG:MASTER:마력내성} -> {CFLAG:MASTER:마력내성 + 5}
    PRINTFORML [12] 수격 내성 {CFLAG:MASTER:수격내성} -> {CFLAG:MASTER:수격내성 + 5}
    PRINTFORML [13] 정신 내성 {CFLAG:MASTER:정신내성} -> {CFLAG:MASTER:정신내성 + 5}
    PRINTFORML [14] {연차}회 연속투자중
    INPUT
    IF RESULT == 1
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:힘++
        REND
    ELSEIF RESULT == 2
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:지성++
        REND
    ELSEIF RESULT == 3
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:속도++
        REND
    ELSEIF RESULT == 4
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:인내++
        REND
    ELSEIF RESULT == 5
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:운++
        REND
    ELSEIF RESULT == 6
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:물리내성+= 5
        REND
    ELSEIF RESULT == 7
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:화염내성+= 5
        REND
    ELSEIF RESULT == 8
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:빙결내성+= 5
        REND
    ELSEIF RESULT == 9
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:전격내성+= 5
        REND
    ELSEIF RESULT == 10
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:질풍내성+= 5
        REND
    ELSEIF RESULT == 11
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:마력내성+= 5
        REND
    ELSEIF RESULT == 12
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:수격내성+= 5
        REND
    ELSEIF RESULT == 13
        REPEAT 연차
        포인트--
        ABL:MASTER:레벨++
        CFLAG:MASTER:정신내성+= 5
        REND
    ELSEIF RESULT == 14
        SELECTCASE 연차
            CASE 1
                IF 포인트 >= 5
                    연차 = 5
                ELSE
                    연차 = 1
                ENDIF
            CASE 5
                IF 포인트 >= 10
                    연차 = 10
                ELSE
                    연차 = 1
                ENDIF
            CASE 10
                IF 포인트 >= 20
                    연차 = 20
                ELSE
                    연차 = 1
                ENDIF
            CASE 20
                IF 포인트 >= 50
                    연차 = 50
                ELSE
                    연차 = 1
                ENDIF
            CASE 50
                IF 포인트 >= 100
                    연차 = 100
                ELSE
                    연차 = 1
                ENDIF
            CASE 100
                연차 = 1
        ENDSELECT
    ENDIF
    IF 포인트 <= 0
        DRAWLINE
        PRINTFORML 레벨업 결과
        PRINTFORML  레  벨   {ABL:MASTER:레벨}
        PRINTFORML    힘     {CFLAG:MASTER:힘}
        PRINTFORML  지  성   {CFLAG:MASTER:지성}
        PRINTFORML  속  도   {CFLAG:MASTER:속도}
        PRINTFORML  인  내   {CFLAG:MASTER:인내}
        PRINTFORML    운      {CFLAG:MASTER:운}
        PRINTFORML 물리 내성  {CFLAG:MASTER:물리내성}
        PRINTFORML 화염 내성  {CFLAG:MASTER:화염내성}
        PRINTFORML 빙결 내성  {CFLAG:MASTER:빙결내성}
        PRINTFORML 전격 내성  {CFLAG:MASTER:전격내성}
        PRINTFORML 질풍 내성  {CFLAG:MASTER:질풍내성}
        PRINTFORML 마력 내성  {CFLAG:MASTER:마력내성}
        PRINTFORML 수격 내성  {CFLAG:MASTER:수격내성}
        PRINTFORML 정신 내성  {CFLAG:MASTER:정신내성}
        DRAWLINE
        RETURN 1
    ENDIF
    CLEARLINE 16
WEND


@T_승패처리(ARG)
#DIM DYNAMIC 마스터획득경험치
#DIM DYNAMIC 타겟획득경험치
;승패를 처리하는 함수
IF ABL:TARGET:레벨 > ABL:MASTER:레벨
    타겟획득경험치 = ((ABL:TARGET:레벨 / (ABL:TARGET:레벨 - ABL:MASTER:레벨 + 1)) *10)+1
    마스터획득경험치 = MAX(ABL:TARGET:레벨 - ABL:MASTER:레벨, 1)*50
ELSE
    타겟획득경험치 = MAX(ABL:TARGET:레벨 - ABL:MASTER:레벨, 1)*50
    마스터획득경험치 = ((ABL:MASTER:레벨 / (ABL:MASTER:레벨 - ABL:TARGET:레벨 + 1)) *10)+1
ENDIF
SELECTCASE ARG
    CASE 0
        ;타겟 패배
        IF 타겟획득경험치 > ABL:TARGET:레벨 * 100
            타겟획득경험치 = ABL:TARGET:레벨 * 100
        ENDIF
        SETCOLOR 0xFF0000
        PRINTFORML 인생의 승리자 : %CALLNAME:MASTER%
        SETCOLOR 0x29088A
        PRINTFORML 패배자 : %CALLNAME:TARGET%
        RESETCOLOR
        CALL 종속도상승(ARG, 5)
        CALL T_패자처리(TARGET,MASTER)
        CALL 스킬강탈
        CALL 경험치처리(MASTER, 마스터획득경험치)
        CALL 경험치처리(TARGET, 타겟획득경험치/4)
        SIF CFLAG:TARGET:탄막승부승리 == 0
	        CFLAG:TARGET:탄막승부승리 = 1
    CASE 1
        ;무승부
        PRINTFORML 무승부!
        CALL 노말라이제이션(ARG, 5)
        마스터획득경험치 = 50
        타겟획득경험치 = 50
        CALL 경험치처리(MASTER, 마스터획득경험치)
        CALL 경험치처리(TARGET, 타겟획득경험치)
    CASE 2
        IF 마스터획득경험치 > ABL:MASTER:레벨 * 100
            마스터획득경험치 = ABL:MASTER:레벨 * 100
        ENDIF
        ;아나타 패배
        SETCOLOR 0xFF0000
        PRINTFORML 인생의 승리자 : %CALLNAME:TARGET%
        SETCOLOR 0x29088A
        PRINTFORML 패배자 : %CALLNAME:MASTER%
        RESETCOLOR
        CALL 주도도상승(ARG, 5)
        CALL T_패자처리(MASTER,TARGET)
        
        CALL 경험치처리(TARGET, 타겟획득경험치)
        CALL 경험치처리(MASTER, 마스터획득경험치/4)
ENDSELECT
RESETCOLOR

@T_패자처리(ARG,ARG:1)
#DIM DYNAMIC 검사용변수
;ARG = 패배자
;ARG:1 = 승리자
IF 항복함 > 0
    ;아무일도 없다
ELSE
    ;항복한게 아니라 피통 0 되서 끝남
    IF 고로시 == 1
        IF ARG == TARGET
            ;타겟이 쓰러짐
            PRINTFORML %T_턴반대는% 바닥에 쓰러졌다...
            $타겟쓰러짐조건루프
            PRINTFORML 어쩔까?
            PRINTFORML [0] 일으켜준다
            PRINTFORML [1] 뭔가 한다
            PRINTFORML [2] Coup De Grace
            $타겟쓰러짐입력루프
            INPUT
            IF RESULT == 0
                ;살려줌, 호감 신뢰 극미량 증가, 종속도 많이 증가
                PRINTFORML 쓰러진 %타겟을% 일으켜 다독여주었다...
                CFLAG:ARG:호감도+= 50
                CFLAG:ARG:신뢰도+= 25
                CALL 종속도상승(ARG, 10)
                RETURN 1
            ELSEIF RESULT == 1
                PRINTFORML 뭘 할까??
                PRINTFORML [0] 돈을 빼앗는다 (%ARG의% 소지금:{CFLAG:ARG:소지금})
                PRINTFORML [1] 범한다
                SIF !TALENT:ARG:당신의노예
                    PRINTFORML [2] 노예로 삼는다
                PRINTFORML [999] 역시 관둘래
                WHILE 1
                    INPUT
                    IF RESULT == 0
                        ;약탈
                        IF CFLAG:ARG:소지금 == 0
                            SETCOLOR 0xFF0000
                            PRINTFORMW %ARG는% 지금 빈털털이다...
                            RESETCOLOR
                        ELSE
                            PRINTFORML %ARG는% 가진돈을 몽땅 빼앗겼다...
                            MONEY += CFLAG:ARG:소지금
                            CFLAG:ARG:소지금 = 0
                            BREAK
                        ENDIF
                    ELSEIF RESULT == 1
                        ;우후후모드 온, 억지로 범하고 있는 플래그를 찾을수 있다면 넣겠는데 못찾겠음
                        CFLAG:ARG:우후후 = 1
                        CFLAG:ARG:대주고말았다 = 1
                        TCVAR:ARG:패배강간 = 1
                        BREAK
                    ELSEIF RESULT == 2&&!TALENT:ARG:당신의노예
                        ;노예화
                        PRINTFORML %ARG는% 잠시 머뭇거리더니 고개를 끄덕였다...
                        CALL 종속도상승(ARG, 200)
                        PRINTFORML %ARG는% %마스터의% [노예]가 되었습니다
                        TALENT:ARG:당신의노예 = 1
                        TALENT:ARG:주인 = 0
                        FOR LOCAL, 1, CHARANUM
                            ;주인이 몇 명 있는가 검사
                            SIF TALENT:LOCAL:주인 == 1
                                검사용변수++
                        NEXT
                        ;한명도 없으면 노예상태는 탈출
                        SIF 검사용변수 == 0
                            TALENT:MASTER:노예 = 0
                        BREAK
                    ELSEIF RESULT == 999
                        GOTO 타겟쓰러짐조건루프
                    ENDIF
                WEND
                RETURN 1
            ELSEIF RESULT == 2
                ;대사처리 좀더 해야한다
                PRINTFORML 쓰러진 %타겟에게% 마지막 일격을 날렸다!
                PRINTFORML %타겟은% 죽었다...
                CALL T_타겟고로시
                RETURN 1
            ELSE
                GOTO 타겟쓰러짐입력루프
            ENDIF
        ELSEIF ARG == MASTER
            ;마스터가 쓰러짐
            ;다른 이벤트도 넣을법 하지만 일단은 사망만 넣어둔다
            CALL 주도도상승(TARGET, 5)
            IF (CFLAG:호감도+CFLAG:신뢰도-(100*MARK:반발각인)+(50*MARK:괘씸각인) + RAND:400)>= 1000
                IF ABL:친밀+ABL:순종 >= 15
                    PRINTFORML %타겟은% 쓰러진 %마스터를% 향해 다가와 다정하게 안아주고 있다...
                ELSEIF ABL:욕망+ABL:정액중독+ABL:새드끼+TALENT:섹프+TALENT:애욕 >= 20
                    PRINTFORML %타겟은% 바닥에서 헤롱헤롱하고 있는 %마스터를% 보곤 입맛을 다시며 다가왔다...
                    CALL KOJO_MESSAGE_SEND("EVENT", 10, TARGET, 1,0)
					CFLAG:TARGET:우후후 = 2
					IF !TCVAR:TARGET:발정 && !TCVAR:TARGET:춘약 && !GETBIT(CFLAG:기정사실 , 1)
						SETBIT CFLAG:TARGET:기정사실 , 1
						CALL COLORMESSAGE(@"%CALLNAME:TARGET%의 합의를 얻은", C_YELLOW, 2)
					ELSE
						CFLAG:TARGET:대주고말았다 = 1
					ENDIF
					CFLAG:MASTER:우후후 = 2
					TFLAG:102 = 3
					TFLAG:껴안기모드 = 0
					TFLAG:덮치기개시시간 = TIME + DAY * 1440
                ELSE
                    PRINTFORML %타겟은% 바닥에 쓰러진 %마스터를% 한심하다는듯 바라보곤 그대로 한대 걷어차버렸다...
                    BASE:MASTER:체력 -= 500
                ENDIF
            ELSE
                CALL T_마스터사망
            ENDIF
        ENDIF
    ELSE
        ;뭐, 일반모드는 그냥 노는거니 가볍게 대화나 하고 끝나는거다
        ;일단 임시 대사, 나중에 더 쓸 필요 있다
        IF ARG == TARGET
            ;타겟이 쓰러짐
            PRINTFORML %타겟은% 헤롱헤롱한채로 바닥에 쓰러져 있다...
        ELSEIF ARG == MASTER
            PRINTFORML %타겟은% 쓰러진 %마스터를% 보며 의기양양한 얼굴이다...
        ENDIF
    ENDIF
ENDIF

@T_타겟고로시
IF TALENT:TARGET:봉래인 == 0
    TALENT:TARGET:사망 = 1
    CFLAG:TARGET:현재위치 = SUKIMA()
    PRINTFORMW ★☆★%타겟은% 사라졌습니다★☆★
    습득한혼:TARGET++
ELSE
    PRINTFORML ..만 봉래인인 %타겟은% 죽지 않고 다시 재생했다...
    PRINTFORMW 하지만 지쳐서 더는 움직일수 없는 모양이다
    BASE:TARGET:체력 = -1000
ENDIF

@T_마스터사망
;좀더 대사처리 필요함, 저장 불러오기 기능도
PRINTFORML %타겟은% 쓰러진 %마스터에게% 마지막 일격을 날렸다...
PRINTFORMW .........
PRINTFORMW ......
PRINTFORMW ...
PRINTFORMW ★☆★%마스터는% 사라졌습니다★☆★
PRINTW
QUIT


@스킬강탈(ARG)
#DIM DYNAMIC 스킬번호
#DIM DYNAMIC 다시뽑기카운터
#DIMS DYNAMIC 스킬명
;승리시의 대상의 스킬을 하나 혹은 전부 가져오는 함수
IF 고로시 == 1
    FOR LOCAL, 500, T_스킬한계값
        IF TALENT:ARG:LOCAL == 1
            IF TALENT:MASTER:LOCAL == 1
                CONTINUE
            ELSE
                RESULT = 0
                TRYCALLFORM IS_UTIMATE{LOCAL}()
                IF RESULT == 1
                    TALENT:MASTER:LOCAL = 1
                    CALLFORMF T_SKILL{LOCAL}_STR("스킬명")
                    PRINTFORML %조사처리(스킬명,"을")% 획득했다
                ELSE
                    CONTINUE
                ENDIF
            ENDIF
        ENDIF
    NEXT
ELSE
    $다시뽑기
    CALL RANDOM_PICK_T_SKILL(ARG)
    IF TALENT:MASTER:(SKILL_ARRAY:스킬번호) == 1&&다시뽑기카운터 < 20
        다시뽑기카운터++
        GOTO 다시뽑기
    ELSE
        RESULT = 0
        TRYCALLFORM IS_UTIMATE{LOCAL}()
        IF RESULT == 1
            TALENT:MASTER:(SKILL_ARRAY:스킬번호) = 1
            IF HAVE_T_SKILL(TARGET,스킬번호) == 1
                CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
                PRINTFORML %조사처리(스킬명,"을")% 획득했다
            ENDIF
        ELSE
            
        ENDIF
    ENDIF
ENDIF

@T_항복(ARG)
#DIM DYNAMIC 검사용변수
;항복 시도 처리
;타겟의 항복을 수락 = RETURN 3
;타겟의 항복을 거절 = RETURN 2
;플레이어의 항복을 수락 = RETURN 1
;플레이어의 항복을 거절 = RETURN 0
IF 고로시 == 1
    IF ARG == MASTER
        ;나중에 추가할것 : 정말정말 못이길거 같을게 확실할경우 같은 특정 상황에서 항복 수락
        ;그런 경우엔 마스터가 [노예]소질을 얻는등의 일을 부여하거나 돈을 뺏기거나 하는 패널티를 부여한다
        ;지금은 일단 무조건 안돼게
        CALL T_시스템처리(1000)
        IF RESULT == 1
            ;강제 성공루트
            PRINTFORML %마스터의% 말에 %T_턴반대는% 얼결에 받아들이고 말았다...
            항복함 = 1
            RETURN 1
        ENDIF
        PRINTFORML %T_턴반대는% 들은 채도 안하고 있다...
        RETURN 0
    ELSEIF ARG == TARGET  
        PRINTFORML %타겟은% 숨을 헐떡이며 항복 의사를 표했다...
        CALL T_시스템처리(1001)
        IF RESULT == 1
            ;강제 성공루트
            PRINTFORMW %타겟의% 말에 %마스터는% 얼결에 받아들이고 말았다...
            항복함 = 2
            RETURN 3
        ENDIF
        $조건루프
        PRINTFORML 어쩔까?
        PRINTFORML [0] 받아들인다
        PRINTFORML [1] 조건이 있다
        PRINTFORML [2] 그런거 없다
        $항복루프
        INPUT
        IF RESULT == 0
            ;조건없이 받아들여줌, 호감 신뢰 증가
            CFLAG:ARG:호감도+= 300
            CFLAG:ARG:신뢰도+= 150
            항복함 = 2
            RETURN 3
        ELSEIF RESULT == 1
            PRINTFORML 무슨 조건?
            PRINTFORML [0] 돈을 줘 (%ARG의% 소지금:{CFLAG:ARG:소지금})
            PRINTFORML [1] 함 대줘
            SIF !TALENT:ARG:당신의노예
                PRINTFORML [2] 너 내 노예가 되라
            PRINTFORML [999] 역시 관둘래
            WHILE 1
                INPUT
                IF RESULT == 0
                    ;약탈
                    IF CFLAG:ARG:소지금 == 0
                        SETCOLOR 0xFF0000
                        PRINTFORMW %ARG는% 지금 빈털털이다...
                        RESETCOLOR
                    ELSE
                        PRINTFORML %ARG는% 가진돈을 몽땅 내놓았다...
                        MONEY += CFLAG:ARG:소지금
                        CFLAG:ARG:소지금 = 0
                        BREAK
                    ENDIF
                ELSEIF RESULT == 1
                    ;우후후모드 온, 억지로 범하고 있는 플래그를 찾을수 있다면 넣겠는데 못찾겠음
                    CFLAG:ARG:우후후 = 1
                    BREAK
                ELSEIF RESULT == 2&&!TALENT:ARG:당신의노예
                    ;노예화
                    PRINTFORML %ARG는% 잠시 머뭇거리더니 고개를 끄덕였다...
                    CALL 종속도상승(ARG, 200)
                    PRINTFORML %ARG는% %마스터의% [노예]가 되었습니다
                    TALENT:ARG:당신의노예 = 1
                    TALENT:ARG:주인 = 0
                    FOR LOCAL, 1, CHARANUM
                        ;주인이 몇 명 있는가 검사
                        SIF TALENT:LOCAL:주인 == 1
                            검사용변수++
                    NEXT
                    ;한명도 없으면 노예상태는 탈출
                    SIF 검사용변수 == 0
                        TALENT:MASTER:노예 = 0
                    BREAK
                ELSEIF RESULT == 999
                    GOTO 조건루프
                ENDIF
            WEND
            항복함 = 2
            RETURN 3
        ELSEIF RESULT == 2
            PRINTFORML %마스터의% 말에 이를 악물은 %T_턴반대는% 마지막 힘을 쥐어짰다...!
            CALL T_회복(ARG, 500)
            항복불가 = 1
            RETURN 2
        ELSE
            GOTO 항복루프
        ENDIF
    ENDIF
ELSE
    ;일반모드는 항복이 무조건 성공한다
    IF ARG == MASTER
        PRINTFORML %마스터는% 두손을 들고 항복 의사를 전했다...
        항복함 = 1
        RETURN 1
    ELSEIF ARG == TARGET
        PRINTFORML %ARG는% 지쳤는지 숨을 헐떡이며 항복해왔다...
        항복함 = 2
        RETURN 3
    ENDIF
    
ENDIF
