@조교전
PRINTL 당신은 조교전을 펼쳤다...

$표시루프
DRAWLINE
PRINTFORML 조교 포인트 : {조교포인트}p
DRAWLINE
PRINTFORML 무엇을 할까?
DRAWLINE
PRINTFORMLC [1] 연성
PRINTFORMLC [2] 제물
PRINTFORMLC [3] 서비스
PRINTL
PRINTFORMLC [4] 포인트 구매
PRINTL
DRAWLINE
PRINTL [999] 돌아간다
PRINTL
$LOOP
INPUT
IF RESULT == 999
    RETURN 1
ELSE
    IF RESULT == 1
        CALL 연성시스템
    ELSEIF RESULT == 2
        CALL 제물시스템
    ELSEIF RESULT == 3
        CALL 조교전서비스
    ELSEIF RESULT == 4
        CALL 조교포인트구매
    ELSE
        GOTO LOOP
    ENDIF
ENDIF
GOTO 표시루프

@조교포인트구매
#DIM DYNAMIC 구매타입
#DIM DYNAMIC 쓰는돈
DRAWLINE
PRINTFORML 현금 50만원, 혹은 카리스마 2000을 소비해 조교포인트 1을 획득할수 있습니다
IF MONEY < 500000&&MONEY:2 < 2000
    PRINTFORMW 소지금이 부족합니다
    RETURN
ENDIF
PRINTFORML [0] 돈으로 교환 (소지:{MONEY}\)
PRINTFORML [1] 카리스마로 교환 (소지:{MONEY:2}CM)
PRINTL [2] 돌아간다
INPUT
IF RESULT == 2
    RETURN
ELSEIF RESULT == 0
    IF MONEY < 500000
        PRINTFORMW 소지금이 부족합니다
        RESTART
    ENDIF
    구매타입 = 1
ELSEIF RESULT == 1
    IF MONEY:2 < 2000
        PRINTFORMW 소지금이 부족합니다
        RESTART
    ENDIF
    구매타입 = 2
ELSE
    RETURN
ENDIF
PRINTFORML 몇 포인트 만큼 환전하시겠습니까?
INPUT
IF RESULT <1
    RETURN
ELSE
    IF 구매타입 == 1
        IF RESULT*500000 > MONEY
            RESULT = MONEY/500000
        ENDIF
        쓰는돈 = RESULT*500000
        MONEY -= 쓰는돈
        조교포인트 += RESULT
        PRINTFORMW {쓰는돈}원 사용되어 {RESULT}p 입금되었습니다
    ELSE
        IF RESULT*2000 > MONEY:2
            RESULT = MONEY:2/2000
        ENDIF
        쓰는돈 = RESULT*2000
        MONEY:2 -= 쓰는돈
        조교포인트 += RESULT
        PRINTFORMW {쓰는돈}CM 사용되어 {RESULT}p 입금되었습니다
    ENDIF
ENDIF