

@뉴탄막전투(ARG,ARG:1)
;ARG - 내 번호
;ARG:1 - 상대방 번호
CALL 뉴탄막전투초기화(ARG,ARG:1)
CALL 뉴탄막루틴(ARG, ARG:1)

SELECTCASE RESULT
    CASE -2 ;ARG의 항복
    CASE -1 ;ARG의 패배
    CASE 0 ;무승부
    CASE 1 ;ARG의 승리
ENDSELECT

@뉴탄막전투초기화(ARG,ARG:1)
#DIM 대상
;일반적으로 ARG = MASTER
;일반적으로 ARG:1 = TARGET

FOR LOCAL, 700, 800 ;현재 뉴탄막 내에서 TCVAR를 사용하고 있는 범위
    ;뉴탄막 내 모든 TCVAR 0으로 초기화
    CVARSET TCVAR, LOCAL
NEXT

REPEAT 2
    대상 = ARG:COUNT
    TCVAR:대상:T_체력 = BASE:대상:체력+MIN((10*CFLAG:대상:인내),1000)
    TCVAR:대상:T_최대체력 = TCVAR:대상:T_체력
    TCVAR:대상:T_힘 = CFLAG:대상:힘
    TCVAR:대상:T_지성 = CFLAG:대상:지성
    TCVAR:대상:T_인내 = CFLAG:대상:인내
    TCVAR:대상:T_속도 = CFLAG:대상:속도
    TCVAR:대상:T_운 = CFLAG:대상:운
    TCVAR:대상:T_물리내성 = CFLAG:대상:물리내성
    TCVAR:대상:T_화염내성 = CFLAG:대상:화염내성
    TCVAR:대상:T_빙결내성 = CFLAG:대상:빙결내성
    TCVAR:대상:T_전격내성 = CFLAG:대상:전격내성
    TCVAR:대상:T_질풍내성 = CFLAG:대상:질풍내성
    TCVAR:대상:T_마력내성 = CFLAG:대상:마력내성
    TCVAR:대상:T_수격내성 = CFLAG:대상:수격내성
    TCVAR:대상:T_정신내성 = CFLAG:대상:정신내성
REND
CALL 공방세팅(ARG, ARG:1)

;----
;기타변수 초기화 
;----


;----

@공방세팅(ARG, ARG:1)
#DIM 세팅대상
#DIM 공격력
#DIM 방어력

;ARG = 대상1, ARG:1 = 대상2
FOR LOCAL, 0, 2
    세팅대상 = ARG:LOCAL
    공격력 = (TCVAR:세팅대상:T_힘*20)+ (TCVAR:세팅대상:T_속도*5)+(TCVAR:세팅대상:T_공격력보정)
    방어력 = (TCVAR:세팅대상:T_속도*5)+ (TCVAR:세팅대상:T_인내*15)+(TCVAR:세팅대상:T_운)+(TCVAR:세팅대상:T_방어력보정)

    SELECTCASE ABL:세팅대상:전투능력
        CASE 0
            TIMES 공격력, 1.0
            TIMES 방어력, 1.0
        CASE 1
            TIMES 공격력, 1.3
            TIMES 방어력, 1.3
        CASE 2
            TIMES 공격력, 1.6
            TIMES 방어력, 1.6
        CASE 3
            TIMES 공격력, 1.9
            TIMES 방어력, 1.9
        CASE 4
            TIMES 공격력, 2.2
            TIMES 방어력, 2.2
        CASE 5
            TIMES 공격력, 2.5
            TIMES 방어력, 2.5
        CASE 6
            TIMES 공격력, 2.7
            TIMES 방어력, 2.7
        CASE 7
            TIMES 공격력, 2.9
            TIMES 방어력, 2.9
        CASEELSE
            TIMES 공격력, 3.0
            TIMES 방어력, 3.0
    ENDSELECT

    IF TCVAR:세팅대상:T_취약
        TIMES 방어력, 0.5
    ENDIF
    ;MASTER나 TARGET일때의 추가처리
    ;차이가 필요하다면 SELECTCASE 전투능력을 여기 안으로 옮겨오면 됨
    IF 세팅대상 == MASTER
    ELSEIF 세팅대상 == TARGET
        IF 고로시 == 1
            TIMES TCVAR:세팅대상:T_공격력, 2.0
            TIMES TCVAR:세팅대상:T_방어력, 2.0
        ENDIF
    ELSE
        THROW 이 함수는 MASTER, TARGET 이외의 경우를 상정하지 않았습니다.
    ENDIF
    TCVAR:세팅대상:T_공격력 = 공격력
    TCVAR:세팅대상:T_방어력 = 방어력
NEXT
RETURN 0

@뉴탄막루틴(대상1, 대상2)
#DIM 대상1 ;플레이어
#DIM 대상2 ;상대방
#DIM 턴넘김 ;0이면 턴이 넘어감

;RETURN 값에 따른 구분
;-2 = 대상1의 항복
;-1 = 대상1의 패배
;0 = 무승부
;1 = 대상1의 승리

$뉴탄막루틴시작

;---------
;내(대상1) 턴
;---------
CALL T_턴처리(대상1, 대상2)
턴넘김 = RESULT
IF 턴넘김 == 1
    CALL 뉴탄막커맨드창
    WHILE 1 ;$내턴입력
    INPUT
    SELECTCASE RESULT
        CASE 100
            ;공격 ;RETURN 0:회피 1:공격성공
            CALL T_COM공격한다
            BREAK
        CASE 101
            ;스킬사용
            IF TCVAR:대상1:T_침묵
                PRINTW 침묵중이라 스킬을 시전할수 없다!
            ELSE
                ;RETURN 0:시전실패 1:시전성공
                CALL T_COM스킬사용
                SIF RESULT == 1
                    BREAK
            ENDIF
        CASE 102
            ;아이템사용
        CASE 200
            ;회피
            CALL T_COM회피한다(대상1)
            BREAK
        CASE 201
            ;휴식
            ;일단은 아무처리 안함
        CASE 999
            ;항복
            RETURN -2
        CASEELSE
    ENDSELECT
    WEND
ENDIF

;승부처리
;일단 체력만 기준으로 봄
;무승부 처리
IF TCVAR:대상1:T_체력 <= 0 && TCVAR:대상2:T_체력 <= 0
    RETURN 0
;대상1 승리
ELSEIF TCVAR:대상2:T_체력 <= 0
    RETURN 1
ENDIF
;-----------------
;--------
;적(대상2) 턴
;--------
DRAWLINE
PRINTL
PRINTW 적턴 시작
PRINTL
CALL T_턴처리(대상2, 대상1)
턴넘김 = RESULT ;AI가 기절/혼란중인 경우 T_턴처리에서 처리됨
IF 턴넘김 == 1
    ;AI 활동영역
    CALL 사고패턴결정(대상2)
    PRINTL
ENDIF

DRAWLINE
PRINTL 턴 엔드
DRAWLINE
;-------
CALL T_턴엔드
;PRINTW

;승부처리
;일단 체력만 기준으로 봄
;무승부 처리
IF TCVAR:대상1:T_체력 <= 0 && TCVAR:대상2:T_체력 <= 0
    RETURN 0
;대상2 승리
ELSEIF TCVAR:대상1:T_체력 <= 0
    RETURN -1
ENDIF

GOTO 뉴탄막루틴시작

@T_턴처리(ARG, ARG:1)
;RETURN = 1이면 턴주인 턴, 0이라면 턴 넘어감
T_턴주인 = ARG 
T_턴반대 = ARG:1

CALL 버프감소처리(ARG)

CALL 공방세팅(ARG, ARG:1)
CALL 뉴탄막상태창

;상태이상에 따른 종료처리
IF TCVAR:ARG:T_기절
    PRINTFORMW %ARG는% 기절중이라 행동할 수 없다!
    RETURN 0
ENDIF
IF TCVAR:ARG:T_혼란 &&RAND:2 == 0
    PRINTFORMW %ARG는% 혼란에 빠져 행동할수 없다!
    RETURN 0
ENDIF
RETURN 1

@T_턴엔드
FOR LOCAL,1,1000
    TRYCALLFORM ONGOING_T_SKILL{LOCAL}(T_턴주인,"턴엔드")
    TRYCALLFORM ONGOING_T_SKILL{LOCAL}(T_턴반대,"턴엔드")
NEXT
CALL 상태이상감소처리(T_턴주인)
CALL 상태이상감소처리(T_턴반대)
T_TIME++

@뉴탄막커맨드창
ALIGNMENT CENTER
PRINTFORM [100] 공격 한다
PRINT    
PRINTFORM [101] 스킬 사용
PRINT    
PRINTFORM [102] 아이템 사용
PRINTL
PRINTFORM [200] 회피에 집중
PRINT    
PRINTFORM [201] 휴식
PRINTL
PRINTFORM [999] 항복
PRINTL
DRAWLINE
ALIGNMENT LEFT


@T_데미지(ARG,데미지,속성)
;ARG = 공격받는 대상
#DIM 데미지
#DIMS 속성
#DIMS 속성내성STR
#DIM 속성내성값
PRINTFORM 피해량 : {데미지}
;대상에 따라 달라지는 보정 처리
IF ARG == MASTER
    SIF 고로시 == 1
        TIMES 데미지 , 1.5
ENDIF
SELECTCASE 속성
    CASE "물리", "화염", "빙결", "전격", "질풍", "수격", "마력", "정신"
        속성내성STR '= @"%속성%내성"
        속성내성값 = ARG능력치(ARG, 속성내성STR)

        ;속성취약시 음수이므로 빼면 증가, 속성내성시 양수이므로 빼면 감소
        데미지 = MAX((데미지 - 속성내성값), 0)
        IF 속성내성값 > 0 ;속성내성으로 데미지 감소
            PRINTFORM  -%속성%내성{속성내성값}
        ELSEIF 속성내성값 < 0 ;속성 취약으로 데미지 증가
            PRINTFORM  +%속성%취약{속성내성값 * -1}
        ENDIF

    CASE "고정"
        ;처리없음
    CASEELSE
        ;특수피해 처리용(독이나 출혈같은거)
        TCVAR:ARG:T_체력 -= 데미지
        PRINTL
        PRINTFORMW \@ ARG == MASTER ? # %ARG는% \@ {데미지}만큼의 %속성% 피해를 \@ ARG == TARGET ? 주었# 받았\@다!
        RETURN 1
ENDSELECT

IF TCVAR:ARG:T_방어력 >= 0
    데미지 -= TCVAR:ARG:T_방어력
    SIF TCVAR:ARG:T_방어력 > 0
        PRINTFORM  -방어력{TCVAR:ARG:T_방어력}
ELSE
    데미지 += TCVAR:ARG:T_방어력
    SIF TCVAR:ARG:T_방어력 < 0
        PRINTFORM  +방어불가{TCVAR:ARG:T_방어력 * -1}
ENDIF

;판정문장 출력 처리
PRINTL
;일반적인 공격
IF ARG == T_턴반대 && 크리티컬판정(TCVAR:T_턴주인:T_운)
    PRINTFORML 급소에 \@ ARG == MASTER ? 맞았# 맞혔\@다!
    데미지 += TCVAR:T_턴주인:T_운
    TIMES 데미지 , 1.5
;상태이상 등으로 인한 데미지
ELSEIF ARG == T_턴주인 && 크리티컬판정(TCVAR:T_턴반대:T_운)
    PRINTFORML 급소에 \@ ARG == MASTER ? 맞았# 맞혔\@다!
    데미지 += TCVAR:T_턴반대:T_운
    TIMES 데미지 , 1.5
ENDIF

IF 데미지 > 0
    TCVAR:ARG:T_체력 -= 데미지
    PRINTFORMW \@ ARG == TARGET ? %CALLNAME:ARG%에게 # \@ {데미지}만큼의 %속성% 피해를 \@ ARG == TARGET ? 주었# 받았\@다!
ELSEIF 데미지 <= -100
    PRINTFORMW \@ ARG == TARGET ? %타겟은% 가볍게 피해버렸다... # 가볍게 피했다 \@
ELSE
    PRINTFORMW \@ ARG == MASTER ? # %타겟은% 아슬아슬하게 탄을 \@ 피했다!
ENDIF

@크리티컬판정(ARG)
#FUNCTION
#DIM DYNAMIC 크리티컬
크리티컬 = MIN(ARG, 100)

IF 크리티컬 >= RAND:100 + 1
    ;명중
    RETURNF 1
ELSE
    ;빗나감
    RETURNF 0
ENDIF

@능력치세팅(ARG)
;190,요정
;191,봉래인
;192,비생물
;193,신령
;194,인간
;195,오니
;196,츠쿠모가미
;197,텐구
;
;일단 임시 세팅, 상세 수치는 나중에 자세히 쓴다
IF CFLAG:ARG:능력치세팅여부 == 0 && CFLAG:ARG:힘 == 0 && CFLAG:ARG:지성 == 0 &&CFLAG:ARG:속도 == 0&&CFLAG:ARG:인내 == 0&&CFLAG:ARG:운 == 0
    CFLAG:ARG:능력치세팅여부 == 1
    TRYCALLFORM 개별능력치세팅{ARG}
    IF RESULT == 1
        RETURN
    ENDIF

    IF TALENT:ARG:인간
        CFLAG:ARG:힘 = 15
        CFLAG:ARG:지성 = 15
        CFLAG:ARG:속도 = 15
        CFLAG:ARG:인내 = 15
        CFLAG:ARG:운 = 15
    ELSEIF TALENT:ARG:요정
        CFLAG:ARG:힘 = 10
        CFLAG:ARG:지성 = 10
        CFLAG:ARG:속도 = 10
        CFLAG:ARG:인내 = 10
        CFLAG:ARG:운 = 10
    ELSEIF TALENT:ARG:봉래인
        CFLAG:ARG:힘 = 15
        CFLAG:ARG:지성 = 15
        CFLAG:ARG:속도 = 15
        CFLAG:ARG:인내 = 25
        CFLAG:ARG:운 = 15
    ELSEIF TALENT:ARG:비생물
        CFLAG:ARG:힘 = 5
        CFLAG:ARG:지성 = 15
        CFLAG:ARG:속도 = 15
        CFLAG:ARG:인내 = 35
        CFLAG:ARG:운 = 15
    ELSEIF TALENT:ARG:신령
        CFLAG:ARG:힘 = 80
        CFLAG:ARG:지성 = 80
        CFLAG:ARG:속도 = 80
        CFLAG:ARG:인내 = 80
        CFLAG:ARG:운 = 80
    ELSEIF TALENT:ARG:오니
        CFLAG:ARG:힘 = 65
        CFLAG:ARG:지성 = 65
        CFLAG:ARG:속도 = 65
        CFLAG:ARG:인내 = 65
        CFLAG:ARG:운 = 65
    ELSEIF TALENT:ARG:츠쿠모가미
        CFLAG:ARG:힘 = 10
        CFLAG:ARG:지성 = 15
        CFLAG:ARG:속도 = 15
        CFLAG:ARG:인내 = 40
        CFLAG:ARG:운 = 15
    ELSEIF TALENT:ARG:텐구
        CFLAG:ARG:힘 = 45
        CFLAG:ARG:지성 = 45
        CFLAG:ARG:속도 = 50
        CFLAG:ARG:인내 = 45
        CFLAG:ARG:운 = 45
    ELSE
        CFLAG:ARG:힘 = 35
        CFLAG:ARG:지성 = 35
        CFLAG:ARG:속도 = 35
        CFLAG:ARG:인내 = 35
        CFLAG:ARG:운 = 35
    ENDIF
ENDIF

@상태이상부여(ARG, 상태이상, 명중률, 지속시간)
;ARG = 상대
#DIM 상태이상 ;TCVAR 내 지속시간번호 ;T_특수지속시간_ID = 720, 끝: 769
#DIM 명중률
#DIM 지속시간

IF 명중률 >= RAND:100 + 1
    SIF !INRANGE(상태이상, T_특수지속시간_ID, (T_특수지속시간_ID + 29)) ;버프 지속시간 제외
        THROW 지속시간ID에 포함되지 않은 상태이상번호 {상태이상}
    TCVAR:ARG:상태이상 = 지속시간
    RETURN 1
ELSE
    RETURN 0
ENDIF

@능력치감소(ARG, 능력치, 감소량, 명중률, 지속시간)
;현재 능력치 = T_기본수치_ID, , 지속시간 = T_기본수치_ID + 20
;ARG = 상대
#DIM 능력치 ;기본수치번호 ;현재 TCVAR:700~709(T_기본수치_ID) 
#DIM 감소량 ;현재 능력치보정ID = T_기본수치_ID + 10
#DIM 명중률
#DIM 지속시간

IF 명중률 >= RAND:100 + 1
    IF INRANGE(능력치, T_기본수치_ID, (T_기본수치_ID + 9))
        ;감소량이 해당 능력치보다 커지는 것을 방지
        감소량 = MIN(TCVAR:ARG:능력치, 감소량)
        TCVAR:ARG:(T_특수지속시간 + (능력치 - T_기본수치_ID)) = 지속시간
        TCVAR:ARG:능력치 -= 감소량
        TCVAR:ARG:(능력치 + 10) += 감소량
    ELSE
        THROW 잘못된 능력치ID {능력치}
    ENDIF
ELSE
    RETURN 0
ENDIF

;============================================
;현재 T_특수지속시간_ID = TCVAR:720~769
;TCVAR:720~729 = 기본수치보정(감소) 지속시간
;TCVAR:730~749 = 상태이상 지속시간
;TCVAR:750~769 = 버프 지속시간
;------------------------------------------
;보정량 관련 ID번호
;기본수치보정량 = TCVAR:710~719
;버프보정량 = TCVAR:770~789
;============================================
@상태이상감소처리(ARG)
;ARG = 대상
#DIM 상태이상번호

FOR 상태이상번호, T_특수지속시간_ID, (T_특수지속시간_ID + 30) ;버프 지속시간 제외
    ;지속중이 아닌 경우 통과
    IF TCVAR:ARG:상태이상번호 == 0
        CONTINUE
    ;지속시간 감소
    ELSEIF TCVAR:ARG:상태이상번호 > 0
        TCVAR:ARG:상태이상번호 --
    ENDIF

    SELECTCASE 상태이상번호
        ;TCVAR:720~729 = 기본수치보정(감소) 지속시간 ;
        CASE T_특수지속시간_ID TO (T_특수지속시간_ID + 9)
            IF TCVAR:ARG:상태이상번호 == 0
                TCVAR:ARG:T_힘 += TCVAR:ARG:T_힘감소량
                TCVAR:ARG:T_힘감소량 = 0
            ENDIF
        ;TCVAR:730~749 = 상태이상 지속시간
        CASE 735 ;T_독 ;DOT 고정데미지
            CALL T_데미지(ARG,50,"독")
        CASE 736 ;T_출혈 ;DOT 퍼센트데미지
            CALL T_데미지(ARG,(TCVAR:ARG:T_최대체력/20),"출혈")
        CASE 737 ;T_저주 ;지연시간 데미지
            SIF TCVAR:ARG:T_저주 == 0
                CALL T_데미지(ARG,TCVAR:ARG:T_최대체력,"저주")
    ENDSELECT
    ;지속시간이 음수인 경우 방지
    TCVAR:ARG:상태이상번호 = MAX(TCVAR:ARG:상태이상번호, 0)
NEXT

@버프부여(ARG, 버프, ARG:1, ARG:2)
#DIMS 버프
;ARG = 대상
;ARG:1 = 증가량
;ARG:2 = 지속시간
SELECTCASE 버프
    CASE "회피"
        TCVAR:ARG:T_회피량 += ARG:1
        TCVAR:ARG:T_회피 = ARG:2
        TCVAR:ARG:T_방어력보정 += TCVAR:ARG:T_회피량
ENDSELECT

@버프감소처리(ARG)
#DIM DYNAMIC 버프번호
#DIM DYNAMIC 버프정도

FOR 버프번호, T_버프_ID, (T_버프_ID + 20)
    버프정도 = TCVAR:ARG:(버프번호 + 20)
    ;버프중이 아니라면 통과
    SIF !TCVAR:ARG:버프번호
        CONTINUE

    TCVAR:ARG:버프번호 --
    IF TCVAR:ARG:버프번호 == 0 && 버프정도 > 0
        SELECTCASE 버프번호
            CASE 770 ;T_회피
                TCVAR:ARG:T_방어력보정 -= 버프정도
        ENDSELECT
        버프정도 = 0
    ENDIF
NEXT
