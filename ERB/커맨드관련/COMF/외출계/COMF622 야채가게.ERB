@COM622
PRINTL 소지 아이템
CALL LIST_ITEM, "야채"
PRINTL 　
PRINTL 　
PRINTFORML 구입할 아이템을 선택해주세요 소지금:\\{MONEY}

CALL SHOW_ITEM, "야채"
PRINTFORML [099] - 야채 배송 서비스
PRINT [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	TIME += 10
	RETURN -1
ELSEIF RESULT == 99
	IF FLAG:식재료구독
		PRINTFORML 이번달엔 이미 신청했다...
	ELSE
		PRINTFORML 야채 구독을 신청할까?(20000￦/월)
		IF MONEY < 20000
			PRINTFORMW ...돈이 없다
		ELSE
			CALL ASK_YN
			IF RESULT
				RESTART
			ELSE
				FLAG:식재료구독 = 1
				MONEY -= 20000
			ENDIF
		ENDIF
	ENDIF
ELSEIF ITEM_TYPES( RESULT, "야채" )
	CALL BUY_ABLE, RESULT
	PRINTL 
	RESTART
ELSE
	GOTO INPUT_LOOP
ENDIF
TIME += 10

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;八百屋
@COM_ABLE622
;一括管理
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
;八百屋ではない場所
SIF !YASAI_SHOP(CFLAG:MASTER:현재위치)
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
;시간정지中
SIF FLAG:70
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

@YASAI_SHOP(ARG)
#FUNCTION
;八百屋
SIF ARG == GET_MAP_REPLACEMENT(227)
	RETURNF 1
RETURNF 0

@야채배달
PRINTFORML 집 앞으로 신선한 야채가 배달되었다...
SELECTCASE RAND:4
	CASE 0
		ITEM:토마토 += 5
		ITEM:백합근 += 3
		ITEM:순무 += 3
		ITEM:당근 += 1
		ITEM:오이 += 2
		PRINTFORML 토마토 5개, 백합근 3개, 순무 3개, 당근 1개, 오이 2개를 받았다
	CASE 1
		ITEM:토마토 += 3
		ITEM:백합근 += 3
		ITEM:순무 += 5
		ITEM:당근 += 3
		ITEM:오이 += 2
		PRINTFORML 토마토 3개, 백합근 3개, 순무 5개, 당근 3개, 오이 2개를 받았다
	CASE 2
		ITEM:양배추 += 5
		ITEM:백합근 += 3
		ITEM:순무 += 3
		ITEM:당근 += 1
		ITEM:오이 += 2
		PRINTFORML 양배추 5개, 백합근 3개, 순무 3개, 당근 1개, 오이 2개를 받았다
	CASE 3
		ITEM:토마토 += 2
		ITEM:가지 += 6
		ITEM:오이 += 4
		PRINTFORML 토마토 2개, 가지 6개, 오이 4개를 받았다
ENDSELECT