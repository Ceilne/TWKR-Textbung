;-------------------------------------------------
;식사한다
;日常系コマンド、レベル1
;-------------------------------------------------
@COM414
#DIM 回復前체력_MASTER
#DIM 回復前기력_MASTER
#DIM 回復後체력_MASTER
#DIM 回復後기력_MASTER
#DIM 回復前체력_TARGET
#DIM 回復前기력_TARGET
#DIM 回復後체력_TARGET
#DIM 回復後기력_TARGET
#DIM NUM_TARGET
#DIM NOW_TARGET
#DIM NOW_SUCCESS
#DIM T_ID
CALL AssembleForMeal
NUM_TARGET = GET_TARGETNUM()
VARSET LOCAL
回復前체력_MASTER = BASE:MASTER:체력
回復前기력_MASTER = BASE:MASTER:기력

FLAG:오늘밥 += 1

CALL 영양소증가(요리영양소:0,요리영양소:1,요리영양소:2,요리영양소:3,요리영양소:4)

FLAG:충치진행도 += RAND:3
IF TARGET
	回復前체력_TARGET = BASE:TARGET:체력
	回復前기력_TARGET = BASE:TARGET:기력
	CALL COM414_CTRL_EAT_DISH("PRI")
	SIF RESULT < 0
		RETURN
	;信頼
	TFLAG:98 += 3
	SIF ABL:MASTER:요리기능 > 1
		TFLAG:98 ++ 
	SIF ABL:MASTER:요리기능 > 3
		TFLAG:98 ++ 
	
	;장난を仕込まれていない
	IF !TASTE_GIVEUP(TARGET, DISH_RADICAL()) && TFLAG:192 >= 0
		;味付けがツボだと失敗しない
		IF CFLAG:TARGET:미각기호 & TCVAR:MASTER:308
			TFLAG:193 = MAX(TFLAG:193,0)
			;信頼にも修正
			TFLAG:98 += 2
		ENDIF
	ENDIF
	SELECTCASE DISHTYPE
		CASE 1

		CASE 2,4,5

		CASE 3

	ENDSELECT
	
	;特殊な식사フラグ
	SELECTCASE DISHNAME
		CASE "샤브샤브"
			;その場にはいてないキャラがいるか確認
			FOR LOCAL, 1, NUM_TARGET + 1
				SIF EQUIP:(TARGET:LOCAL):하반신속옷2 > 0
					CONTINUE
				TFLAG:194 = 1
				BREAK
			NEXT
		CASE "긴기 요리"
			TFLAG:194 = 34
		CASE "모짜렐라 치즈와 토마토의 샐러드"
			TFLAG:194 = 30
		CASE "도핑 콘소메 수프"
			TFLAG:194 = 31
		CASEELSE
			SIF DISHTYPE == 4
				TFLAG:194 = 2
	ENDSELECT
	
	;その場に居るTARGET全員で回す
	;TARGET:0以外を先に処理して最後にTARGET:0
	NOW_TARGET = TARGET
	NOW_SUCCESS = TFLAG:193
	FOR LOCAL:4, 1, NUM_TARGET + 1
		TARGET = TARGET:(LOCAL:4)
		SIF TARGET == NOW_TARGET
			CONTINUE
		CALL KOJO_MESSAGE_SEND("SUCCESS",-1,TARGET,-1,-1)
		CALL COM414_CTRL_EAT_DISH("LATER")
		CALL COM414_1
	NEXT
	TARGET = NOW_TARGET
	TFLAG:193 = NOW_SUCCESS

	IF !CFLAG:수면
		CALL COM414_1
	ELSE
		SIF TFLAG:193 == 1
			EXP:MASTER:회화경험 --
	ENDIF
	回復後체력_TARGET = BASE:TARGET:체력
	回復後기력_TARGET = BASE:TARGET:기력
ENDIF

;軽食
IF DISHTYPE == 1
	CALL 식사(MASTER)
	TIME += 20
	SIF CFLAG:동행
		CFLAG:동행 += 20
;요리が샤브샤브ではいてないキャラがいると노팬티샤브샤브に
ELSEIF TFLAG:194 == 1
	BASE:MASTER:체력 = MIN(BASE:MASTER:체력 + MAXBASE:MASTER:체력 / 3 , MAXBASE:MASTER:체력)
	BASE:MASTER:기력 = MIN(BASE:MASTER:기력 + MAXBASE:MASTER:기력 / 2 , MAXBASE:MASTER:기력)
	BASE:MASTER:정력 = MIN(BASE:MASTER:정력 + MAXBASE:MASTER:정력 / 2 , MAXBASE:MASTER:정력)
	;식사をしたフラグ
	CALL 만복도상승(MASTER,"주식")
	TIME += 40
	SIF CFLAG:동행
		CFLAG:동행 += 40
ELSEIF DISHTYPE == 2
	CALL 식사(MASTER)
	TIME += 40
	SIF CFLAG:동행
		CFLAG:동행 += 40
ELSEIF DISHTYPE == 3
	CALL 식사(MASTER)
	TIME += 20
	SIF CFLAG:동행
		CFLAG:동행 += 20
ELSEIF DISHTYPE == 4 || DISHTYPE == 5
	CALL 식사(MASTER)
	TIME += 40
	SIF CFLAG:동행
		CFLAG:동행 += 40
ENDIF
回復後체력_MASTER = BASE:MASTER:체력
回復後기력_MASTER = BASE:MASTER:기력
;CALL RESET_DISH
DRAWLINE
PRINTFORML 체력+{ABS(回復後체력_MASTER - 回復前체력_MASTER)}（%CALLNAME:MASTER%）
PRINTFORML 기력+{ABS(回復後기력_MASTER - 回復前기력_MASTER)}（%CALLNAME:MASTER%）
IF TARGET
	PRINTFORML 체력+{ABS(回復後체력_TARGET - 回復前체력_TARGET)}（%CALLNAME:TARGET%）
	PRINTFORML 기력+{ABS(回復後기력_TARGET - 回復前기력_TARGET)}（%CALLNAME:TARGET%）
ENDIF
RETURN 1


;식사結果の成功制御、判定部分がほぼ同一だったので切り出し
@COM414_CTRL_EAT_DISH(SCENE)
#DIMS SCENE
SELECTCASE TFLAG:192
CASE 0
	;通常
	LOCAL = RAND:(MAX(100 - ABL:MASTER:요리기능 * 2, 1))
	LOCAL:1 = MIN(90 + GET_SUCCESS_RATE() / 5, 99)
	SELECTCASE LOCAL
		CASE IS <= 9
			TFLAG:193 = 1
		CASE IS >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
		CASEELSE
			;残りは成功
			TFLAG:193 = 0
	ENDSELECT
CASE 1
	IF RAND:(MAX(100 - ABL:MASTER:요리기능 * 2, 1)) <= 9
		TFLAG:193 = 1
	ELSE
		;残りは成功
		TFLAG:193 = 0
	ENDIF
CASE -1, -2
	TFLAG:193 = -1
ENDSELECT

IF SCENE == "PRI"
	SIF TFLAG:192 == -2
		RETURN -1
	SELECTCASE TFLAG:193
	CASE 1
		TFLAG:98 = 2
	CASE -1
		TFLAG:98 = -2
	CASEELSE
		TFLAG:98 = 0
	ENDSELECT
ENDIF

@COM414_1
#DIM 空腹
#DIM DISH_ITAZURA
空腹 = 0
IF DISHTYPE == 3
	SIF TIME_PROGRESS(TCVAR:디저트공복시각)
		空腹 = 1
ELSE
	SIF TIME_PROGRESS(TCVAR:공복시각)
		空腹 = 1
ENDIF
SIF !空腹
	RETURN

;満腹でない
;固定で獲得するソース
SELECTCASE DISHSPOIL
	CASE 0
		SOURCE:환락 = 800
	CASE 1
		SOURCE:환락 = 400
	CASE 2
		SOURCE:환락 = 100
		SOURCE:불결 = 400
		SOURCE:반감 = 400
ENDSELECT

;ABL:순종をみる
IF ABL:순종 <= 1
	SOURCE:환락 += (ABL:순종 * 60)
ELSEIF ABL:순종 <= 3
	SOURCE:환락 += 500 + (ABL:순종 * 60)
ELSEIF ABL:순종 <= 5
	SOURCE:환락 += 700 + (ABL:순종 * 60)
ELSEIF ABL:순종 <= 8
	SOURCE:환락 += 1000 + (ABL:순종 * 75)
ELSEIF ABL:순종 <= 11
	SOURCE:환락 += 1500 + (ABL:순종 * 75)
ELSE
	SOURCE:환락 += 3000 + (ABL:순종 * 30)
ENDIF

;노팬티샤브샤브で노출追加
SIF TFLAG:194 == 1
	SOURCE:노출 += 1000 
;호감도をみる
IF CFLAG:2 <= 500
	SOURCE:환락 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:환락 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:환락 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:환락 < 0
	SOURCE:환락 = 0

;장난요리を先に処理
DISH_ITAZURA = TASTE_GIVEUP(TARGET, DISH_RADICAL())
SELECTCASE DISH_ITAZURA
CASE 0
	;미각기호を見る
	SIF CFLAG:TARGET:미각기호 & TCVAR:MASTER:308
		SOURCE:환락 += 200
CASE DISH_RADICAL_엄청매운
	SOURCE:고통 += 500
CASE DISH_RADICAL_격감
	SOURCE:반감 += 300
CASE DISH_RADICAL_고추냉이
	SOURCE:고통 += 500
CASE DISH_RADICAL_레몬
	SOURCE:불결 += 300
ENDSELECT
IF DISH_ITAZURA
	SOURCE:반감 += 500
	TFLAG:98 -= 2
	IF TALENT:유치
		TIMES SOURCE:고통, 1.2
		TIMES SOURCE:반감, 1.2
		TIMES SOURCE:불결, 1.2
	ENDIF
	IF CFLAG:행동 == 5
		TIMES SOURCE:반감, 1.2
		TIMES SOURCE:불결, 1.2
		TFLAG:98 -= 2
	ENDIF
	SOURCE:환락 = 0
	IF TALENT:대식가
		TIMES SOURCE:반감, 1.2
		TIMES SOURCE:불결, 1.2
		TFLAG:98 -= 2
	ENDIF
	CALL KIGEN_CHANGE(TARGET, 70, -1)
	CALL 식사(TARGET)
	RETURN
ENDIF

SOURCE:수동 = 200 + 100 * ABL:순종
SOURCE:정복 = 200 + 100 * ABL:새드끼

IF TFLAG:193 == -1
	TIMES SOURCE:환락 , 0.10
	TIMES SOURCE:정복 , 0.50
	TIMES SOURCE:수동 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:환락 , 1.00
	TIMES SOURCE:정복 , 1.00
	TIMES SOURCE:수동 , 1.00
	CALL KIGEN_CHANGE(TARGET,30,1)
ELSE
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:정복 , 2.00
	TIMES SOURCE:수동 , 2.00
	IF TALENT:기분 < 0
		CALL KIGEN_CHANGE(TARGET,100,1)
	ELSE
		CALL KIGEN_CHANGE(TARGET,50,1)
	ENDIF
ENDIF
SOURCE:환락 = SOURCE:환락 * (ABL:MASTER:요리기능 + DISHQUALITY * 2) / 5
SOURCE:정복 = SOURCE:정복 * (ABL:MASTER:요리기능 + DISHQUALITY * 2) / 5
SOURCE:수동 = SOURCE:수동 * (ABL:MASTER:요리기능 + DISHQUALITY * 2) / 5
IF CFLAG:행동 == 5
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:정복 , 2.00
	TIMES SOURCE:수동 , 2.00
	TFLAG:98 += 2
ENDIF
CALL 식사(TARGET)

SIF TALENT:대식가
	TIMES SOURCE:환락 , 1.50

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE414
;식사実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(414)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;장난をした요리は自分で食べない
SIF DISH_RADICAL() && !TARGET
	RETURN 0
;요리がない
SIF DISHNAME == ""
	RETURN 0
;軽食以外は屋内じゃなきゃ無理
SIF FLAG:20 >= 10 && !INROOM(CFLAG:MASTER:현재위치)
	RETURN 0
;食べたばかり
IF DISHTYPE == 3
	SIF !TIME_PROGRESS(TCVAR:MASTER:디저트공복시각)
		RETURN 0
ELSE
	SIF !TIME_PROGRESS(TCVAR:MASTER:공복시각)
		RETURN 0
ENDIF
SIF CFLAG:MASTER:우후후
	RETURN 0
RETURN 1

