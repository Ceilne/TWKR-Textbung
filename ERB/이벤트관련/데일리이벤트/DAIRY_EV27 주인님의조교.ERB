@DAILY_CONDITION_EV27
RETURN -1

@DAILY_EV27
#DIM 주인의수
#DIM 주인순서
#DIM 루프횟수
#DIM 검증비트
#DIM 조교횟수
#DIM 절정카운터
주인의수 = 0
주인순서 = 0
검증비트 = 0
조교횟수 = 0
절정카운터 = 0
루프횟수 = 0
;주인이 몇 명인지
FOR 루프횟수, 1, CHARANUM
	IF TALENT:루프횟수:주인 == 1&&!TALENT:루프횟수:사망 && !CFLAG:루프횟수:출금 && !CFLAG:루프횟수:감금됨
		주인의수++
	ENDIF
NEXT
;그 주인들 중 몇 번째 주인이 찾아오는지
IF 주인의수 == 0
	;주인수 0일때에 무한루프 발생을 억제
	RETURN -1
ENDIF
주인순서 = RAND:주인의수
FOR 루프횟수, 1, CHARANUM
	IF TALENT:루프횟수:주인 == 1&&!TALENT:루프횟수:사망 && !CFLAG:루프횟수:출금 && !CFLAG:루프횟수:감금됨
		검증비트++
		IF 검증비트 >= 주인순서
			TARGET = 루프횟수
			BREAK
		ENDIF
	ENDIF
NEXT

PRINTFORMW 늦은 밤, %마스터의% 방문을 누군가 똑똑 두드렸다
PRINTFORM 문을 열어보니...
PRINTFORMW %타겟이% 서 있었다!
PRINTFORMW 방으로 들어온 %타겟은% %마스터를% 넘어뜨린 채 조교를 개시했다...
조교횟수 = RAND:5 + 2
REPEAT 조교횟수
	SELECTCASE RAND:6
		CASE 0
			PRINTFORMW %타겟은% %마스터의% 엉덩이를 호되게 때렸다...
			EXP:MASTER:고통쾌락경험++
			IF RAND:2 == 0
				CALL 주도도상승(TARGET, 1)
			ENDIF
			절정카운터 += RAND:2 * 10
			IF 절정카운터 >= 100
				EXP:MASTER:절정경험++
				절정카운터 = 0
				IF TALENT:MASTER:성별 == 1
					PRINTFORMW %마스터는% 엉덩이를 맞음과 동시에 교성을 지르며 절정에 이르렀다...
					PRINTFORMW %타겟은% 무척 만족스러워 하는 표정을 지었다
				ELSE
					PRINTFORMW 엉덩이를 맞음과 동시에 %마스터의% 자지에서 정액이 주르륵 흘러나왔다
					PRINTFORMW 맞으면서도 느꼈냐며 %타겟의% 비웃는 목소리가 들렸다
				ENDIF
			ENDIF
		CASE 1
			IF TALENT:MASTER:성별 != 1
				PRINTFORMW %타겟은% %마스터의% 자지를 쥐락펴락하며 %마스터의% 반응을 지켜보았다...
				EXP:MASTER:사정경험++
				IF RAND:2 == 0
					CALL 주도도상승(TARGET, 1)
				ENDIF
				절정카운터 += RAND:5 * 10
				IF 절정카운터 >= 100
					EXP:MASTER:절정경험++
					EXP:MASTER:C절정경험++
					절정카운터 = 0
					PRINTFORMW %마스터의% 자지에서 백탁액이 분수처럼 뿜어져나왔다
					PRINTFORMW %타겟은% 꺄르르 웃으며 그렇게 좋냐고 속삭여왔다
				ENDIF
			ELSE
				PRINTFORMW %타겟은% %마스터의% 클리토리스를 뜯을것 마냥 당겨가며 %마스터의% 비명을 즐겼다...
				EXP:MASTER:C경험++
				IF RAND:2 == 0
					CALL 주도도상승(TARGET, 1)
				ENDIF
				절정카운터 += RAND:5 * 10
				IF 절정카운터 >= 100
					EXP:MASTER:절정경험++
					EXP:MASTER:C절정경험++
					절정카운터 = 0
					PRINTFORMW %마스터의% 애액이 분수처럼 뿜어져나왔다
					PRINTFORMW %타겟은% 꺄르르 웃으며 그렇게 좋냐고 속삭여왔다
				ENDIF
			ENDIF
		CASE 2
			PRINTFORMW %타겟은% %마스터의% 유두를 당기고, 물고, 빙글빙글 돌려가며 괴롭혔다...
			EXP:MASTER:B경험++
			IF RAND:2 == 0
				CALL 주도도상승(TARGET, 1)
			ENDIF
			절정카운터 += RAND:4 * 10
			IF 절정카운터 >= 100
				EXP:MASTER:B절정경험++
				EXP:MASTER:절정경험++
				절정카운터 = 0
				PRINTFORMW %마스터는% 파르르 떨면서 절정해버렸다
				IF TALENT:MASTER:성별 == 2		
					PRINTFORMW 남자인데도 가슴으로 느껴버렸냐며 %타겟의% 타박을 들었다...
				ELSE
					PRINTFORMW %타겟은% 그런 %마스터의% 가슴을 손가락으로 톡톡 튕기며 즐거워했다...
				ENDIF
			ENDIF
		CASE 3
			PRINTFORMW %타겟은% %마스터의% 항문에 큼지막한 딜도를 박고 그 반응을 즐기고 있다...
			EXP:MASTER:A경험++
			IF RAND:2 == 0
				CALL 주도도상승(TARGET, 2)
			ENDIF
			절정카운터 += RAND:3 * 10
			IF 절정카운터 >= 100
				EXP:MASTER:A절정경험++
				EXP:MASTER:절정경험++
				절정카운터 = 0
				PRINTFORMW %마스터는% 눈을 까뒤집고 절정에 신음했다...
				IF TALENT:MASTER:성별 == 1
					PRINTFORMW 암컷같은 목소리라며 %타겟은% 기뻐했다...
				ELSE
					PRINTFORMW %타겟은% 무척이나 신나서 딜도를 넣었다 뺐다하며 즐겼다...
				ENDIF
			ENDIF
		CASE 4
			PRINTFORMW %타겟은% %마스터를% 살포시 안은 채 귀여워해줬다...
			EXP:MASTER:애정경험++
		CASE 5
			PRINTFORMW %타겟은% %마스터의% 입 안에 술을 넣어주었다
			EXP:MASTER:애정경험++
	ENDSELECT
REND
PRINTFORMW 이제야 %타겟은% 만족한채 싱글벙글 웃으며 돌아갔다...
CFLAG:MASTER:철야 = 1