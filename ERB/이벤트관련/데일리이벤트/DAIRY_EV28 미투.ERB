@DAIRY_CONDITION_EV28
#DIM DYNAMIC 조건
조건 = 10
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:연모||TALENT:LOCAL:연인||TALENT:LOCAL:사모||TALENT:LOCAL:애욕||TALENT:LOCAL:당신의노예||TALENT:LOCAL:주인
		조건++
NEXT
SIF RAND:(100+조건) > 4
	RETURN -1
CALL DAIRY_EV28
RETURN 1

@DAIRY_EV28
#DIM 합의금
#DIM 연속미투방지
합의금 = 0

IF 연속미투방지
	연속미투방지 -= 1
	RETURN 1
ENDIF
연속미투방지 = 2
IF TALENT:29:사망
	PRINTFORML ...그러고보니, 이때쯤엔 호외를 뿌리고 다니는 아야의 모습이 있었을텐데
	PRINTFORML 이제는 볼 수 없게 되었다...
ELSE
	PRINTFORML 붕붕마루 신문의 호외가 나왔습니다!
	IF !TALENT:29:연모&&!TALENT:29:연인&&!TALENT:29:사모&&!TALENT:29:애욕&&!TALENT:29:당신의노예&&!TALENT:29:주인
		PRINTFORM 익명의 제보로, %마스터가% 성추행
		IF EXP:MASTER:삽입경험
			PRINTFORM 및 성폭력
		ENDIF
		PRINTFORML 을 했다는 증언이 실려있었다!
		PRINTFORMW %마스터는% 당장 항의하려 했지만 피해자의 눈물이 증거품으로 제출되었다...

		FOR LOCAL, 1, CHARANUM
			CFLAG:LOCAL:호감도 = MAX(CFLAG:LOCAL:호감도 - 100, 0)
			CFLAG:LOCAL:신뢰도 = MAX(CFLAG:LOCAL:신뢰도 - 40, 0)
		NEXT
		CALL COLORMESSAGE("전원의 호감도가 100, 신뢰도가 40 떨어졌다……", C_YELLOW, 2,1)
		합의금 = RAND:10 * 10000 + 10000
		PRINTFORMW %마스터는% {합의금}원 만큼의 합의금을 지불해야한다...
		PRINTFORML 합의금 : {합의금}
		PRINTFORML 소지금 : {MONEY}
		PRINTFORML 내시겠습니까?
		PRINTFORML [0] 예
		PRINTFORML [1] 못 내!
		INPUT
		IF RESULT == 0
			IF MONEY < 합의금
				PRINTFORMW 돈이 부족하다...
				PRINTFORMW 어쩔수 없이 은행에서 빌리기로 하였다
				CALL 채무생성(합의금 - MONEY,((합의금 - MONEY)/10000 )+1)
				MONEY = 0
			ELSE
				MONEY -= 합의금	
			ENDIF
			PRINTFORMW %마스터는% 억울함에 눈물지으면서도 합의금을 지불했다...
		ELSE
			PRINTFORMW .........
			PRINTFORMW ......
			PRINTFORMW ...
			PRINTFORMW 「에, 피고는.......를....강제로 추행하고....한 사실이 있다, 맞습니까?」
			PRINTFORMW 「......예, 그럼 판결을 내리겠습니다」
			PRINTFORM 「피고 %CALLNAME:MASTER%에게...징역 
			SETCOLOR 0xFF0000
			PRINTFORM 12월
			RESETCOLOR
			PRINTFORMW 을 선고합니다」
			PRINTFORMW 「끌고 가세요」
			PRINTFORMW %마스터는% 그렇게 인간 마을 지하 깊숙한 감옥에 갇혔다...
			PRINTFORMW .......
			;------------------
			DAY += 360
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:임신경과일수 > 0
					CFLAG:LOCAL:임신경과일수 = 60
				ENDIF
			NEXT
			PRINTFORMW ...1년이 흘렀다...
			PRINTFORMW 간수는 멍하니 벽만 바라보고 있는 %마스터를% 억지로 꺼내 집에 끌고 갔다...
			CFLAG:MASTER:철야 = 1
			BASE:MASTER:체력 = -9999
			BASE:MASTER:기력 = -9999
			;----------------임시처리
			;FLAG:감방남은일수 += 540
			[SKIPSTART]
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:임신경과일수 > 0
					CFLAG:LOCAL:임신경과일수 = 60
				ENDIF
			NEXT
			PRINTFORMW ...1년 반이 흘렀다...
			PRINTFORMW 간수는 멍하니 벽만 바라보고 있는 %마스터를% 억지로 꺼내 집에 끌고 갔다...
			CFLAG:MASTER:철야 = 1
			BASE:MASTER:체력 = -9999
			BASE:MASTER:기력 = -9999
			[SKIPEND]
		ENDIF
	ELSE
		;아야한테 함락소질이 있다
		PRINTFORML ...왜인진 몰라도 %마스터가% 한 여러 선행들이 실려있었다!
		PRINTFORML 자질구레한 일들인데다 한지 안한지도 기억이 잘 안나긴 하지만...
		FOR LOCAL, 1, CHARANUM
			CFLAG:LOCAL:호감도 += 50
			CFLAG:LOCAL:신뢰도 += 25
		NEXT
		CALL COLORMESSAGE("모두에게서 평가가 조금 나아진것 같다", C_YELLOW, 2,1)
	ENDIF
ENDIF