@ENKAI_SETTING
;------------------------------------------------------------------------------------------------------
;연회의 개최 판정
;------------------------------------------------------------------------------------------------------
;1턴으로 마시는 술의 양은 경과시간 * 10 통상은 30분이므로 300, 연회규모 1200이라면 2시간 정도의 연회가 된다
;주최자는 연회의 준비와 정리의 일을 해야 한다
;하루에 복수의 연회를 여는 사양은 아니기 때문에 주의(사람이 분산해 버려서 외롭고…)
;------------------------------------------------------------------------------------------------------
PRINTL  

;開催判定は宴会フラグが立っていない状態でしか行われない（バグ防止）
SIF FLAG:연회개최플래그
	RETURN
	
;メイン主催者が出禁になっているなら開催されない
;宴会種別は98以上には設定しないこと
;2 幻想郷年忘れ会　メイン主催者：なし　大晦日の一週間前に１００％発生する
IF DAY:2 == 4 && DAY:3 >= 25 && DAY:3 <= 30
	;宴会名
	EnkaiTitle = 환상향 송년회
	;宴会種別
	FLAG:연회개최플래그 = 2
	;宴会規模
	FLAG:31 = 6000
	;宴会会場
	FLAG:32 = 9
	;開始時間
	FLAG:33 = 900
	;開始日
	FLAG:개시일 = 31 - DAY:3
	;雨天中止
	FLAG:36 = 0
	PRINTL  
	PRINTFORMW 송년회 시즌이 다가왔다
;月見酒
ELSEIF DAY:2 == 3 && DAY:3 >= 1 && DAY:3 < 15
	;宴会名
	EnkaiTitle = 달맞이 술잔치
	;宴会種別
	FLAG:연회개최플래그 = 3
	;宴会規模
	FLAG:31 = 2000 + RAND:1000
	;会場
	FLAG:32 = 409
	;開始時間
	FLAG:33 = 1140
	;開始日
	FLAG:개시일 = 15 - DAY:3
	;雨天中止
	FLAG:36 = 1
	PRINTL  
	IF MAIN_MAP == 4
		PRINTFORMW 에이린에게 달맞이 술찬치에 권유되었다
	ELSE
		PRINTFORMW 영원정에서 달맞이 술잔치에 초대되었다
	ENDIF
;수확제
ELSEIF DAY:2 == 3 && DAY:3 >= 16 && DAY:3 < 27
	;宴会種別
	FLAG:연회개최플래그 = 4
	;宴会規模
	FLAG:31 = 2000 + RAND:3000
	;会場
	FLAG:32 = 202
	;開始時間
	FLAG:33 = 540
	;開始日
	FLAG:개시일 = 27 - DAY:3
	;雨天中止
	FLAG:36 = 0
	PRINTL  
	PRINTFORMW 올해도 수확제의 계절이 다가왔다
; レミリアの気紛れなパーティ　メイン主催者：レミリア　自宅が紅魔館　土曜日に５０％で開催予告、実施日は日曜日
ELSEIF CFLAG:16:출금 == 0 && DAY%7 == 6 && MAIN_MAP == 3 && RAND:50 <= 49
	;宴会名
	EnkaiTitle = 레밀리아의 변덕스러운 파티
	;宴会種別
	FLAG:연회개최플래그 = 6
	;宴会規模
	FLAG:31 = 1500 + RAND:2500
	;宴会会場
	FLAG:32 = 324
	;開始時間
	FLAG:33 = 1140
	;開始日
	FLAG:개시일 = 1
	;雨天中止
	FLAG:36 = 1
	PRINTL  
	PRINTFORMW %조사처리(CALLNAME:16,"는")% 파티를 하고 싶어진 것 같다
;SAMPLE 霊夢の気紛れ酒盛り　メイン主催者：霊夢　土曜日に５０％で開催予告、実施日は日曜日
ELSEIF CFLAG:1:출금 == 0 && DAY%7 == 6 && RAND:100 <= 49
	;宴会名
	EnkaiTitle = 레이무의 변덕스러운 술 잔치
	;宴会種別
	FLAG:연회개최플래그 = 1
	;宴会規模
	FLAG:31 = 1000 + RAND:1500
	;宴会会場
	IF RAND:5 <= 3
		FLAG:32 = 11
	ELSEIF RAND:4 == 0
		FLAG:32 = 2
	ELSEIF RAND:4 == 0
		FLAG:32 = 20
	ELSEIF RAND:4 == 0
		FLAG:32 = 21
	ELSE
		FLAG:32 = 218
	ENDIF
	;開始時間
	FLAG:33 = 1360 - (FLAG:31 / 10)
	;開始日
	FLAG:개시일 = 1

	;雨天中止
	FLAG:36 = 1
	PRINTL  
	PRINTFORMW %조사처리(CALLNAME:1,"는")% 연회를 하고 싶어진 것 같다
;	ELSEIF DAY:3 >= 8 && DAY:3 <= 10
;		;연회명
;		LOCALS = 밤안내 독경 라이브
;		;연회 종별
;		FLAG:연회개최플래그 = 5
;		;연회규모
;		FLAG:31 = 2000 + RAND:30 * 40
;		;연회회장
;		FLAG:32 = 104
;		;개시시간
;		FLAG:33 = 1440 + 240 - (FLAG:31 /10) ;아침 4시까지 연회 한다
;		;개시일
;		FLAG:34 = 10 - DAY:3
;		;현재의 참가인수를 리셋
;		FLAG:35 = 0
;		;우천 중지
;		FLAG:36 = 0
;		PRINTL  
;		PRINTFORMW 밤안내 독경 라이브의 시기가 가까워져 왔다
	ENDIF
ENDIF
;現在の参加人数をリセット
FLAG:35 = 0
CALL SET_ENKAI_LOCATIONDISPLAY
SIF FLAG:연회개최플래그
	CALL 연회개요
PRINTL  

@ENKAI_ENTRY
#DIM DYNAMIC HOST_LIST  , 캐릭터수상한;主催者参加
#DIM DYNAMIC GUEST_LIST , 캐릭터수상한;客参加
#DIM DYNAMIC NG_LIST    , 캐릭터수상한;来ない
#DIM ResidentRate	;そのマップに住んでいるキャラの参加確率
#DIM VisitorRate	;そのマップに住んでいないキャラの参加確率
#DIM HL
;---   宴会の参加者決定   -----------------------------------------------------------------------------
;CFLAG:연회참가 0=不参加 1=参加 2=主催者 3=脱落者 4=飲まないけど参加
;------------------------------------------------------------------------------------------------------
;フラグ初期化
CVARSET CFLAG, GETNUM(CFLAG, "연회참가"), 0, 1
SELECTCASE FLAG:연회개최플래그
	;1 霊夢の気紛れ酒盛り
	CASE 1
		SELECTCASE FLAG:연회차입
			CASE 51
				FLAG:31 += RAND:1000
			CASE 52
				FLAG:31 += RAND:2000
		ENDSELECT
		;1:레이무, 2:루코토 11:마리사
		HOST_LIST  = 1, 2, 11
		;10:스이카
		GUEST_LIST = 10
		;세이자는 참가하지 않는다
		NG_LIST    = 97
		
		IF FLAG:31 >= 2500
		;規模が大きいと参加確率大
			ResidentRate = 12
			VisitorRate = 12
		ELSE
			ResidentRate = 7
			VisitorRate = 7
		ENDIF
	;2 幻想郷歳忘れ会
	CASE 2
		;1:霊夢, 2:るーこと, 11:魔理沙
		HOST_LIST  = 1, 2, 11
		;10:萃香
		GUEST_LIST = 10
		;正邪は参加しない
		NG_LIST    = 97
		ResidentRate = 20
		VisitorRate = 20

	;月見酒
	CASE 3
		;52:우동, 53, 72:네-링　75:메디슨도 돕는다
		HOST_LIST  = 52, 53, 72, 75
		;62:나
		GUEST_LIST = 62
		;세이자는 참가하지 않는다
		NG_LIST    = 97
		ResidentRate = 33
		VisitorRate = 9
	;수확제
	CASE 4
		EnkaiTitle = 수확제
		FOR LOCAL,1,CHARANUM
			IF CFLAG:LOCAL:자택위치 / 100 == 2
				FOR HL, 0, 20
					SIF HOST_LIST:HL
						CONTINUE
					HOST_LIST:HL = LOCAL
				NEXT
			ENDIF
		NEXT
		;秋姉妹は必ず来る
		GUEST_LIST = 77, 78
		;正邪は参加しない
		NG_LIST    = 97
		ResidentRate = 0
		VisitorRate = 8
	;読経ライブ
;	CASE 5
;		;명련사멤버는 일한다
;		HOST_LIST = 39, 40, 41, 55, 85, 86, 87, 88
;		;GUEST_LIST 없음
;		;하쿠레이 신사조는 오지 않는다
;		;세이자도 오지 않는다
;		NG_LIST   = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 97
;		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
;		FOR LOCAL, 1, CHARANUM
;			IF CFLAG:LOCAL:연회참가
;				SIF CFLAG:LOCAL:연회참가 < 0
;					CFLAG:LOCAL:연회참가 = 0
;				CONTINUE
;			ENDIF
;			;외는 술을 마시지 않는다
;			SIF RAND:15 == 0
;				CFLAG:LOCAL:연회참가 = 9
;		NEXT
	;6 레밀리아의 변덕인 파티
	CASE 6
		;15:사쿠야, 58:메이링, 59:소악마는 일한다
		HOST_LIST  = 15, 58, 59
		;16:레밀리아, 50:플랑은 일하지 않는다
		GUEST_LIST = 16, 50
		;54:パチュリー, 97:正邪は参加しない
		NG_LIST    = 54, 97
		ResidentRate = 33
		IF FLAG:31 >= 2500
		;規模が大きいと参加確率大
			VisitorRate = 14
		ELSE
			VisitorRate = 9
		ENDIF
	CASEELSE
		RETURN
ENDSELECT
CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST, ResidentRate, VisitorRate)


;リストを利用した宴会キャラのプリセット関数
@ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST, ResidentRate, VisitorRate)
#DIM REF HOST_LIST  ;主催者参加
#DIM REF GUEST_LIST ;客参加
#DIM REF NG_LIST    ;来ない
#DIM L_NUM
#DIM L_ID
#DIM ResidentRate
#DIM VisitorRate
#DIM VenueMap

VenueMap = FLAG:32 / 100

L_NUM = FINDELEMENT(HOST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(HOST_LIST:L_ID):연회참가 = 2
	NEXT
ENDIF
L_NUM = FINDELEMENT(GUEST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(GUEST_LIST:L_ID):연회참가 = 1
	NEXT
ENDIF
;설정한 것을 명확화하기 위해서―1으로 합니다
L_NUM = FINDELEMENT(NG_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(NG_LIST:L_ID):연회참가 = -1
	NEXT
ENDIF

FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:연회참가
		SIF CFLAG:LOCAL:연회참가 < 0
			CFLAG:LOCAL:연회참가 = 0
		CONTINUE
	ENDIF
	IF CFLAG:LOCAL:자택위치 / 100 == VenueMap
		SIF RAND:100 < ResidentRate
			CFLAG:LOCAL:연회참가 = 1
	ELSE
		SIF RAND:100 < VisitorRate
			CFLAG:LOCAL:연회참가 = 1
	ENDIF
NEXT

@ENKAI_WORK(ARG)
;연회의 행동 내용을 입력한다
;NOSAMES:모든 인수가 다르면(자) 1, 1개에서도 일치하고 있으면(자) 0을 돌려준다
IF FLAG:연회개최플래그 > 0 && (NOSAMES(CFLAG:ARG:직종, 48, 49, 50, 51) || CFLAG:ARG:행동 == 0)
	;연회의 시간에 되면(자) 준비 일의 양에 응해 주최자가 움직이기 시작한다
	IF TIME >= FLAG:개시시간 - (FLAG:연회규모 / 30) - 15 && TIME < FLAG:33 && CFLAG:ARG:연회참가 == 2
		CALL ENKAI_PREPARE(ARG)
		RETURN 1
	;놀러 올 뿐(만큼)의 놈은 시간에 되면(자) 놀러 온다
	ELSEIF DAY == FLAG:34 && TIME >= FLAG:33 && CFLAG:ARG:동행 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	;벌써 시작되어있는 경우
	ELSEIF FLAG:연회개최플래그 >= 98 && CFLAG:ARG:동행 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	ENDIF
ELSEIF FLAG:연회개최플래그 > 0
	RETURN 1
ENDIF
RETURN 0


@ENKAI_PREPARE(ARG)
;주최자의 준비 일
IF CFLAG:ARG:직종 != 연회준비
	MAXBASE:ARG:일량 = FLAG:31 / 3
	;최대치, 2400
	SIF MAXBASE:ARG:일량 >= 2400
		MAXBASE:ARG:일량 = 2400
	BASE:ARG:일량 = MAXBASE:ARG:일량
	CFLAG:ARG:직종 = 연회준비
	CFLAG:ARG:직장 = TFLAG:연회장
	TCVAR:ARG:315 = 0
	CFLAG:ARG:행동 = 5
ENDIF
RETURN 1

@ENKAI_START(ARG)
;각각이 마시는 페이스를 바꾸므로, 연회의 규모에 응해 일량이 변화
IF CFLAG:ARG:직종 != ENKAI_SANKA && TCVAR:ARG:만취 == 0
	MAXBASE:ARG:일량 = FLAG:31
	;최대치, 통상 7200(반나절)·술통째로 삼킴신이라면 제한 없음
	SIF MAXBASE:ARG:일량 >= 7200 && TALENT:ARG:술내성 != 3
		MAXBASE:ARG:일량 = 7200
	MAXBASE:ARG:일량 += RAND:500
	IF TALENT:ARG:술내성 == -1
		TIMES MAXBASE:ARG:일량, 0.75
	ELSEIF TALENT:ARG:술내성 == 1
		TIMES MAXBASE:ARG:일량, 1.25
	ELSEIF TALENT:ARG:술내성 == 2
		TIMES MAXBASE:ARG:일량, 1.5
	ELSEIF TALENT:ARG:술내성 == 3
		TIMES MAXBASE:ARG:일량, 2.0
	ENDIF
	BASE:ARG:일량 = MAXBASE:ARG:일량
	;일내용·연회에 참가
	CFLAG:ARG:직종 = ENKAI_SANKA
	CFLAG:ARG:직장 = TFLAG:연회장
	CFLAG:ARG:현재위치 = TFLAG:연회장
	TCVAR:ARG:315 = 0
	CFLAG:ARG:행동 = 5
	IF CFLAG:ARG:연회참가 == 2
		;주최자는 정리 예정을 세트(일량은 최대 1200)
		CFLAG:ARG:정리일량 = MIN(FLAG:연회규모 / 6, 1200)
		CFLAG:ARG:정리장소 = FLAG:연회회장
	ENDIF
ENDIF

RETURN 1


@ENKAI_KATAZUKE(ARG)
;주최자의 정리 일
MAXBASE:ARG:일량 = CFLAG:ARG:정리일량
BASE:ARG:일량 = MAXBASE:ARG:일량
;일내용·연회의정리
CFLAG:ARG:직종 = 50
CFLAG:ARG:직장 = GET_MAP_REPLACEMENT(CFLAG:ARG:정리장소)
TCVAR:ARG:도울수없다 = 0
CFLAG:ARG:행동 = 5
RETURN 1


@ENKAI_DRUNK(ARG)
;만취하는 라인을 넘고 있으면(자) 돌아간다
IF BASE:ARG:술기운 > (MAXBASE:ARG:술기운 / 10) * 8
	RETURN 1
ENDIF

;술을 마시지 않으면 취하지 않는다 
IF TALENT:ARG:술내성 == -2 || CFLAG:ARG:임신자각상태 == 1
	CFLAG:ARG:직종 = 0
	CFLAG:ARG:연회참가 = 4
	IF CFLAG:MASTER:연회참가 == CFLAG:ARG:연회참가
		PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 이미 충분히 즐긴 것 같다
	ENDIF
ELSE
	CFLAG:ARG:직종 = 0
	CFLAG:ARG:연회참가 = 3
;	PRINTFORMW %CALLNAME:ARG%はほろ酔い気分のようです
	IF CFLAG:MASTER:연회참가 == CFLAG:ARG:연회참가
		PRINTFORMW %조사처리(CALLNAME:ARG,"는")% 이미 충분히 즐긴 것 같다
	ENDIF
ENDIF
RETURN 1


@ENKAI_STOP
;악천후등에 의한 중지
FOR LOCAL, 0, CHARANUM
	CFLAG:LOCAL:직종 = 0
	MAXBASE:LOCAL:일량 = 0
	BASE:LOCAL:일량 = 0
	CFLAG:LOCAL:연회참가 = 0
	CFLAG:LOCAL:행동 = 0
NEXT
FLAG:연회개최플래그 = 0
FLAG:31 = 0
FLAG:32 = 0
TFLAG:연회장 = 0
FLAG:33 = 0
FLAG:개시일 = 0
FLAG:35 = 0
FLAG:36 = 0
FLAG:연회차입 = 0
;연회회장표시 = 
RETURN 0

@연회예정표시
PRINTFORM {FLAG:33 / 60}:{(FLAG:33 % 60)/10} {(FLAG:33 % 60)%10}에 %연회장표시()%에서 %EnkaiTitle%


@SET_ENKAI_LOCATIONDISPLAY
연회회장표시 = 

@연회장표시
#FUNCTIONS
LOCALS = 연회장표시 에러
IF FLAG:연회개최플래그
	IF IN_HOME(FLAG:32)
		LOCALS = %NAME_FROM_PLACE(FLAG:32)%
	ELSE
		LOCALS = %GET_MAPNAME(FLAG:32/100)%의 %GET_PLACENAME(FLAG:32)%
	ENDIF
ENDIF
RETURNF LOCALS

@연회명표시
#FUNCTIONS
SELECTCASE FLAG:연회개최플래그
	CASE 1
		LOCALS = 레이무의 변덕 술잔치
	CASE 2
		LOCALS = 환상향 나이 잊고 회
	CASE 3
		LOCALS = 달을 보면서 술을 마심
	CASE 4
		LOCALS = 수확제
	CASE 5
		LOCALS = 밤안내 독경 라이브
	CASE 6
		LOCALS = 레밀리아의 변덕인 파티
ENDSELECT
RETURNF LOCALS

@연회개요
PRINTFORML \@ FLAG:개시일 == 1? 내일# {FLAG:개시일}일 뒤\@, {FLAG:33 / 60}:{(FLAG:33 % 60)/10} {(FLAG:33 % 60)%10}에 %연회장표시()%에서, 「%EnkaiTitle%」%조사만처리(EnkaiTitle,"를")% 연다고 한다

PRINT 들리는 이야기에 따르면
IF FLAG:31 >= 5000
	PRINTW ……이건 장기전이 될 것 같다
ELSEIF FLAG:31 >= 2500
	PRINTW , 진수성찬을 푸짐하게 준비하고 있는 것 같다
ELSEIF FLAG:31 >= 1500
	PRINTW , 친목을 다지는 데는 딱 좋을 것 같다
ELSE
	PRINTW , 조그만한 모임을 예정하고 있는 것 같다
ENDIF
PRINTFORMW \@ FLAG:개시일 == 1? 갑작스러운 행사지만, 참가해 볼까……# 참가하려면 예정을 비워 두자……\@
SIF FLAG:36 > 0
	PRINTFORMW 또한, 비가 내리면 취소 된다고 한다……
FLAG:개시일 += DAY


@연회참가인원수재계산
;연회의 참가인수를 세트
FLAG:참가인수 = 0
FOR LOCAL, 0, CHARANUM
	IF (CFLAG:LOCAL:연회참가 == 1 || CFLAG:LOCAL:연회참가 == 2) && !TCVAR:LOCAL:만취
		SIF CFLAG:LOCAL:현재위치 == TFLAG:연회장
			FLAG:참가인수 ++
	ENDIF
NEXT
