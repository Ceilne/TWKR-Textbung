@암시장조건
#FUNCTION

RETURNF 1

IF ABL:MASTER:교양 < 2||ABL:MASTER:전투능력 < 3
    RETURNF 1
ENDIF
RETURNF 0

@암시장
PRINTFORML "어서와, 찾는건 뭐야?"
$암시장루프
DRAWLINE
PRINTFORML [100] - 구매
PRINTFORML [101] - 판매
PRINTFORML [102] - 의뢰
DRAWLINE
PRINTFORML [900] - 돌아간다
$LOOP
INPUT
IF RESULT == 900
    RETURN 1
ELSE
    SELECTCASE RESULT
        CASE 100
            CALL 암시장구매
        CASE 101
            CALL 암시장판매
        CASE 102
            CALL 암시장의뢰
        CASEELSE
            GOTO LOOP
    ENDSELECT
ENDIF
GOTO 암시장루프

;물건 리스트 정하는 식
;1~30 식재
;31~50 음란한 물건
;51~70 약물류
;71~90 밀주
;91~100 무기

;1000,고기,1000
;1001,간,5000
;1002,뼈,1500
;1003,영혼조각,50000
;1004,요정의날개,2500
;1005,머리카락,500
;1006,뿔,1250
;1007,토끼귀,3000
;1008,날개,7000
;1009,마력의파편,3500
;1010,맥주,5000
;1011,위스키,10000
;1012,브랜디,12000
;1013,와인,15000
;1014,보드카,14500
;1015,스피리터스,15500
;1016,벌꿀주,17000
;1017,고량주,14000
;1018,소주,7000
;1019,발포주,1500
;1020,대마,5000,;떨 한대 할래?
;1021,양귀비,5000
;1022,환각버섯,3500
;1023,필로폰,10000
;1024,엑스터시,15000
;1025,해어진부적,2500
;1026,녹슨봉마침,4500
;1027,불발된탄막,8000
;1028,낡은불제봉,10000
;1029,온전한영격찰,20000
@암시장구매
#DIM DYNAMIC 구매품
#DIM DYNAMIC 구매개수
PRINTFORML "뭘 사고 싶어?"
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 30
    PRINTFORMLC [{LOCAL}] %ITEMNAME:(LOCAL+1000)%
    PRINTFORMLC (\\{((ITEMPRICE:(LOCAL+1000)*암시장물가)/100)})
    LOCAL:1 ++
    IF LOCAL:1 % 4 == 0&&LOCAL:1 != 0
        PRINTL
    ENDIF
NEXT
PRINTL
DRAWLINE
PRINTFORML [999]돌아간다
$LOOP
INPUT
IF RESULT == 999
    RETURN 1
ELSE
    IF RESULT < 0||RESULT > 29
        GOTO LOOP
    ELSE
        구매품 = RESULT + 1000
        IF MONEY < ((ITEMPRICE:(RESULT+1000)*암시장물가)/100)
            PRINTW 돈이 부족하다...
        ELSE
            PRINTFORML 몇개나?(0 이하 입력으로 취소)
            INPUT
            IF RESULT <= 0
                RESTART
            ELSE
                구매개수 = RESULT
                IF MONEY < ((ITEMPRICE:구매품*암시장물가)/100)*구매개수
                    FOR LOCAL,0,구매개수
                        IF MONEY >= ((ITEMPRICE:구매품*암시장물가)/100)*(구매개수-LOCAL)
                            BREAK
                        ENDIF
                    NEXT
                    MONEY -= ((ITEMPRICE:구매품*암시장물가)/100)*(구매개수-LOCAL)
                    ITEM:구매품 += (구매개수-LOCAL)
                    PRINTFORML %조사처리(ITEMNAME:구매품,"을")% {(구매개수-LOCAL)}개 얻었다
                ELSE
                    MONEY -= ((ITEMPRICE:구매품*암시장물가)/100)*구매개수
                    ITEM:구매품 += 구매개수
                    PRINTFORML %조사처리(ITEMNAME:구매품,"을")% {구매개수}개 얻었다
                ENDIF
                
            ENDIF
        ENDIF
    ENDIF
ENDIF
RESTART


@암시장판매
#DIM DYNAMIC 판매품
#DIM DYNAMIC 판매개수
#DIM DYNAMIC 판매물가
판매물가 = 암시장물가 / 2
PRINTFORML "뭘 팔고 싶어?"
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 50
    IF ITEM:(LOCAL+1000) > 0
        PRINTFORMLC [{LOCAL}] %ITEMNAME:(LOCAL+1000)% * {ITEM:(LOCAL+1000)}
        PRINTFORMLC (\\{((ITEMPRICE:(LOCAL+1000)*판매물가)/100)})
        LOCAL:1 ++
        IF LOCAL:1 % 4 == 0&&LOCAL:1 != 0
            PRINTL
        ENDIF
    ENDIF
NEXT
PRINTL
PRINTL
DRAWLINE
PRINTFORML [999]돌아간다
$LOOP
INPUT
IF RESULT == 999
    RETURN 1
ELSE
    IF RESULT < 0||RESULT > 29
        GOTO LOOP
    ELSE
        판매품 = RESULT + 1000
        IF ITEM:판매품 == 0||판매품 < 1000||판매품 > 1029
        ELSE
            PRINTFORML 몇개나?(0 이하 입력으로 취소)
            INPUT
            IF RESULT <= 0
                RESTART
            ELSE
                판매개수 = MIN(RESULT,ITEM:(판매품))
                MONEY += ((ITEMPRICE:판매품*판매물가)/100)*판매개수
                ITEM:(판매품) -= 판매개수
                PRINTFORML %조사처리(ITEMNAME:판매품,"을")% {판매개수}개 팔아, {((ITEMPRICE:판매품*판매물가)/100)*판매개수}원 받았다
            ENDIF
        ENDIF
    ENDIF
ENDIF
RESTART

@암시장의뢰
PRINTFORML "뭘 의뢰할거야?"(소지금 : {MONEY})
DRAWLINE
PRINTFORML [100] - 소문을 퍼뜨려줘(\\{물가적용(20000)})
PRINTFORML [101] - 영원정의 약 저장고를 털어줘(\\{물가적용(100000)})
PRINTFORML [102] - 요즘 누가 괴롭혀서(\\물가적용(10000))
PRINTFORML [103] - 창관에서 한 명 데려와줘(\\물가적용(250000))
DRAWLINE
PRINTFORML [900] - 돌아간다
$LOOP
INPUT
IF RESULT == 900
    RETURN 1
ELSE
    SELECTCASE RESULT
        CASE 100
            IF MONEY >= 물가적용(20000)
                IF 소문일일횟수 <= 0
                    PRINTFORMW "아무리 그래도 하루에 몇번이고 소문내면 효과없다고?"
                    RESTART
                ENDIF
                PRINTFORML "무슨 소문을 퍼뜨려줄까?"
                PRINTFORML [1] - 나에 대한 미담!(전원의 호감/신뢰도 상승)
                PRINTFORML [2] - 카리스마 폭락 예고!(물가 변동폭이 상승함)
                PRINTFORML [9] - 돌아간다
                $소문루프
                INPUT
                IF RESULT == 9

                ELSE
                    SELECTCASE RESULT
                        CASE 1
                            MONEY -= 물가적용(20000)
                            PRINTFORML "오케이, 그쯤이야 쉽지"
                            PRINTFORMW "금방 퍼뜨려줄게...하지만 꼭 성공한다는 보장은 없다?"
                            PRINTW ....
                            IF RAND:10 == 0
                                PRINTFORML "...열심히 한다곤 했는데, 잘 됐는지는 모르겠어, 사람들이 워낙 들으려 들질 않으니 원..."
                                CALL 전원호감도하락(25,0)
                                CALL 전원신뢰도하락(5,0)
                            ELSE
                                PRINTFORML "은근히 솔깃해 하는걸 보니 그럭저럭 들어준거 같아"
                                CALL 전원호감도상승(50,0)
                                CALL 전원신뢰도상승(10,0)
                            ENDIF
                            소문일일횟수--
                        CASE 2
                            MONEY -= 물가적용(20000)
                            PRINTFORML "으으으음...알았어, 해 볼게"
                            PRINTFORML "다만...이거 결과가 당신한테 좋게만 될거라곤 믿지마, 어찌될진 나도 몰라"
                            FLAG:물가변동폭 += 10
                            소문일일횟수--
                        CASEELSE
                            GOTO 소문루프
                    ENDSELECT
                ENDIF
            ELSE
                PRINTW "돈이 모자란걸?"
                RESTART
            ENDIF
        CASE 101
            IF MONEY >= 물가적용(100000)
                MONEY -= 물가적용(100000)
                PRINTFORML "음...약 저장소를...? 알았어, 오늘 밤 까지는 해결해 둘게"
                PRINTFORML "칫...거긴 갔다간 몸에 이상한게 돋아날것만 같은데 말이지..."
                FLAG:영원정폭발 = 1
            ELSE
                PRINTW "돈이 모자란걸?"
                RESTART
            ENDIF
        CASE 102
            IF MONEY >= 물가적용(10000)
                PRINTFORML "알았어, 누굴 손봐줄까?"
                FOR LOCAL:1, 1, CHARANUM
                    IF CFLAG:(LOCAL:1):주도도 > 0&&TALENT:(LOCAL:1):사망 == 0
                        PRINTFORML [{LOCAL:1}] %CALLNAME:(LOCAL:1)%
                    ENDIF
                NEXT
                PRINTFORML [999] 취소
                $혼내줘
                INPUT
                IF RESULT == 999
                    RETURN 1
                ELSE
                    IF (RESULT > CHARANUM||RESULT < 0)
                        GOTO 혼내줘
                    ELSE
                        SIF (CFLAG:RESULT:주도도 == 0||TALENT:RESULT:사망 == 1)
                            GOTO 혼내줘
                        MONEY -= 물가적용(10000)
                        PRINTFORML "오케이, 지금 바로 갈게"
                        PRINTFORMW ....
                        PRINTFORML "...후우...힘들었지만, 어떻게든 했어...앞으론 덜 괴롭힐거야, 아마도"
                        PRINTFORML "...해결 안되면 다음에 또 이용해줘?"
                        CALL 노말라이제이션(RESULT, 20)
                    ENDIF
                ENDIF
            ELSE
                PRINTW "돈이 모자란걸?"
                RESTART
            ENDIF
        CASE 103
            IF MONEY >= 물가적용(250000)
                PRINTFORML "...뭐어? 거기서 누굴 데려오라고...?"
                PRINTFORML "...거기가 얼마나 무시무시한 곳인데 거길 가라고...하...정말이지..."
                PRINTFORML "...알았어, 갈게, 간다고...그래서, 누굴 데려와?"
                FOR LOCAL, 1, CHARANUM
                    IF TALENT:LOCAL:창녀
                        PRINTFORML [{LOCAL:1}] %CALLNAME:(LOCAL:1)%
                    ENDIF
                NEXT
                PRINTFORML [999] 취소
                $찾아줘
                INPUT
                IF RESULT == 999
                    RETURN 1
                ELSE
                    IF (RESULT > CHARANUM||RESULT < 0)
                        GOTO 찾아줘
                    ELSE
                        SIF !TALENT:RESULT:창녀
                            GOTO 찾아줘
                        MONEY -= 물가적용(250000)
                        PRINTFORMW "알았어, 오늘 밤에 갔다 올게"
                        PRINTFORMW "...하아...힘든 밤이 되겠네..."
                        CFLAG:RESULT:구출작전 = 1
                    ENDIF
                ENDIF
            ELSE
                PRINTW "돈이 모자란걸?"
                RESTART
            ENDIF
        CASEELSE
            GOTO LOOP
    ENDSELECT
ENDIF
RESTART

@암시장물가책정
IF 암시장물가 == 0
    암시장물가 = 100
ENDIF
암시장물가 += (RAND:31 - 15)
IF 암시장물가 < 50
    암시장물가 = 50
ELSEIF 암시장물가 > 1000
    암시장물가 = 1000
ENDIF