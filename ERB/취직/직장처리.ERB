@직장처리
#DIM DYNAMIC 급여 ; 만원단위
#DIM DYNAMIC 요구일량 ; 한달 단위로 요구
#DIMS DYNAMIC 직장명
#DIM DYNAMIC 가능여부
SIF !FLAG:직장정보
    RETURN 1
CALL 직장상세(FLAG:직장정보)
급여 = RESULT:1
요구일량 = RESULT:2
직장명 += RESULTS
PRINTFORML 오늘은 월급날이다...
IF DAY > FLAG:직장시작일 + 31
    ;평소
    PRINTFORML 요구일량:{요구일량} / 한 일:{FLAG:직장일량}
    IF 요구일량 <= FLAG:직장일량
        ;성공
        MONEY += 급여 * 10000
        PRINTFORML {급여*10000}원의 봉급을 받았다...
    ELSE
        ;실패
        FLAG:직장실패++
        PRINTFORML 요구사항을 맞추지 못했다...
    ENDIF 
ELSE
    ;첫달
    PRINTFORML 요구일량:{요구일량} / 한 일:{FLAG:직장일량}
    IF 요구일량 <= FLAG:직장일량
        ;성공
        MONEY += 급여 * 10000
        PRINTFORML {급여*10000}원의 봉급을 받았다...
    ELSEIF 요구일량/2 <= FLAG:직장일량
        ;성공
        MONEY += 급여 * 5000
        PRINTFORML 첫 달이라 반절만 일했으므로 {급여*5000}원의 봉급을 받았다...
    ELSE
        ;실패
        FLAG:직장실패++
        PRINTFORML 요구사항을 맞추지 못했다...
    ENDIF 
ENDIF
IF FLAG:직장실패 > 3
    PRINTFORMW 거듭된 실패에 결국 쫓겨나고 말았다...
    CALL 백수화
ENDIF
PRINTW
FLAG:직장일량 = 0

@백수화
실직정보:0 = FLAG:직장정보
실직정보:1 = DAY
FLAG:직장정보 = 0
FLAG:직장시작일 = 0
FLAG:직장일량 = 0
FLAG:직장위치1 = 0
FLAG:직장위치2 = 0
FLAG:직장지역 = 0
FLAG:직장실패 = 0