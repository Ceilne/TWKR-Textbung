@요리재료표시함수(ARG)
IF ARG <= 0
    RETURN -1
ENDIF
IF ITEM:ARG <= 0
    SETCOLOR 0x808080
ENDIF
PRINTFORMLC [{ARG, 4}] %ITEMNAME:ARG% x{ITEM:ARG - 사용된재료:ARG}

RESETCOLOR
RETURN 1

@요리하기
#DIM 표시되는재료, 100
VARSET 사용된재료
VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
VARSET 주재료
VARSET 주재료요리법
VARSET 부재료
VARSET 부재료요리법
VARSET 양념

표시되는재료:0 = GETNUM(ITEM,"잡곡")
표시되는재료:1 = GETNUM(ITEM,"묵은쌀")
표시되는재료:2 = GETNUM(ITEM,"백미")
표시되는재료:3 = GETNUM(ITEM,"고기")
표시되는재료:4 = GETNUM(ITEM,"물고기")
표시되는재료:5 = GETNUM(ITEM,"맛있는물고기")
표시되는재료:6 = GETNUM(ITEM,"보통버섯")
표시되는재료:7 = GETNUM(ITEM,"간")
표시되는재료:8 = GETNUM(ITEM,"당근")
표시되는재료:9 = GETNUM(ITEM,"순무")
표시되는재료:10 = GETNUM(ITEM,"죽순")
표시되는재료:11 = GETNUM(ITEM,"아이스크림")

PRINTFORML 어떤 메인 재료를 사용할까?
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 돌아간다

WHILE 1
    INPUT
    IF RESULT == 9999 ;돌아간다
        RETURN -1
    ELSEIF RESULT == 0 ;선택 대상 없음. 로터를 고른다면 또 모를까.
        CONTINUE
    ENDIF

    FOR LOCAL, 0, 100
        IF 표시되는재료:LOCAL == -1 ; 표시되는재료 배열 공란 발견시
            RETURN 0
        ELSEIF RESULT == 표시되는재료:LOCAL && ITEM:RESULT > 0
            사용된재료:RESULT ++
            주재료 = RESULT
            CALL 요리법선택
            주재료요리법 = RESULT
            CALL 서브재료선택
            CALL 양념선택
            CALL MAKECOOK
            RETURN 1
        ENDIF
    NEXT
WEND

@요리법선택
PRINTL 어떻게 요리할까...?
DRAWLINE
PRINTL [001] 굽기
PRINTL [002] 삶기
PRINTL [003] 튀기기
PRINTL [004] 끓이기
PRINTL [005] 찌기
PRINTL [006] 조리기
PRINTL [007] 볶기
PRINTL [008] 그대로
DRAWLINE
WHILE 1
    INPUT
    IF INRANGE(RESULT, 1, 8)
        RETURN RESULT
    ELSE
        PRINTW 잘못된 입력입니다.
    ENDIF
WEND

@서브재료선택
#DIM DYNAMIC 첨가횟수
#DIM 표시되는재료, 100
VARSET 표시되는재료
표시되는재료:0 = GETNUM(ITEM,"가지")
표시되는재료:1 = GETNUM(ITEM,"파")
표시되는재료:2 = GETNUM(ITEM,"순무")
표시되는재료:3 = GETNUM(ITEM,"당근")
표시되는재료:4 = GETNUM(ITEM,"고기")
표시되는재료:5 = GETNUM(ITEM,"약초")
표시되는재료:6 = GETNUM(ITEM,"고려인삼")
표시되는재료:7 = GETNUM(ITEM,"간")
표시되는재료:8 = GETNUM(ITEM,"양배추")
표시되는재료:9 = GETNUM(ITEM,"참마")
표시되는재료:9 = GETNUM(ITEM,"오이")
표시되는재료:9 = GETNUM(ITEM,"토마토")
$LOOP
IF 첨가횟수 >= 9
    RETURN 0
ENDIF
PRINTL
DRAWLINE
PRINTL 추가로 뭘 넣을까?
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 이대로 끝
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 0
ELSE
    FOR LOCAL, 0, 100
         IF RESULT == 표시되는재료:LOCAL&&RESULT != 0
            IF ITEM:RESULT > 0
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    GOTO LOOP1
    $MAKE
    사용된재료:RESULT ++
    부재료:첨가횟수 = RESULT
    CALL 요리법선택
    부재료요리법:첨가횟수 = RESULT
    첨가횟수++
ENDIF
GOTO LOOP

@양념선택
#DIM DYNAMIC 첨가횟수
#DIM 표시되는재료, 100
VARSET 표시되는재료
표시되는재료:0 = GETNUM(ITEM,"벌꿀")
표시되는재료:1 = GETNUM(ITEM,"소주")
표시되는재료:2 = GETNUM(ITEM,"맥주")
표시되는재료:3 = GETNUM(ITEM,"와인")

$LOOP
IF 첨가횟수 >= 10
    RETURN
ENDIF
PRINTL 무슨 양념?
DRAWLINE
PRINTL 기본 양념
DRAWLINE
PRINTLC [0001] 소금
PRINTLC [0002] 후추
PRINTLC [0003] 설탕
PRINTLC [0004] 식초
PRINTL
PRINTLC [0005] 된장
PRINTLC [0006] 기름
PRINTLC [0007] 간장
PRINTLC [0008] 고춧가루
PRINTL
PRINTLC [0009] 젓갈
PRINTL
DRAWLINE
PRINTL 고급 양념
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
DRAWLINE
PRINTL [9999] 이대로 끝
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 0
ELSE
    IF RESULT > 0&&RESULT < 10
        GOTO MAKE
    ENDIF
    FOR LOCAL, 0, 100
         IF RESULT == 표시되는재료:LOCAL&&RESULT != 0
            IF ITEM:RESULT > 0
                사용된재료:RESULT ++
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    GOTO LOOP1
    $MAKE
    양념:첨가횟수 = RESULT
    첨가횟수++
ENDIF
GOTO LOOP


@MAKECOOK
#DIM DYNAMIC 레시피있음
PRINTL
PRINTL
PRINTL
DRAWLINE
PRINTL 조리중
DRAWLINE
;===============
; PHASE 0 - 재료를 삭제
;===============
ITEM:(주재료) --
FOR LOCAL, 0 ,10
    SIF 부재료:LOCAL
        ITEM:(부재료:LOCAL) --
    SIF 양념:LOCAL > 10
        ITEM:(양념:LOCAL) --
NEXT
;===============
; PHASE 1 - 레시피를 읽어와 일치하는게 있는지 확인, 없다면 그대로 창작요리- 이름 책정
;===============
VARSET 요리이름
FOR LOCAL,0,1000
    TRYCALLFORM 요리레시피{LOCAL}
    IF RESULT == 1
        TRYCALLFORM 요리명{LOCAL}
        요리이름 = %RESULTS%
        PRINTFORML %조사처리(RESULTS,"을")% 만들었다!
        레시피있음 = LOCAL
        BREAK
    ENDIF
NEXT
IF 레시피있음 == 0
    PRINTL 이 요리의 이름은?
    PRINTL [0] 자동 지정
    PRINTL [1] 내가 짓겠어!
    INPUT
    IF RESULT == 0
        CALL 요리명책정
    ELSE
        PRINT 요리의 이름을 입력해주세요:
        INPUTS
    ENDIF
    요리이름 = %RESULTS%
ENDIF
;===============
; PHASE 2 - 영양정보 확인
;===============
VARSET 요리영양소
CALL 영양소표(주재료)
CALL 영양소올리기(RESULT:0,RESULT:1,RESULT:2,RESULT:3,RESULT:4)
FOR LOCAL,0 ,10
    CALL 영양소표(부재료:LOCAL)
    CALL 영양소올리기(RESULT:0,RESULT:1,RESULT:2,RESULT:3,RESULT:4)
NEXT
FOR LOCAL,0 ,10
    CALL 영양소표(양념:LOCAL)
    CALL 영양소올리기(RESULT:0,RESULT:1,RESULT:2,RESULT:3,RESULT:4)
NEXT

;===============
; PHASE 3 - 맛 책정 안되있으면 자동으로
;===============
IF 레시피있음 == 0
    $맛책정
    DISHTASTE += "주식/"
    DISHTASTE += 요리이름 + "/"
    IF 재료첨가여부("주재료",GETNUM(ITEM,"잡곡"))
        DISHTASTE += "야취/"
    ENDIF
    IF 재료첨가여부("주재료",GETNUM(ITEM,"토마토"))
        DISHTASTE += "토마토/"
    ENDIF
    IF 재료첨가여부("양념",8)
        DISHTASTE += "스파이스/"
    ENDIF
    IF 재료첨가여부("주재료",GETNUM(ITEM,"잡곡"))||재료첨가여부("주재료",GETNUM(ITEM,"묵은쌀"))||재료첨가여부("주재료",GETNUM(ITEM,"백미"))
        DISHTASTE += "쌀/"
    ENDIF
    IF 재료첨가여부("주재료",GETNUM(ITEM,"고기"))||재료첨가여부("주재료",GETNUM(ITEM,"간"))
        DISHTASTE += "고기 요리/"
    ENDIF
    IF 재료첨가여부("주재료",GETNUM(ITEM,"물고기"),Y_그대로)||재료첨가여부("주재료",GETNUM(ITEM,"맛있는물고기"),Y_그대로)
        DISHTASTE += "품위있는/"
        DISHTASTE += "심플/"
    ENDIF
    IF 주재료요리법 == Y_튀기기
        DISHTASTE += "튀김요리/"
        DISHTASTE += "기름/"
    ENDIF
    IF 주재료요리법 == Y_볶기
        DISHTASTE += "기름/"
    ENDIF
    IF 주재료요리법 == Y_찌기||주재료요리법 == Y_끓이기||주재료요리법 == Y_조리기
        DISHTASTE += "농후/"
    ENDIF
    IF 주재료요리법 == Y_끓이기
        DISHTASTE += "국물/"
    ENDIF
    IF 주재료요리법 == Y_삶기
        DISHTASTE += "물기/"
    ENDIF
    IF 요리법포함여부(Y_그대로)&&!요리법포함여부(Y_볶기)&&!요리법포함여부(Y_튀기기)
        DISHTASTE += "산뜻한/"
    ENDIF
    DISHTASTE += "수수께끼 요리/"
ELSE
    CALLFORM 요리맛책정{레시피있음}
    IF RESULTS == ""
        GOTO 맛책정
    ENDIF
    DISHTASTE = RESULTS
ENDIF
DISHNAME = %요리이름%
DISHTYPE = 2
DISHQUALITY = 3

;
; PHASE 4 경험증가
;

EXP:MASTER:요리경험 += 2
SIF TARGET
    EXP:TARGET:요리경험 += 2
@요리명책정
#DIMS DYNAMIC 이름버퍼
;일단 가라로


IF 주재료 == GETNUM(ITEM,"고기")
    이름버퍼 += "고기 요리"
ELSEIF 주재료 == GETNUM(ITEM,"물고기")||주재료 == GETNUM(ITEM,"맛있는물고기")
    이름버퍼 += "물고기 요리"
ELSEIF (주재료 == GETNUM(ITEM,"잡곡")||주재료 == GETNUM(ITEM,"묵은쌀")||주재료 == GETNUM(ITEM,"백미"))&&주재료요리법 == Y_찌기
    이름버퍼 += "밥"
ENDIF


IF 이름버퍼 == ""
    이름버퍼 += "수수께끼 요리"
ENDIF
VARSET RESULTS
RESULTS = %이름버퍼%
RETURN

@요리법포함여부(ARG)
#FUNCTION
IF 주재료요리법 == ARG
    RETURNF 1
ELSE
    FOR LOCAL,0,10
        IF 부재료요리법:LOCAL == ARG
            RETURNF 1
        ENDIF
    NEXT
ENDIF
RETURNF 0

@재료첨가여부(ARGS,ARG,ARG:1 = 0)
#FUNCTION
;대상 재료를 넣었는지 안 넣었는지
IF ARGS == "모두"
    IF 주재료 == ARG&&(ARG:1 == 0 || ARG:1 == 주재료요리법)
        RETURNF 1
    ELSE
        FOR LOCAL, 0, 10
            IF 부재료:LOCAL == ARG&&(ARG:1 == 0 || ARG:1 == 부재료요리법:LOCAL)
                RETURNF 1
            ENDIF
            IF 양념:LOCAL == ARG
                RETURNF 1
            ENDIF
        NEXT
    ENDIF
    RETURNF 0
ELSEIF ARGS == "주재료"
    IF 주재료 == ARG&&(ARG:1 == 0 || ARG:1 == 주재료요리법)
        RETURNF 1
    ELSE
        RETURNF 0
    ENDIF
ELSEIF ARGS == "부재료"
    FOR LOCAL, 0, 10
        IF ARG == -1&&부재료:LOCAL != 0
            RETURNF 0
        ENDIF
        IF 부재료:LOCAL == ARG&&(ARG:1 == 0 || ARG:1 == 부재료요리법:LOCAL)
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0
ELSEIF ARGS == "양념"
    FOR LOCAL, 0, 10
        IF ARG == -1&&양념:LOCAL != 0
            RETURNF 0
        ENDIF
        IF 양념:LOCAL == ARG
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0
ENDIF

@영양소올리기(ARG:0,ARG:1,ARG:2,ARG:3,ARG:4)
요리영양소:0 += ARG:0
요리영양소:1 += ARG:1
요리영양소:2 += ARG:2
요리영양소:3 += ARG:3
요리영양소:4 += ARG:4